*********************************************************************************
*										*
*	    ,o888888o.    888888888888.   88888888888888 88888888888888		*
*	   8888     `88.  888888    `88.  888888         888888         	*
*	,888888       `8. 888888     `88  888888         888888         	*
*	8888888           888888     ,88  888888         888888         	*
*	8888888           888888.   ,88'  88888888888888 88888888888888 	*
*	8888888           888888888888'   888888         888888         	*
*	8888888           888888`8b       888888         888888         	*
*	`888888       .8' 888888 `8b.     888888         888888         	*
*	   8888     ,88'  888888   `8b.   888888         888888         	*
*	    `8888888P'    888888     `88. 88888888888888 88888888888888		*
*										*
*********************************************************************************
*******************************************************************************
**	DISCLAIMER
*******************************************************************************
**	This model is provided as is, where is, and with no warranty of any kind
**	either expressed or implied, including but not limited to any implied 
**	warranties of merchantability and fitness for a particular purpose.
*******************************************************************************

***********************************************************
****	Cree SiC MOSFET CPM3-0900-0010A-die Spice Library 
****	Version 1.0 Date: 11-24-2016
****	Version 2.0 Date: 01-22-2018
***********************************************************
****	Revision record
****	Version 1	Initial Release
****	Version 2	This spice model is compatible to both Ltspice and Orcad Pspsice
*********************************************************************************
****Parasitics Excluded
****Tj = Junction Temperature
****Tc = Case Temperature
****D = Drain
****G = Gate
****S = Source
***********************************************************

.subckt CPM3-0900-0010A d g s Tj 

xgmos	d3 d1 g1 s1 Tj gmos_CPM3-0900-0010A
Ls 		s	s1	1f
R_Ls 		s	s1      10
Rg 		g1	g2	1.6
Lg 		g	g3	1f
R_Lg		g	g3	20
Ld 		d	d3      1f
R_Ld		d	d3	10

G1		d2	d1      Value {V(d1,s1)}
R_A		d2	d1	1E16
vgate_s		g3	g2	0
vdrain_s	d3	d2	0
Gheat		0	Tj	value {abs((V(d,s)*I(Vdrain_s)))+abs((V(g1,g2)*I(Vgate_s)))}
xCGD		d2	g1	cgdmos_CPM3-0900-0010A 
CGS		g1	s1	4448p
D1		s1	d2	bodydiode_CPM3-0900-0010A



.ends CPM3-0900-0010A

*****************************************

.subckt gmos_CPM3-0900-0010A d3 d1 g1 s1 Tj

e3		NET3	0	Value {0.00001*(V(Tj)**2)-0.0073*V(Tj)+2.5308}
R_C		NET3	0 	1E6

e2		NET2	0	Value {0.035*v(g2,s1)+0.175}
R_B		NET2	0 	1E6

e4		NET4	0	value {
+				if(V(Tj)<60,
+					0.7
+					,
+					0.3
+					)
+					}    
R_d		NET4	0 	1E6

e5		NET5	0	value {
+				if(V(g2,s1)>13,
+					-3u*(V(Tj)**2)-0.0006*v(Tj)+0.4578   
+					,
+					0
+					)
+					}
R_e		NET5	0 	1E6

e6		NET6	0	value {
+				if(V(g2,s1)>11 & V(g2,s1)<=13,
+					-5u*(V(Tj)**2)-0.0005*v(Tj)+0.6285
+					,
+					0
+					)
+					}
R_f		NET6	0 	1E6

e7		NET7	0	value {
+				if(V(g2,s1)>9 & V(g2,s1)<=11,
+					-7u*(V(Tj)**2)-0.0002*v(Tj)+0.86
+					,
+					0
+					)
+					}
R_g		NET7	0 	1E6

e8		NET8	0	value {
+				if(V(g2,s1)>7 & V(g2,s1)<=9,
+					-8u*(V(Tj)**2)+0.0008*v(Tj)+1.0354    
+					,
+					0
+					)
+					}
R_h		NET8	0 	1E6

e9		NET9	0	value {
+				if(V(g2,s1)>1 & V(g2,s1)<=7,
+					-20u*(V(Tj)**2)+0.0033*v(Tj)+1.27   
+					,
+					0
+					)
+					}
R_j		NET9	0 	1E6

e1		NET1	0	value {
+				if(V(Tj)<60,
+					0.45
+					,
+					0
+					)
+					}    
R_k		NET1	0 	1E6


.param p8  = 0.0011
.param p11 = -8
.param p12 = 19

R100 g2 s1	1E6
E100 g2 s1 	value {limit(V(g1,s1),p11,p12)} 


********************************
G1 d1 s1 value {
+	if(V(s1,d3)<0,
+		0
+		,
+		if (V(g2,s1)<V(NET3,0) ,
+		-((0.035)*(v(g2,s1)-V(NET3,0)))*(-(1+p8*v(s1,d3))*0.008)*(((log(1+exp(v(g2,s1)-V(NET3,0))))**2)-
+		((log(1+exp(v(g2,s1)-V(NET3,0)-(0.854*v(s1,d3)))))**2))
+		,
+		-(((v(NET5,0)+v(NET6,0)+v(NET7,0)+v(NET8,0)+v(NET9,0))*(v(g2,s1)-V(NET3,0)))+v(NET4,0)+v(NET1,0))*(1+p8*v(s1,d3))*
+		(((log(1+exp(v(g2,s1)-V(NET3,0))))**2)-((log(1+exp(v(g2,s1)-V(NET3,0)-(V(NET2,0)*v(s1,d3)))))**2))
+		)
+			)
+			}
G2 d1 s1 value {
+	if(V(d3,s1)<0,
+		0
+		,
+		if (V(g2,s1)<V(NET3,0) ,
+		((0.035)*(v(g2,s1)-V(NET3,0)))*(-(1+p8*v(d3,s1))*0.008)*(((log(1+exp(v(g2,s1)-V(NET3,0))))**2)-
+		((log(1+exp(v(g2,s1)-V(NET3,0)-(0.854*v(d3,s1)))))**2))
+		,
+		(((v(NET5,0)+v(NET6,0)+v(NET7,0)+v(NET8,0)+v(NET9,0))*(v(g2,s1)-V(NET3,0)))+V(NET4,0))*(1+p8*v(d3,s1))*
+		(((log(1+exp(v(g2,s1)-V(NET3,0))))**2)-((log(1+exp(v(g2,s1)-V(NET3,0)-(V(NET2,0)*v(d3,s1)))))**2))
+		)
+			)
+			}

.ends gmos_CPM3-0900-0010A

****************************************

.subckt cgdmos_CPM3-0900-0010A d2 g1
.param k1=1900p	
.param k2=0.49		
.param ka=35	
.param kb=0.3	
.param kc=6.5277	
G11 g1 d1 value {
+		k1*(
+		(1+(limit(v(d1,g1),0,600))*(1+ka*(1+TANH(kb*V(d1,g1)-kc))/2))**-k2
+		)*ddt(v(g1,d1))
+			}
R_CGD d1 d2 1e-4
.ends cgdmos_CPM3-0900-0010A


****************************************
.model bodydiode_CPM3-0900-0010A
+d(
+is=55.89n 
+cjo=7600p 
+bv=1160 
+m=0.68006 
+EG=5.42827 
+vj=5.0208436 
+n=7.55 
+rs=0.0065 
+trs1=89.5n  
+trs2=2.79u 
+Tnom=25
+tt=1n 
+ibv=500u 
+Xti=0.13 
+level=1
+)








