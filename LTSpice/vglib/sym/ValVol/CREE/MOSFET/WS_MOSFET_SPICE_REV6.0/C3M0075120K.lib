*********************************************************************************
*										*
*	    ,o888888o.    888888888888.   88888888888888 88888888888888		*
*	   8888     `88.  888888    `88.  888888         888888         	*
*	,888888       `8. 888888     `88  888888         888888         	*
*	8888888           888888     ,88  888888         888888         	*
*	8888888           888888.   ,88'  88888888888888 88888888888888 	*
*	8888888           888888888888'   888888         888888         	*
*	8888888           888888`8b       888888         888888         	*
*	`888888       .8' 888888 `8b.     888888         888888         	*
*	   8888     ,88'  888888   `8b.   888888         888888         	*
*	    `8888888P'    888888     `88. 88888888888888 88888888888888		*
*										*
*********************************************************************************
*******************************************************************************
**	DISCLAIMER
*******************************************************************************
**	This model is provided as is, where is, and with no warranty of any kind
**	either expressed or implied, including but not limited to any implied 
**	warranties of merchantability and fitness for a particular purpose.
*******************************************************************************

***********************************************************
****	Cree SiC MOSFET C3M0075120K Spice Library 
****	Version 2.0 Date: 11-27-2017
****	Version 3.0 Date: 01-22-2018
****	Version 4.0 Date: 06-19-2018
***********************************************************
****	Revision record
****	Version 1	Initial Release
****	Version 2	Include Tc at gmos subcircuit
****	Version 3	This spice model is compatible to both Ltspice and Orcad Pspsice
****	Version 4	Update the thermal RC model
***********************************************************
****Parasitics Included
****Tj = Junction Temperature
****Tc = Case Temperature
****D = Drain
****G = Gate
****S1 = Kelvin Source
****S2 = Power Source
***********************************************************

.subckt C3M0075120K d g s1 s2 Tj Tc 

xgmos	d3 d1 g1 s Tj Tc gmos_C3M0075120K
RS1		s1	sb	24.88m
Ls1 		sb	s	7.574n
R_Ls1 		sb	s       20
RS2		s2	sa	3.322m
Ls2 		sa	s	3.435n
R_Ls2 		sa	s      	20
R_g 		g1	g2	10.5
RG		g	ga	37.85m
Lg 		ga	g3	11.364n
R_Lg		ga	g3	20
Rd		d	da	87.42u
Ld 		da	d3      4.366n
R_Ld		da	d3	2

G1		d2	d1      Value {V(d1,s)}
R_A		d2	d1	1E6
vgate_s		g3	g2	0
vdrain_s	d3	d2	0
Gheat		0	Tj	value {abs((V(d,s1)*I(Vdrain_s)))+abs((V(g1,g2)*I(Vgate_s)))}
xCGD		d2	g1	cgdmos_C3M0075120K 
CGS		g1	s	1347p
D1		s	d2	bodydiode_C3M0075120K
*Rba		s	va	0.07
*Rbb		va	vb	0.01
*Rbc		Vb	d2	0.05
*Rbd		vb	d2	0.05

R0 N1 Tj 24.985m
R1 N2 N1 59.17m
R2 N3 N2 498.45m
R3 Tc N3 468.96m
C0 Tj 0 396.233u
C1 N1 0 1.22m
C2 N2 0 3.926m
C3 N3 0 355.24m

.ends C3M0075120K

*****************************************

.subckt gmos_C3M0075120K d3 d1 g1 s Tj Tc

e3		NET3	0	Value {20u*(V(Tj)**2)-0.0082*V(Tj)+2.7086}
R_C		NET3	0 	1E6

e2		NET2	0	Value {13.39m*(V(g2,s)**2)-289.14m*v(g2,s)+2.26}
R_B		NET2	0 	1E6

e4		NET4	0	value {
+				if(V(d3,s)<0,
+				-8.12195u*(V(Tj)**2)+381.34u*v(Tj)+145.54m
+				,
+				-4.639u*(V(Tj)**2)+635.829u*v(Tj)+49m
+				)
+				}
R_d		NET4	0 	1E6

e5		NET5	0	value {
+				if(V(g2,s)>13,
+					-136.585n*(V(Tj)**2)-104.0975u*v(Tj)+52.687m    
+					,
+					0
+					)
+					}
R_e		NET5	0 	1E6

e6		NET6	0	value {
+				if(V(g2,s)>11 & V(g2,s)<=13,
+					-214.634n*(V(Tj)**2)-106.439u*v(Tj)+74.795m
+					,
+					0
+					)
+					}
R_f		NET6	0 	1E6

e7		NET7	0	value {
+				if(V(g2,s)>9 & V(g2,s)<=11,
+					-500n*(V(Tj)**2)+47.5u*v(Tj)+84.125m
+					,
+					0
+					)
+					}
R_g		NET7	0 	1E6

e8		NET8	0	value {
+				if(V(g2,s)>7 & V(g2,s)<=9,
+					-85.365n*(V(Tj)**2)+134.939u*v(Tj)+77.6798m    
+					,
+					0
+					)
+					}
R_h		NET8	0 	1E6

e9		NET9	0	value {
+				if(V(g2,s)>1 & V(g2,s)<=7,
+					273.17n*(V(Tj)**2)+208.195u*v(Tj)+60.624m   
+					,
+					0
+					)
+					}
R_j		NET9	0 	1E6



.param p8  = 0.0011
.param p11 = -8
.param p12 = 19

R100 g2 s	1E6
E100 g2 s 	value {limit(V(g1,s),p11,p12)} 


********************************
G1 d1 s value {
+	if(V(s,d3)<0,
+		0
+		,
+		if (V(g2,s)<V(NET3,0) ,
+		-((0.05)*(v(g2,s)-V(NET3,0)))*(-(1+p8*v(s,d3))*0.008)*(((log(1+exp(v(g2,s)-V(NET3,0))))**2)-
+		((log(1+exp(v(g2,s)-V(NET3,0)-(0.854*v(s,d3)))))**2))
+		,
+		-(((v(NET5,0)+v(NET6,0)+v(NET7,0)+v(NET8,0)+v(NET9,0))*(v(g2,s)-V(NET3,0)))+v(NET4,0))*(1+p8*v(s,d3))*
+		(((log(1+exp(v(g2,s)-V(NET3,0))))**2)-((log(1+exp(v(g2,s)-V(NET3,0)-(V(NET2,0)*v(s,d3)))))**2))
+		)
+			)
+			}
G2 d1 s value {
+	if(V(d3,s)<0,
+		0
+		,
+		if (V(g2,s)<V(NET3,0) ,
+		((0.05)*(v(g2,s)-V(NET3,0)))*(-(1+p8*v(d3,s))*0.008)*(((log(1+exp(v(g2,s)-V(NET3,0))))**2)-
+		((log(1+exp(v(g2,s)-V(NET3,0)-(0.854*v(d3,s)))))**2))
+		,
+		(((v(NET5,0)+v(NET6,0)+v(NET7,0)+v(NET8,0)+v(NET9,0))*(v(g2,s)-V(NET3,0)))+v(NET4,0))*(1+p8*v(d3,s))*
+		(((log(1+exp(v(g2,s)-V(NET3,0))))**2)-((log(1+exp(v(g2,s)-V(NET3,0)-(V(NET2,0)*v(d3,s)))))**2))
+		)
+			)
+			}

.ends gmos_C3M0075120K

****************************************

.subckt cgdmos_C3M0075120K d2 g
.param k1=535p	
.param k2=0.538		
.param ka=20	
.param kb=0.5	
.param kc=6.5277	
G11 g d1 value {
+		k1*(
+		(1+(limit(v(d1,g),0,600))*(1+ka*(1+TANH(kb*V(d1,g)-kc))/2))**-k2
+		)*ddt(v(g,d1))
+			}
R_CGD d1 d2 1e-4
.ends cgdmos_C3M0075120K


****************************************
.model bodydiode_C3M0075120K d(is=158.89n cjo=1119p bv=1590 m=0.49 EG=2.22827 vj=1.6 n=5.05 
+	rs=0.056 trs1=1u  trs2=-5.5003u Tnom=25
+	tt=5n ibv=500u Xti=0.13 level=1)








