**************** Power Discrete MOSFET Electrical Circuit Model *****************
** Product Name: FCD9N60NTM
** 600V N-Channel MOSFET and D-PAK
** Model Type: BSIM3V3
**-------------------------------------------------------------------------------
.SUBCKT FCD9N60NTM 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5    DbodyMOD
Dbreak 5 11  DbreakMOD
Ebreak 11 7 17 7 600
Lgate 1 9    1.13e-9
Ldrain 2 5   1.44e-9
Lsource 3 7  8.43e-10
RLgate 1 9   1.1
RLdrain 2 5  1.4
RLsource 3 7 0.8
Rgate 9 6    0.5
It 7 17      1
Rbreak 17 7  RbreakMOD 1
.MODEL RbreakMOD RES (TC1=1.22e-3 TC2=-1.35e-6)
.MODEL DbodyMOD D (IS=2.80e-13  N=1      RS=9.08e-3   TRS1=2.6e-3 TRS2=3.2e-7
+ CJO=4.42e-9      M=0.74       VJ=0.64  TT=6.74e-7   XTI=3       EG=1.11)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1.0e-3 TRS2=1e-6)
Rdrain 5 16 RdrainMOD 0.33
.MODEL RdrainMOD RES (TC1=9.2e-3 TC2=2.62e-5)
M_BSIM3 16 6 7 7 Bsim3 W=1.29 L=2.0e-6 NRS=1
.MODEL  Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 PARAMCHK=1 NQSMOD=0
+ TOX=780e-10     XJ=1.4e-6      NCH=1.3e17
+ U0=700          VSAT=5.5e5     DROUT=1.0 
+ DELTA=0.1       PSCBE2=0       RSH=1.09e-3
+ VTH0=3.20       VOFF=-0.1      NFACTOR=1.1
+ LINT=1.4e-7     DLC=1.4e-7     FC=0.5
+ CGSO=1.8e-12    CGSL=0         CGDO=1.65e-12  
+ CGDL=4.92e-10   CJ=0           CF=0
+ CKAPPA=0.2      KT1=-1.64      KT2=0
+ UA1=-1.5e-9     NJ=10 )
.ENDS

******************** Power Discrete MOSFET Thermal Model ************************
** 600V N-Channel MOSFET and D-PAK
**-------------------------------------------------------------------------------
.SUBCKT FCD9N60NTM_Thermal TH TL
CTHERM1 TH 6 1.64e-5
CTHERM2 6 5  7.04e-4
CTHERM3 5 4  1.72e-3
CTHERM4 4 3  2.94e-3
CTHERM5 3 2  4.02e-2
CTHERM6 2 TL 9.82e-1
RTHERM1 TH 6 4.38e-3
RTHERM2 6 5  1.88e-2
RTHERM3 5 4  4.58e-1
RTHERM4 4 3  7.89e-1
RTHERM5 3 2  1.13e+0
RTHERM6 2 TL 1.80e+0
.ENDS FCD9N60NTM_Thermal 
**-------------------------------------------------------------------------------
** Creation: Aug.-25-2010   Rev.: 0.0
** Fairchild Semiconductor
