* OrCAD Model Editor - Version 9.1
*$
.SUBCKT FDC638APZ 2 1 3   
*Nom Temp=25 deg C
Dbody 5 7 DbodyMOD
Dbreak 7 11 DbreakMOD
Ebreak 5 11 17 7 -22
Lgate 1 9 0.6e-9
Ldrain 2 5 1e-9 
Lsource 3 7 0.178e-9
RLgate 1 9 6
RLdrain 2 5 10
RLsource 3 7 1.78
Rgate 9 6 18
It 7 17 1
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=4e-4 TC2=-1e-7)
.MODEL DbodyMOD D (IS=1.3e-10 n=1.12 RS=3.5e-2 TRS1=1e-4 TRS2=1e-6
+ CJO=2.5e-10 M=0.6 TT=14n XTI=-8)
.MODEL DbreakMOD D (RS=8e-4 TRS1=1e-3 TRS2=0)
Rdrain 5 16 RdrainMOD 23.5m 
.MODEL RdrainMOD RES (TC1=3e-3 TC2=1.5e-5)
M_BSIM3 16 6 7 7 M_MOD W=1.014 L=1.03u NRS=1  
.MODEL  M_MOD PMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0)
***Process Parameters
+ TOX=240e-10 ;Oxide thickness
+ XJ=0.31u ;Channel depth 
+ NCH=1.75e17 ;Channel concentration
***Channel Current
+ U0=200 vsat=1e5 drout=1 
+ DELTA=0.25 PSCBE2=0 RSH=3.44m 
***Threshold voltage
+ VTH0=-0.8
***Sub-threshold characteristics
+ VOFF=-0.1 NFACTOR=1.4 
***Junction diodes and Capacitance
+ LINT=0.285u DLC=0.285u 
+ CGSO=230p CGSL=0p CGDO=120p CGDL=390p 
+ CJ=0 CF=0p CKAPPA=1 
*** Temperature parameters
+ KT1=-0.61 KT2=0 UA1=1.5E-9
+ NJ=10 
.ENDS 
*FDC638APZ(Rev.A) 12/21/06
*ST
*$
