.SUBCKT FDC610PZ 2 1 3
*Nom Temp 25 deg C
Dbody 5 7 DbodyMOD
Dbreak 7 11 DbreakMOD
Lgate 1 9 0.518e-9
Ldrain 2 5 0.1e-9
Lsource 3 7 0.268e-9
RLgate 1 9 5.18
RLdrain 2 5 1
RLsource 3 7 2.68
Rgate 9 6 12.4
It 7 17 1
Ebreak 5 11 17 7 -33
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=0.74e-3 TC2=-0.4e-6)
.MODEL DbodyMOD D (IS=4.0e-13 n=1 RS=22.57e-3 TRS1=1e-3 TRS2=1e-6 ;RS=24e-3
+ CJO=0.1e-9 M=0.5 TT=0.1e-9 XTI=3)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1e-3 TRS2=1e-6 )
Rdrain 5 16 RdrainMOD 18e-3 ;27.5e-3
.MODEL RdrainMOD RES (TC1=3.9e-3 TC2=1e-6)
M_BSIM3 16 6 7 7 Bsim3 W= 1.56 L=1.1e-6 NRS=1 NRD=0
.MODEL Bsim3 PMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
*Process Parameters
+ TOX= 550e-10 ;Oxide thickness
+ XJ= 0.3e-6 ;Channel depth
+ NCH= 1.36e17 ;Channel concentration
*Channel Current
+ U0=270 VSAT=500000 DROUT=1.8
+ DELTA=0.4 PSCBE2=0 RSH=6.888e-3
*Threshold voltage
+ VTH0=-2.45
*Sub-threshold characteristics
+ VOFF=-0.16 NFACTOR=1
*Junction diodes and Capacitance
+ LINT=0.3e-6 DLC=0.3e-6
+ CGSO=212e-12 CGSL=0 CGDO=23e-12 CGDL=355e-12 ;CGDL=350e-12
+ CJ=0 CF=0 CKAPPA=1.2
* Temperature parameters
+ KT1=-1.23 KT2=0 UA1=7e-9
+ NJ=10)
*13 Aug 2007 Rev.A_SP
.ENDS
