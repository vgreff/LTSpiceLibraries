.SUBCKT FDM3622 2 1 3
******************************************************************
**      Fairchild Discrete Modeling Group                       **
******************************************************************
**      Website         www.fairchildsemi.com\models            **
******************************************************************
**      (C) Copyright 2009 Fairchild Semiconductor Corporation  **
**                      All rights reserved                     **
**                                                              **
**                        FDM3622 Spice model                   **
**                      Revision RevB, 20 July 2012             **
******************************************************************		
*Nom Temp 25 deg C		
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Lgate 1 9 1.74e-9
Ldrain 2 5 0.1e-9
Lsource 3 7 0.35e-9
Rgate 9 6 14.4
It 7 17 1
Ebreak 11 7 17 7 110
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=1.1e-3 TC2=-1e-6)
.MODEL DbodyMOD D (IS=1.2e-12 n=1 RS=31.4e-3 TRS1=2e-3 TRS2=2e-6
+ CJO=5.0e-10 M=0.53 TT=1e-9 XTI=4)
.MODEL DbreakMOD D (RS=80e-3 TRS1=1e-3 TRS2=1e-6 )
Rsource 77 7 0.49e-3
Rdrain 5 16 RdrainMOD 38.3e-3
.MODEL RdrainMOD RES (TC1=7.3e-3 TC2=18e-6)
M_BSIM3 16 6 77 77 Bsim3 W=1.57 L=2.07e-6 NRS=0 NRD=0
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
*Process Parameters
+ TOX=1080e-10 ;Oxide thickness
+ XJ=1e-6 ;Channel depth
+ NCH=2.3005e17 ;Channel concentration
*Channel Current
+ U0=1400 VSAT=500000 DROUT=1.8
+ DELTA=0.19 PSCBE2=0 RSH=2.1e-3
*Threshold voltage
+ VTH0=2.1
*Sub-threshold characteristics
+ VOFF=-0.1 NFACTOR=0.8
*Junction diodes and Capacitance
+ LINT=0.6e-6 DLC=0.6e-6
+ CGSO=240e-12 CGSL=0 CGDO=8e-12 CGDL=200e-12
+ CJ=0 CF=0 CKAPPA=0.5
* Temperature parameters
+ KT1=-2.2 KT2=0 UA1=1e-9
+ NJ=10)
.ENDS FDM3622

.SUBCKT FDM3622_JA TJ TA
*Thermal Model Subcircuit
* 22 Oct 04
CTHERM1 tj c2 1.1E-4	   
CTHERM2 c2 c3 1.2E-4
CTHERM3 c3 c4 3.0E-4
CTHERM4 c4 c5 2.0E-3
CTHERM5 c5 c6 6.4E-3
CTHERM6 c6 c7 3.2E-2
CTHERM7 c7 c8 2.9e-1
CTHERM8 c8 ta 3

RTHERM1 tj c2 0.02
RTHERM2 c2 c3 0.13
RTHERM3 c3 c4 0.2 
RTHERM4 c4 c5 1.1
RTHERM5 c5 c6 3.3
RTHERM6 c6 c7 6.8
RTHERM7 c7 c8 12.2
RTHERM8 c8 ta 27
.ends

