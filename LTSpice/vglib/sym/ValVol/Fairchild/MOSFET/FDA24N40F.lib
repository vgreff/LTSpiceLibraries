****************** Power Discrete MOSFET Electrical Circuit Model *******************
** Product Name: FDA24N40F
** 400V N-Channel MOSFET and TO-220
** Model Type: BSIM3V3
**-------------------------------------------------------------------------------
.SUBCKT FDA24N40F 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5    DbodyMOD
Dbreak 5 11  DbreakMOD
Ebreak 11 7 17 7 400
Lgate 1 9    1.18e-9
Ldrain 2 5   1.44e-9
Lsource 3 7  1.48e-9
RLgate 1 9   11.8
RLdrain 2 5  14.4
RLsource 3 7 14.9
Rgate 9 6    15
It 7 17      1
Rbreak 17 7  RbreakMOD 1
.MODEL RbreakMOD RES (TC1=1.24e-3 TC2=-8.02e-7)
.MODEL DbodyMOD D (IS=2.05e-12 N=1     RS=6.42e-3 TRS1=2.3e-3 TRS2=1e-6 
+ CJO=3.74e-9      M=0.52      VJ=0.47 TT=1.05e-7 XTI=3       EG=1.18)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1e-3 TRS2=1.0e-6)
Rdrain 5 16 RdrainMOD 0.14
.MODEL RdrainMOD RES (TC1=1.05e-2 TC2=2.0e-5)
M_BSIM3 16 6 7 7 Bsim3 W=3.8 L=2.0e-6 NRS=1
.MODEL  Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 PARAMCHK=1 NQSMOD=0
+ TOX=1100e-10     XJ=1.1e-7      NCH=1.01e17
+ U0=700           VSAT=1.0e5     DROUT=1.0
+ DELTA=0.1        PSCBE2=0       RSH=1.39e-3
+ VTH0=4.55        VOFF=-0.1      NFACTOR=1.1
+ LINT=3.08e-7     DLC=3.08e-7    FC=0.5
+ CGSO=1.5e-12     CGSL=0         CGDO=1.2e-12
+ CGDL=7.54e-10    CJ=0           CF=0 
+ CKAPPA=0.0014    KT1=-1.72      KT2=0
+ UA1=1.09e-10     NJ=10
.ENDS
**-------------------------------------------------------------------------------

****************** Power Discrete MOSFET Thermal Model **************************
** Package: TO-3PN
**-------------------------------------------------------------------------------
.SUBCKT FDA24N40F_THERMAL TH TL
CTHERM1 TH 6 3.24e-4
CTHERM2 6 5  4.28e-3
CTHERM3 5 4  8.04e-3
CTHERM4 4 3  2.42e-2
CTHERM5 3 2  8.02e-2
CTHERM6 2 TL 4.82e-1
RTHERM1 TH 6 1.50e-4
RTHERM2 6 5  4.80e-3
RTHERM3 5 4  1.50e-2
RTHERM4 4 3  8.48e-2
RTHERM5 3 2  1.93e-1
RTHERM6 2 TL 2.32e-1
.ENDS FDA24N40F_THERMAL
**-------------------------------------------------------------------------------
** Creation: Jan.-19-2009   Rev.:0.0
** Fairchild Semiconductor
