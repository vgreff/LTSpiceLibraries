* PSpice Model Editor - Version 10.5.0
*$
.SUBCKT FDB3502 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Lgate 1 9 4.393e-9
Ldrain 2 5 1.44e-9
Lsource 3 7 2.5e-9
RLgate 1 9 43.93
RLdrain 2 5 14.4
RLsource 3 7 25.06
Rgate 9 6 1.5
It 7 17 1
Ebreak 11 7 17 7 88
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=8.5e-4 TC2=-1e-6)
.MODEL DbodyMOD D (IS=2.135e-12 n=1 RS=8.25e-3 TRS1=2e-3 TRS2=1e-6
+ CJO=0.32e-9 M=0.43 TT=65e-9 XTI=3)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1e-3 TRS2=1e-6)
Rdrain 5 16 RdrainMOD 27.85e-3
.MODEL RdrainMOD RES (TC1=9.5e-3 TC2=12e-6)
M_BSIM3 16 6 7 7 Bsim3 W=1.355 L=1.54e-6 NRS=0 NRD=0
.MODEL Bsim3 NMOS(LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0)
**Process Parameters
+ TOX=1000e-10 ;Oxide thickness
+ XJ=0.93e-6 ;Channel depth
+ NCH=1.4e17 ;Channel concentration
**Channel Current
+ U0=700 VSAT=8e5 DROUT=1.5
+ DELTA=0.18 PSCBE2=1e-5 RSH=1.823e-3   
**Threshold voltage
+ VTH0=3.7
**Sub-threshold characteristics
+ VOFF=-.1 NFACTOR=1.1
**Junction diodes and Capacitance
+ LINT=0.235e-6 DLC=0.235e-6 
+ CGSO=98p CGSL=0 CGDO=23.5p CGDL=122p
+ CJ=0 CF=0 CKAPPA=1
** Temperature parameters
+ KT1=-2.8 KT2=0 UA1=2.65e-9
+ NJ=10)
.ENDS
*FDB3502(Rev.A) 4/9/2008
*ST
*$
