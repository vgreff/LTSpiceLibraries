**************** Power Discrete MOSFET Electrical Circuit Model *****************
** Product Name: FCP22N60N
** 600V N-Channel MOSFET and TO-220
** Model Type: BSIM3V3
**-------------------------------------------------------------------------------
.SUBCKT FCP22N60N 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5    DbodyMOD
Dbreak 5 11  DbreakMOD
Ebreak 11 7 17 7 700
Lgate 1 9    1.12e-9
Ldrain 2 5   1.44e-9
Lsource 3 7  1.85e-9
RLgate 1 9   11.2
RLdrain 2 5  14.4
RLsource 3 7 18.5
Rgate 9 6    1.5
It 7 17      1
Rbreak 17 7  RbreakMOD 1
.MODEL RbreakMOD RES (TC1=1.20e-3 TC2=-1.85e-6)
.MODEL DbodyMOD D (IS=1.05e-12  N=1      RS=4.86e-3   TRS1=2.6e-3 TRS2=4.15e-6
+ CJO=8.24e-9      M=0.56       VJ=0.84  TT=7.58e-7   XTI=3       EG=1.12)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1.0e-3 TRS2=1e-6)
Rdrain 5 16 RdrainMOD 0.155
.MODEL RdrainMOD RES (TC1=9.1e-3 TC2=2.42e-5)
M_BSIM3 16 6 7 7 Bsim3 W=2.38 L=2.0e-6 NRS=1
.MODEL  Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 PARAMCHK=1 NQSMOD=0
+ TOX=780e-10     XJ=1.4e-6      NCH=1.3e17
+ U0=700          VSAT=1.0e5     DROUT=1.0 
+ DELTA=0.1       PSCBE2=0       RSH=1.09e-3
+ VTH0=3.35       VOFF=-0.1      NFACTOR=1.1
+ LINT=3.22e-7    DLC=1.22e-7    FC=0.5
+ CGSO=2.05e-14   CGSL=0         CGDO=1.25e-12
+ CGDL=1.14e-9    CJ=0           CF=0
+ CKAPPA=0.00014  KT1=-1.12      KT2=0
+ UA1=-1.05e-9    NJ=10 )
.ENDS

******************** Power Discrete MOSFET Thermal Model ************************
** 600V N-Channel MOSFET and TO-220
**-------------------------------------------------------------------------------
.SUBCKT FCP22N60N_Thermal TH TL
CTHERM1 TH 6 1.64e-6
CTHERM2 6 5  5.08e-5
CTHERM3 5 4  5.64e-4
CTHERM4 4 3  3.82e-3
CTHERM5 3 2  2.22e-2
CTHERM6 2 TL 1.32e-1
RTHERM1 TH 6 1.50e-4
RTHERM2 6 5  4.80e-3
RTHERM3 5 4  1.80e-2
RTHERM4 4 3  9.54e-2
RTHERM5 3 2  1.58e-1
RTHERM6 2 TL 3.34e-1
.ENDS FCP22N60N_Thermal 
**-------------------------------------------------------------------------------
** Creation: May-19-2009   Rev.: 0.0
** Fairchild Semiconductor

