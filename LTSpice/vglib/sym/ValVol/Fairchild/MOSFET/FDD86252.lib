.SUBCKT FDD86252 2 1 3	
******************************************************************
**      Fairchild Discrete Modeling Group                       **
******************************************************************
**      Website         www.fairchildsemi.com\models            **
******************************************************************
**      (C) Copyright 2009 Fairchild Semiconductor Corporation  **
**                      All rights reserved                     **
**                                                              **
**                      FDD86252 Spice model                    **
**                    Revision RevA, 05 Apr 2011                **
******************************************************************	
*Nom Temp 25 deg C		
Dbody 7 5 DbodyMOD 		
Dbreak 5 11 DbreakMOD 		
Lgate 1 9 5.108e-9		
Ldrain 2 5 0.1e-9		
Lsource 3 7 2.295e-9		
RLgate 1 9 51.08		
RLdrain 2 5 1		
RLsource 3 7 22.95		
Rgate 9 6 0.47		

* Shielded  Gate
D1 100 5 D_SG_cap
D2 100 101 D_SG_cap
R1 101 7 0.876
C1 6 101 87e-12
.MODEL D_SG_cap D (IS=1e-9 n=1 RS=4e-3 CJO=0.994e-9 M=0.711 t_abs=25)

It 7 17 1		
Ebreak 11 7 17 7 160		
Rbreak 17 7 RbreakMOD 1 		
.MODEL RbreakMOD RES (TC1=0.75e-3 TC2=-0.9e-6)		
.MODEL DbodyMOD D (IS=5.77e-12 n=1.05 RS=7e-3 TRS1=2.5e-3 TRS2=1e-6 		
+ CJO=0.252e-9 M=0.565 TT=1e-9 XTI=6.75)		
.MODEL DbreakMOD D (RS=-1e-3 TRS1=0 TRS2=1e-6 )		

Rsource 7a 7 0.715e-3		
Rdrain 5 16 RdrainMOD 40e-3		
.MODEL RdrainMOD RES (TC1=7.25e-3 TC2=21e-6)		

M_BSIM3 16 6 7a 7a Bsim3 W=1.1544 L=1.1e-6 NRS=0 NRD=0		
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0		
*Process Parameters		
+ TOX=1000e-10 ;Oxide thickness		
+ XJ=0.54e-6 ;Channel depth		
+ NCH=0.8e17 ;Channel concentration		
*Channel Current		
+ U0=700 VSAT=500000 DROUT=1.8		
+ DELTA=0.05 PSCBE2=0 RSH=0.715e-3		
*Threshold voltage		
+ VTH0=3.55
*Sub-threshold characteristics		
+ VOFF=-0.13 NFACTOR=1.7
*Junction diodes and Capacitance		
+ LINT=0.19e-6 DLC=0.19e-6 		
+ CGSO=300e-12 CGSL=0 CGDO=1e-12 CGDL=150e-12 		
+ CJ=0 CF=0 CKAPPA=1
* Temperature parameters 		
+ KT1=-3.0 KT2=0 UA1=15.5e-9		
+ NJ=10)		
.ENDS   		
