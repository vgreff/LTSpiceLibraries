* PSpice Model Editor - Version 16.3.0
*$
.SUBCKT FDC3535 2 1 3
******************************************************************
**      Fairchild Discrete Modeling Group                       **
******************************************************************
**      Website         www.fairchildsemi.com\models            **
******************************************************************
**      (C) Copyright 2009 Fairchild Semiconductor Corporation  **
**                      All rights reserved                     **
**                                                              **
**                      FDC3535 Spice model                     **
**                      Revision RevA, 29 Jun 2010              **
******************************************************************		
*Nom Temp 25 deg C		
Dbody 5 7 DbodyMOD 		
Dbreak 7 11 DbreakMOD 		
Lgate 1 9 0.559e-9		
Ldrain 2 5 0.1e-9		
Lsource 3 7 0.281e-9		
RLgate 1 9 5.59		
RLdrain 2 5 1		
RLsource 3 7 2.81		
Rgate 9 6 1.48		
It 7 17 1		
Ebreak 5 11 17 7 -85.7		
Rbreak 17 7 RbreakMOD 1 		
.MODEL RbreakMOD RES (TC1=0.81e-3 TC2=-0.2e-6)		
.MODEL DbodyMOD D (IS=0.67e-12 n=1 RS=20e-3 TRS1=0.6e-3 TRS2=4e-6 		
+ CJO=0.09e-9 M=0.35 TT=3e-9 XTI=0.7)		
.MODEL DbreakMOD D (RS=0 TRS1=65e-3 TRS2=300e-6 )		
Rsource 7a 7 4.467e-3		
Rdrain 5 16 RdrainMOD 130e-3		
.MODEL RdrainMOD RES (TC1=6.6e-3 TC2=8.5e-6)		
M_BSIM3 16 6 7a 7a Bsim3 W=0.66 L=1.7e-6 NRS=0 NRD=0		
.MODEL Bsim3 PMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0		
*Process Parameters		
+ TOX=410e-10 ;Oxide thickness		
+ XJ=1.6ue-6 ;Channel depth		
+ NCH=1.5e17 ;Channel concentration		
*Channel Current		
+ U0=420 VSAT=100000 DROUT=1.8		
+ DELTA=0.7 PSCBE2=0.00001 RSH=4.467e-3		
*Threshold voltage		
+ VTH0=-1.785		
*Sub-threshold characteristics		
+ VOFF=-0.21 NFACTOR=1.0		
*Junction diodes and Capacitance		
+ LINT=0.4e-6 DLC=0.4e-6 		
+ CGSO=355e-12 CGSL=0 CGDO=2e-12 CGDL=700e-12 		
+ CJ=0 CF=0 CKAPPA=0.5		
* Temperature parameters 		
+ KT1=-1.1 KT2=0 UA1=8.8e-9		
+ NJ=10)		
.ENDS   		

*$
