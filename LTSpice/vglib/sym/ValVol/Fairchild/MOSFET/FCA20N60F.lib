**************** Power Discrete MOSFET Electrical Circuit Model *****************
** Product Name: FCA20N60F
** 600V N-Channel MOSFET and TO-3PN
** Model Type: BSIM3V3
**-------------------------------------------------------------------------------
.SUBCKT FCA20N60F 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5    DbodyMOD
Dbreak 5 11  DbreakMOD
Ebreak 11 7 17 7 650
Lgate 1 9    1.125e-9
Ldrain 2 5   1.500e-9
Lsource 3 7  1.105e-9
RLgate 1 9   1.12
RLdrain 2 5  1.50
RLsource 3 7 1.10
Rgate 9 6    15
It 7 17      1
Rbreak 17 7  RbreakMOD 1
.MODEL RbreakMOD RES (TC1=1.05e-3 TC2=-1.15e-6)
.MODEL DbodyMOD D (IS=1.5e-12  N=1      RS=8.42e-3   TRS1=1.5e-3 TRS2=2.2e-6 
+ CJO=7.85e-9      M=0.52      VJ=0.68  TT=1.56e-7   XTI=3       EG=1.12)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1.0e-3 TRS2=1e-6)
Rdrain 5 16 RdrainMOD 0.15
.MODEL RdrainMOD RES (TC1=8.3e-3 TC2=1.5e-5)
M_BSIM3 16 6 7 7 Bsim3 W=3.62 L=2.0e-6 NRS=1
.MODEL  Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 PARAMCHK=1 NQSMOD=0
+ TOX=970e-10     XJ=1.4e-6      NCH=1.3e17
+ U0=700          VSAT=4.5e5     DROUT=1.0
+ DELTA=0.1       PSCBE2=0       RSH=1.09e-3
+ VTH0=4.38       VOFF=-0.1      NFACTOR=1.1
+ LINT=2.8e-7     DLC=1.8e-7     FC=0.5
+ CGSO=2.0e-14    CGSL=0         CGDO=1.5e-14
+ CGDL=1.46e-9    CJ=0           CF=0
+ CKAPPA=0.00015  KT1=-1.27      KT2=0
+ UA1=-4.0e-9     NJ=10
.ENDS

******************** Power Discrete MOSFET Thermal Model ************************
** 600V N-Channel MOSFET and TO-3PN
**-------------------------------------------------------------------------------
.SUBCKT FCA20N60F_Thermal TH TL
CTHERM1 TH 6 3.64e-4
CTHERM2 6 5  5.08e-3
CTHERM3 5 4  1.64e-2
CTHERM4 4 3  3.22e-2
CTHERM5 3 2  2.42e-1
CTHERM6 2 TL 5.82e-1
RTHERM1 TH 6 3.30e-3
RTHERM2 6 5  7.08e-3
RTHERM3 5 4  1.20e-2
RTHERM4 4 3  7.80e-2
RTHERM5 3 2  2.20e-1
RTHERM6 2 TL 2.80e-1
.ENDS FCA20N60F_Thermal 
**-------------------------------------------------------------------------------
** Creation: Nov.-18-2008   Rev.: 0.0
** Fairchild Semiconductor

