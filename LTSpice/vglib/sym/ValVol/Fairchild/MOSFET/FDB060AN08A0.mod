*Spice model for FDB060AN08A0 & FDP060AN08A0
.SUBCKT FDB060AN08A0 2 1 3      
*Nom Temp=25 deg C 
*October 2002

Ca 12 8 2.5e-9
Cb 15 14 2.1e-9
Cin 6 8 4.7e-9

Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Dplcap 10 5 DplcapMOD

Ebreak 11 7 17 18 82.1
Eds 14 8 5 8 1
Egs 13 8 6 8 1
Esg 6 10 6 8 1
Evthres 6 21 19 8 1
Evtemp 20 6 18 22 1

It 8 17 1

Lgate 1 9 5.3e-9
Ldrain 2 5 1.0e-9
Lsource 3 7 5e-9

RLgate 1 9 53
RLdrain 2 5 10
RLsource 3 7 50

Mmed 16 6 8 8 MmedMOD
Mstro 16 6 8 8 MstroMOD 
Mweak 16 21 8 8 MweakMOD    

Rbreak 17 18 RbreakMOD 1
Rdrain 50 16 RdrainMOD 9e-4
Rgate 9 20 1.4
RSLC1 5 51 RSLCMOD 1e-6
RSLC2 5 50 1e3
Rsource 8 7 RsourceMOD 3e-3
Rvthres 22 8 RvthresMOD 1
Rvtemp 18 19 RvtempMOD 1
S1a 6 12 13 8 S1AMOD
S1b 13 12 13 8 S1BMOD
S2a 6 15 14 13 S2AMOD
S2b 13 15 14 13 S2BMOD

Vbat 22 19 DC 1

ESLC 51 50  VALUE={(V(5,51)/ABS(V(5,51)))*(PWR(V(5,51)/(1e-6*350),5))}

.MODEL DbodyMOD D (IS=2E-11 N=1.04 RS=1.76e-3 TRS1=2.7e-3 TRS2=1e-6
+ CJO=3.2e-9 M=5.6e-1 TT=3e-10 XTI=3.9)
.MODEL DbreakMOD D (RS=3e-1 TRS1=1e-3 TRS2=-8.9e-6)
.MODEL DplcapMOD D (CJO=1.56e-9 IS=1e-30 N=10 M=0.53)

.MODEL MmedMOD NMOS (VTO=3.6 KP=6 IS=1e-30 N=10 TOX=1 L=1u W=1u RG=1.4)
.MODEL MstroMOD NMOS (VTO=4.22 KP=220 IS=1e-30 N=10 TOX=1 L=1u W=1u)
.MODEL MweakMOD NMOS (VTO=3 KP=0.03 IS=1e-30 N=10 TOX=1 L=1u W=1u RG=14 RS=0.1) 

.MODEL RbreakMOD RES (TC1=9.4e-4 TC2=-9e-7)
.MODEL RdrainMOD RES (TC1=2.2e-2 TC2=6e-5)
.MODEL RSLCMOD RES (TC1=2e-3 TC2=1e-5)
.MODEL RsourceMOD RES (TC1=1e-3 TC2=1e-6)
.MODEL RvthresMOD RES (TC1=-6e-3 TC2=-1.6e-5)
.MODEL RvtempMOD RES (TC1=-2.4e-3 TC2=1e-6)

.MODEL S1AMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-8 VOFF=-5)
.MODEL S1BMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-5 VOFF=-8)
.MODEL S2AMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-4 VOFF=-3.5)
.MODEL S2BMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-3.5 VOFF=-4)
.ENDS

.SUBCKT FDB060AN08A0_Thermal TH TL
*Thermal Model Subcircuit
*October 2002

CTHERM1 TH 6 9.6e-3
CTHERM2 6 5 9.7e-3
CTHERM3 5 4 9.8e-3
CTHERM4 4 3 1e-2
CTHERM5 3 2 3e-2
CTHERM6 2 TL 9e-2

RTHERM1 TH 6 3.2e-3
RTHERM2 6 5 8.1e-3
RTHERM3 5 4 2.3e-2
RTHERM4 4 3 1.2e-1
RTHERM5 3 2 1.5e-1
RTHERM6 2 TL 1.6e-1

.ends

