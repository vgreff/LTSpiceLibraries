* PSpice Model Editor - Version 10.5.0
*$
.SUBCKT FDB2710 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Ebreak 11 7 17 7 275
Lgate 1 9 7.421e-9
Ldrain 2 5 0.1e-9
Lsource 3 7 2.809e-9
RLgate 1 9 74.21
RLdrain 2 5 1
RLsource 3 7 28.09
Rgate 9 6 7.14
It 7 17 1
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=8e-4 TC2=-4e-7)
.MODEL DbodyMOD D (IS=2.87e-12 n=1 RS=1.48E-3 TRS1=2.5e-3 TRS2=1e-5 
+ CJO=3.65e-9 M=0.74 TT=112n XTI=3.5)
.MODEL DbreakMOD D (RS=1.8e-1 TRS1=1e-3 TRS2=0)
Rdrain 5 16 RdrainMOD 35.35m
.MODEL RdrainMOD RES (TC1=2e-3 TC2=1.5e-5)
M_BSIM3 16 6 7 7 Bsim3 W=11.47 L=1.98u NRS=0 NRD=0
.MODEL  Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
**Process Parameters
+ TOX=1250e-10 ;Oxide thickness
+ XJ=1.75u ;Channel depth
+ NCH=2.1e17 ;Channel concentration
**Channel Current
+ U0=1350 VSAT=5e5 DROUT=1.2
+ DELTA=0.15 PSCBE2=1e-5 RSH=1
**Threshold voltage
+ VTH0=3.2
**Sub-threshold characteristics
+ VOFF=-0.1 NFACTOR=1.2
**Junction diodes and Capacitance
+ LINT=0.38u DLC=0.38u 
+ CGSO=108p CGSL=0p CGDO=7.3p CGDL=125p
+ CJ=0 CF=0 CKAPPA=0.1
** Temperature parameters
+ KT1=-2.2 KT2=0 UA1=2.8e-9
+ NJ=10
.ENDS
*FDB2710(Rev.A) 6/4/08
*ST
*$
