**************** Power Discrete MOSFET Electrical Circuit Model *****************
** Product Name: FCB20N60
** 600V N-Channel MOSFET and D2-PAK
** Model Type: BSIM3V3
**-------------------------------------------------------------------------------
.SUBCKT FCB20N60 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5    DbodyMOD
Dbreak 5 11  DbreakMOD
Ebreak 11 7 17 7 650
Lgate 1 9    1.13e-9
Ldrain 2 5   1.44e-9
Lsource 3 7  1.65e-9
RLgate 1 9   1.1
RLdrain 2 5  1.4
RLsource 3 7 1.6
Rgate 9 6    15
It 7 17      1
Rbreak 17 7  RbreakMOD 1
.MODEL RbreakMOD RES (TC1=1.02e-3 TC2=-1.35e-6)
.MODEL DbodyMOD D (IS=2.05e-12  N=1      RS=8.36e-3   TRS1=1.0e-3 TRS2=2.15e-7
+ CJO=8.02e-9      M=0.64       VJ=0.64  TT=5.28e-7   XTI=3       EG=1.14)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1.0e-3 TRS2=1e-6)
Rdrain 5 16 RdrainMOD 0.15
.MODEL RdrainMOD RES (TC1=8.1e-3 TC2=1.62e-5)
M_BSIM3 16 6 7 7 Bsim3 W=4.4 L=2.0e-6 NRS=1
.MODEL  Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 PARAMCHK=1 NQSMOD=0
+ TOX=980e-10     XJ=1.4e-6      NCH=1.3e17
+ U0=700          VSAT=5.5e5     DROUT=1.0 
+ DELTA=0.1       PSCBE2=0       RSH=1.09e-3
+ VTH0=4.35       VOFF=-0.1      NFACTOR=1.1
+ LINT=2.8e-7     DLC=4.92e-7    FC=0.5
+ CGSO=1.5e-14    CGSL=0         CGDO=1.65e-13  
+ CGDL=1.22e-9    CJ=0           CF=0
+ CKAPPA=0.0028   KT1=-1.54      KT2=0
+ UA1=-1.5e-9     NJ=10
.ENDS

******************** Power Discrete MOSFET Thermal Model ************************
** 600V N-Channel MOSFET and D2-PAK
**-------------------------------------------------------------------------------
.SUBCKT FCB20N60_Thermal TH TL
CTHERM1 TH 6 3.64e-4
CTHERM2 6 5  5.08e-3
CTHERM3 5 4  1.64e-2
CTHERM4 4 3  3.22e-2
CTHERM5 3 2  2.42e-1
CTHERM6 2 TL 5.82e-1
RTHERM1 TH 6 3.30e-3
RTHERM2 6 5  7.08e-3
RTHERM3 5 4  1.20e-2
RTHERM4 4 3  7.80e-2
RTHERM5 3 2  2.20e-1
RTHERM6 2 TL 2.80e-1
.ENDS FCB20N60_Thermal 
**-------------------------------------------------------------------------------
** Creation: Nov.-19-2008   Rev.: 0.0
** Fairchild Semiconductor

