* OrCAD Model Editor - Version 9.1
*$
.SUBCKT FDD4141 2 1 3   
*Nom Temp=25 deg C
Dbody 5 7 DbodyMOD
Dbreak 7 11 DbreakMOD
Ebreak 5 11 17 7 -43
Lgate 1 9 4.936e-9
Ldrain 2 5 0.1e-9 
Lsource 3 7 2.345e-9
RLgate 1 9 49.36
RLdrain 2 5 1
RLsource 3 7 23.45
Rgate 9 6 4.6
It 7 17 1
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=7e-4 TC2=-2e-7)
.MODEL DbodyMOD D (IS=7.47e-11 n=1.2 RS=2.4e-3 TRS1=5e-5 TRS2=8e-6
+ CJO=6e-10 M=0.5 TT=14n XTI=2)
.MODEL DbreakMOD D (RS=8e-2 TRS1=1e-3 TRS2=0)
Rdrain 5 16 RdrainMOD 7.8e-3 
.MODEL RdrainMOD RES (TC1=3.2e-3 TC2=1e-5)
M_BSIM3 16 6 7 7 M_MOD W=4.7 L=0.88u NRS=1 NRD=0 
.MODEL  M_MOD PMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0)
**Process Parameters
+ TOX=500e-10 ;Oxide thickness
+ XJ=0.59u ;Channel depth 
+ NCH=1.56e17 ;Channel concentration
***Channel Current
+ U0=250 vsat=1e5 drout=0.8 
+ DELTA=0.2 PSCBE2=1e-5 RSH=0.881e-3 
**Threshold voltage
+ VTH0=-1.7
***Sub-threshold characteristics
+ VOFF=-0.1 NFACTOR=1.4 
***Junction diodes and Capacitance
+ LINT=0.175u DLC=0.175u 
+ CGSO=120p CGSL=0p CGDO=35p CGDL=132p 
+ CJ=0 CF=0p CKAPPA=1.2
**Temperature parameters
+ KT1=-1 KT2=0 UA1=1.2E-9
+ NJ=10) 
.ENDS 
*FDD4141(Rev.A) 7/6/07
*ST
*$
