**************** Power Discrete MOSFET Electrical Circuit Model *****************
** Product Name: FDD3N40
** 400V N-Channel MOSFET and D-PAK
** Model Type: BSIM3V3
**-------------------------------------------------------------------------------
.SUBCKT FDD3N40 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5    DbodyMOD
Dbreak 5 11  DbreakMOD
Ebreak 11 7 17 7 400
Lgate 1 9    1.18e-9
Ldrain 2 5   1.40e-9
Lsource 3 7  9.66e-10
RLgate 1 9   11.8
RLdrain 2 5  14.0
RLsource 3 7 9.66
Rgate 9 6    1.5
It 7 17      1
Rbreak 17 7  RbreakMOD 1
.MODEL RbreakMOD RES (TC1=1.15e-3 TC2=-1.15e-6)
.MODEL DbodyMOD D (IS=1.1e-13  N=1      RS=4.26e-2   TRS1=1.0e-3 TRS2=1.2e-7 
+ CJO=2.56e-10     M=0.44      VJ=0.64  TT=3.316e-7  XTI=3       EG=1.18)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1.0e-3 TRS2=5.0e-6)
Rdrain 5 16 RdrainMOD 2.8
.MODEL RdrainMOD RES (TC1=7.02e-3 TC2=9.82e-6)
M_BSIM3 16 6 7 7 Bsim3 W=0.66 L=2.0e-6 NRS=1
.MODEL  Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 PARAMCHK=1 NQSMOD=0
+ TOX=1100e-10    XJ=1.4e-6       NCH=1.3e17
+ U0=700          VSAT=1.0e5      DROUT=10.0   
+ PDIBLC2=1e-6    DELTA=0.1       PSCBE2=0
+ RSH=1.0e-3      VTH0=4.42       VOFF=-0.1
+ NFACTOR=1.1     LINT=3.6e-7     DLC=5.5e-7
+ FC=0.5          CGSO=1.05e-14   CGSL=0 
+ CGDO=2.55e-12   CGDL=2.78e-10   CJ=0 
+ CF=0            CKAPPA=0.00014  KT1=-1.25
+ KT2=0           UA1=-1.05e-9    NJ=10 )
.ENDS

******************** Power Discrete MOSFET Thermal Model ************************
** Package: D-PAK
**-------------------------------------------------------------------------------
.SUBCKT FDD3N40_Thermal TH TL
CTHERM1 TH 6 1.440e-5
CTHERM2 6 5  2.080e-4
CTHERM3 5 4  8.040e-4
CTHERM4 4 3  3.020e-3
CTHERM5 3 2  8.820e-3
CTHERM6 2 TL 3.820e-2
RTHERM1 TH 6 2.380e-3
RTHERM2 6 5  9.880e-3
RTHERM3 5 4  8.670e-2
RTHERM4 4 3  8.870e-1
RTHERM5 3 2  1.156e+0
RTHERM6 2 TL 2.058e+0
.ENDS FDD3N40_Thermal 
**-------------------------------------------------------------------------------
** Creation: Mar.-30-2009   Rev.: 0.0
** Fairchild Semiconductor

