* PSpice Model Editor - Version 10.5.0
*$
.SUBCKT FDC855N 2 1 3  
*Nom Temp=25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Ebreak 11 7 17 7 33
Lgate 1 9 0.51e-9
Ldrain 2 5 0.1e-9
Lsource 3 7 0.174e-10
RLgate 1 9 5.18
RLdrain 2 5 1
RLsource 3 7 1.74
Rgate 9 6 1.93
It 7 17 1
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=8e-4 TC2=-1e-6)
.MODEL DbodyMOD D (IS=1.43e-12 n=1 RS=15.3E-3 TRS1=1.8e-3 TRS2=0
+ CJO=2.1e-10 M=0.35 TT=8n XTI=1)
.MODEL DbreakMOD D (RS=8e-2 TRS1=1e-3 TRS2=0)
Rdrain 5 16 RdrainMOD 13.5m
.MODEL RdrainMOD RES (TC1=1.7e-3 TC2=2e-5)
M_BSIM3 16 6 7 7 Bsim3 W=1.02 L=0.95u NRS=1
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
**Process Parameters
+ TOX=500e-10 ;Oxide thickness
+ XJ=0.46u ;Channel depth
+ NCH=1.43e17 ;Channel concentration
**Channel Current 
+ U0=700 VSAT=5e5 DROUT=1.5
+ DELTA=0.1 PSCBE2=0 RSH=4.6e-3 
**Threshold voltage
+ VTH0=2.1
**Sub-threshold characteristics
+ VOFF=-0.1 NFACTOR=1.1
**Junction diodes and Capacitance
+ LINT=0.175u DLC=0.175u 
+ CGSO=150p CGSL=0 CGDO=28p CGDL=170p
+ CJ=0 CF=0 CKAPPA=1.8 
** Temperature parameters
+ KT1=-1.1 KT2=0 UA1=1.4e-9
+ NJ=10
.ENDS
*FDC855N(Rev.A) 1/22/2008
*ST 
*$
