* PSpice Model Editor - Version 10.5.0
*$
.SUBCKT FDB5800 2 1 3  
*Nom Temp=25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Ebreak 11 7 17 7 65
Lgate 1 9 4.989e-9
Ldrain 2 5 1.44e-9
Lsource 3 7 2.49e-9
RLgate 1 9 49.89
RLdrain 2 5 14.4
RLsource 3 7 24.9
Rgate 9 6 1.4
It 7 17 1
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=8e-4 TC2=-0.4e-6)
.MODEL DbodyMOD D (IS=8.85e-12 n=1 RS=1.97E-3 TRS1=1.2e-3 TRS2=1e-6 
+ CJO=2.3e-9 M=0.6 TT=10.5n XTI=1)
.MODEL DbreakMOD D (RS=300e-3 TRS1=1e-3 TRS2=1e-6)
Rdrain 5 16 RdrainMOD 3.3e-3
.MODEL RdrainMOD RES (TC1=9.3e-3 TC2=5e-6)
M_BSIM3 16 6 7 7 Bsim3 W=8.65 L=1.95u NRS=1
.MODEL  Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
**Process Parameters
+ TOX=600e-10 ;Oxide thickness
+ XJ=1.4u ;Channel depth
+ NCH=1.3e17 ;Channel concentration
**Channel Current
+ U0=700 VSAT=5.5e5 DROUT=2.5
+ DELTA=0.1 PSCBE2=0 RSH=0.39e-3   
**Threshold voltage
+ VTH0=1.65
**Sub-threshold characteristics
+ VOFF=-0.1 NFACTOR=1.1
**Junction diodes and Capacitance
+ LINT=0.28u DLC=0.28u 
+ CGSO=110p CGSL=0 CGDO=20p CGDL=190p
+ CJ=0 CF=0 CKAPPA=0.28
** Temperature parameters
+ KT1=-1.3 KT2=0 UA1=2e-9
+ NJ=10
.ENDS
*FDB5800(Rev.A) 11/12/07
*ST 
*$
