**************** Power Discrete MOSFET Electrical Circuit Model *****************
** Product Name: 2N7002T
** N-Channel Enhancement Mode Field Effect Transistor
** Package: SOT-523F
** Model Type: BSIM3V3
**-------------------------------------------------------------------------------
.SUBCKT F2N7002T 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5    DbodyMOD
Dbreak 5 11  DbreakMOD
Ebreak 11 7 17 7 78
Lgate 1 9    4.989e-9
Ldrain 2 5   1.44e-9
Lsource 3 7  2.49e-9
RLgate 1 9   4.89
RLdrain 2 5  20.4
RLsource 3 7 20.9
Rgate 9 6    1.4
It 7 17      1
Rbreak 17 7  RbreakMOD 1
.MODEL RbreakMOD RES (TC1=1.1e-3 TC2=-2.55e-6)
.MODEL DbodyMOD D (IS=8.05e-12  N=1      RS=7.42e-1   TRS1=2.1e-3 TRS2=4e-6 
+ CJO=2.84e-11      M=0.46      VJ=0.57  TT=3.25e-7   XTI=3       EG=1.0)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1.0e-3 TRS2=1.8e-5)
Rdrain 5 16 RdrainMOD 1.14
.MODEL RdrainMOD RES (TC1=6.8e-3 TC2=3.05e-5)
M_BSIM3 16 6 7 7 Bsim3 W=0.072 L=2.0e-6 NRS=1
.MODEL  Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 PARAMCHK=1 NQSMOD=0
+ TOX=1000e-10   XJ=1.4e-6      NCH=1.3e17
+ U0=700         VSAT=2.25e4    DROUT=1.0
+ DELTA=0.05     PSCBE2=0       RSH=1.09e-3
+ VTH0=1.76      VOFF=-0.1      NFACTOR=1.1
+ LINT=1.48e-7   DLC=1.48e-7    FC=0.5
+ CGSO=2.2e-11   CGSL=0         CGDO=8.8e-11
+ CGDL=1.01e-12  CJ=0           CF=0
+ CKAPPA=0.0028  KT1=-0.76      KT2=0
+ UA1=2e-10      NJ=10          A0=0.065 
+ AGS=0.82
.ENDS
**-------------------------------------------------------------------------------
** Creation: Apr.-18-2008
** Fairchild Semiconductor

