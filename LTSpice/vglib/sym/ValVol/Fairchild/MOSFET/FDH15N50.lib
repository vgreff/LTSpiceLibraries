.SUBCKT FDH15N50  2 1 3; 
* Spice model for the FDH15N50, FDB15N50 & FDP15N50
* NOM TEMP=25 deg C
* 8/23/02

Ca 12 8 11e-10
Cb 15 14 12e-10
Cin 6 8 1865e-12
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Dplcap 10 5 DplcapMOD
Ebreak 11 7 17 18 526.4
Eds 14 8 5 8 1
Egs 13 8 6 8 1
Esg 6 10 6 8 1
Evthres 6 21 19 8 1
Evtemp 20 6 18 22 1
It 8 17 1
Lgate 1 9 7.456n
Ldrain 2 5 3.955n
Lsource 3 7 6.155n
RLgate 1 9 74.56
RLdrain 2 5 39.55
RLsource 3 7 61.55
Mmed 16 21 8 8 MmedMOD
Mstro 16 6 8 8 MstroMOD
Mweak 16 21 8 8 MweakMOD
Rbreak 17 18 RbreakMOD 1
Rdrain 50 16 RdrainMOD 2.96e-1
Rgate 9 20 1.53
RSLC1 5 51 RSLCMOD 1e-6
RSLC2 5 50 1e3
Rsource 8 7 RsourceMOD 1.5e-2
Rvthres 22 8 RvthresMOD 1
Rvtemp 18 19 RvtempMOD 1
S1a 6 12 13 8 S1AMOD
S1b 13 12 13 8 S1BMOD
S2a 6 15 14 13 S2AMOD
S2b 13 15 14 13 S2BMOD
Vbat 22 19 DC 1
ESLC 51 50  VALUE={(V(5,51)/ABS(V(5,51)))*(PWR(V(5,51)/(1e-6*18.8),3))}
.MODEL DbodyMOD D (IS=7e-12 RS=3.2e-3 N=1.09 IKF=9 TRS1=3.9e-3 TRS2=5e-6
+ CJO=1.2e-9 M=.55 TT=3.85e-7 XTI=5.5)
.MODEL DbreakMOD D (RS=3.15 TRS1=-5e-6 TRS2=0)
.MODEL DplcapMOD D (CJO=6.9e-11 IS=1e-30 N=10 M=.42)
.MODEL MmedMOD NMOS (VTO=3.82 KP=4 IS=1e-30 N=10 TOX=1 L=1u W=1u RG=1.53)
.MODEL MstroMOD NMOS (VTO=4.34 KP=62 IS=1e-30 N=10 TOX=1 L=1u W=1u)
.MODEL MweakMOD NMOS (VTO=3.237 KP=0.05 IS=1e-30 N=10 TOX=1 L=1u W=1u RG=15.3)
.MODEL RbreakMOD RES (TC1=1.345e-3 TC2=-1.04e-6)
.MODEL RdrainMOD RES (TC1=1.014e-2 TC2=3.57e-5)
.MODEL RSLCMOD RES (TC1=3.5e-3 TC2=0)
.MODEL RsourceMOD RES (TC1=1e-3 TC2=1e-6)
.MODEL RvthresMOD RES (TC1=-2.72e-3 TC2=-2.04e-5)
.MODEL RvtempMOD RES (TC1=-3.0e-3 TC2=3e-6)
.MODEL S1AMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-3.0 VOFF=-1.0)
.MODEL S1BMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-1.0 VOFF=-3.0)
.MODEL S2AMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=7.0 VOFF=9.0)
.MODEL S2BMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=9.0 VOFF=7.0)
.ENDS
