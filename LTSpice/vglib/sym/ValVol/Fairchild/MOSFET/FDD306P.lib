* OrCAD Model Editor - Version 9.1
*$
.SUBCKT FDD306P 2 1 3   
*Nom Temp=25 deg C
Dbody 5 7 DbodyMOD
Dbreak 7 11 DbreakMOD
Ebreak 5 11 17 7 -14
Lgate 1 9 0.44e-9
Ldrain 2 5 0.1e-9 
Lsource 3 7 0.12e-9
RLgate 1 9 4.4
RLdrain 2 5 1
RLsource 3 7 1.2
Rgate 9 6 4.2
It 7 17 1
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=8e-4 TC2=-2e-7)
.MODEL DbodyMOD D (IS=1e-8 n=1.35 RS=3.3E-2 TRS1=4.5e-3 TRS2=8.5e-6
+ CJO=5.5e-10 M=0.6 TT=17.4n XTI=0.01)
.MODEL DbreakMOD D (RS=8e-2 TRS1=9e-2 TRS2=0)
Rdrain 5 16 RdrainMOD 11m 
.MODEL RdrainMOD RES (TC1=3e-3 TC2=1e-5)
M_BSIM3 16 6 7 7 M_MOD W=0.776 L=1.22u NRS=1  
.MODEL  M_MOD PMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0)
***Process Parameters
+ TOX=160e-10 ;Oxide thickness
+ XJ=0.93u ;Channel depth 
+ NCH=2.2e17 ;Channel concentration
***Channel Current
+ U0=200 vsat=5e6 drout=1.2 
+ DELTA=0.5 PSCBE2=0 RSH=2.1m 
***Threshold voltage
+ VTH0=-0.55
***Sub-threshold characteristics
+ VOFF=-0.1 NFACTOR=1.2
***Junction diodes and Capacitance
+ LINT=0.43u DLC=0.43u 
+ CGSO=620p CGSL=0p CGDO=80p CGDL=1100p 
+ CJ=0 CF=0p CKAPPA=2.5
*** Temperature parameters
+ KT1=-0.55 KT2=0 UA1=1.8E-10 
+ NJ=10 
.ENDS 
*FDD306P (Rev.A) 6/12/06
*ST
*$
