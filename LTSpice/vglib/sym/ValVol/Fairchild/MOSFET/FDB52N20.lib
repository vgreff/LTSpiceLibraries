**************** Power Discrete MOSFET Electrical Circuit Model *****************
** Product Name: FDB52N20
** 200V N-Channel MOSFET
** Model Type: BSIM3V3
**-------------------------------------------------------------------------------
.SUBCKT FDB52N20 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5    DbodyMOD
Dbreak 5 11  DbreakMOD
Ebreak 11 7 17 7 200
Lgate 1 9    1.18e-9
Ldrain 2 5   1.44e-9
Lsource 3 7  1.66e-9
RLgate 1 9   11.8
RLdrain 2 5  14.4
RLsource 3 7 16.6
Rgate 9 6    1.5
It 7 17      1
Rbreak 17 7  RbreakMOD 1
.MODEL RbreakMOD RES (TC1=1.1e-3 TC2=-1.52e-6)
.MODEL DbodyMOD D (IS=1.45e-11  N=1  RS=7.42e-3  TRS1=1.5e-3  TRS2=1.0e-6 
+ CJO=3.94e-9  M=0.48  VJ=0.47  TT=1.54e-7  XTI=3  EG=1.12)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1e-3 TRS2=1e-6)
Rdrain 5 16 RdrainMOD 0.038
.MODEL RdrainMOD RES (TC1=9.0e-3 TC2=1.1e-5)
M_BSIM3 16 6 7 7 Bsim3 W=4.1 L=2.0e-6 NRS=1
.MODEL  Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 PARAMCHK=1 NQSMOD=0
+ TOX=1100e-10   XJ=1.4e-6    NCH=1.3e17     UA=2.56e-9
+ U0=700         VSAT=1.0e5   DROUT=20.0 
+ DELTA=0.10     PSCBE2=0     RSH=1.09e-3
+ VTH0=4.75      VOFF=-0.1    NFACTOR=1.1
+ LINT=5.8e-7    DLC=1.4e-7   FC=0.5
+ CGSO=1.2e-13   CGSL=0       CGDO=2.0e-13
+ CGDL=6.15e-10  CJ=0         CF=0 
+ CKAPPA=0.02    KT1=-1.78    KT2=0
+ UA1=2.55e-10   NJ=10 )
.ENDS

*************** Power Discrete MOSFET Thermal Model ********************
** Product: FDB52N20
**----------------------------------------------------------------------
.SUBCKT FDB52N20_THERMAL TH TL
CTHERM1 TH 6 1.44e-5
CTHERM2 6 5  4.68e-3
CTHERM3 5 4  2.94e-2
CTHERM4 4 3  5.22e-2
CTHERM5 3 2  3.92e-1
CTHERM6 2 TL 4.82e-1
RTHERM1 TH 6 1.50e-4
RTHERM2 6 5  4.80e-3
RTHERM3 5 4  7.50e-3
RTHERM4 4 3  4.48e-2
RTHERM5 3 2  8.03e-2
RTHERM6 2 TL 2.12e-1
.ENDS FDB52N20_THERMAL
**---------------------------------------------------------------------
** Creation: Apr.-03-2009   Rev.:0.0
** Fairchild Semiconductor

