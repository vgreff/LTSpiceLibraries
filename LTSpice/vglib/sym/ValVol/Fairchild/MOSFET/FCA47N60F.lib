**************** Power Discrete MOSFET Electrical Circuit Model *****************
** Product Name: FCA47N60F
** 600V N-Channel MOSFET and TO-3PN
** Model Type: BSIM3V3
**-------------------------------------------------------------------------------
.SUBCKT FCA47N60F 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5    DbodyMOD
Dbreak 5 11  DbreakMOD
Ebreak 11 7 17 7 700
Lgate 1 9    1.125e-9
Ldrain 2 5   1.500e-9
Lsource 3 7  1.105e-9
RLgate 1 9   11.25
RLdrain 2 5  15.00
RLsource 3 7 11.05
Rgate 9 6    1.5
It 7 17      1
Rbreak 17 7  RbreakMOD 1
.MODEL RbreakMOD RES (TC1=1.01e-3 TC2=-1.05e-6)
.MODEL DbodyMOD D (IS=4.5e-12  N=1      RS=2.58e-3   TRS1=1.2e-4 TRS2=3.4e-6 
+ CJO=1.96e-8      M=0.58      VJ=0.52  TT=2.34e-7   XTI=3       EG=1.16)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1.0e-3 TRS2=1e-6)
Rdrain 5 16 RdrainMOD 0.062
.MODEL RdrainMOD RES (TC1=8.3e-3 TC2=1.15e-5)
M_BSIM3 16 6 7 7 Bsim3 W=9.92 L=2.0e-6 NRS=1
.MODEL Bsim3 NMOS ( LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 PARAMCHK=1 NQSMOD=0
+ TOX=970e-10     XJ=1.4e-6      NCH=1.3e17
+ U0=700          VSAT=5.0e5     DROUT=1.0
+ DELTA=0.1       PSCBE2=0       RSH=2.53e-3
+ VTH0=4.25       VOFF=-0.1      NFACTOR=1.1
+ LINT=2.04e-7    DLC=2.04e-7    FC=0.5
+ CGSO=2.8e-15    CGSL=0         CGDO=3.8e-15
+ CGDL=1.36e-9    CJ=0           CF=0
+ CKAPPA=0.02     KT1=-1.47      KT2=0
+ UA1=-1.5e-9     NJ=10 )
.ENDS

******************** Power Discrete MOSFET Thermal Model ************************
** 600V N-Channel MOSFET and TO-3PN
**-------------------------------------------------------------------------------
.SUBCKT FCA47N60F_Thermal TH TL
CTHERM1 TH 6 8.44e-4
CTHERM2 6 5  1.08e-3
CTHERM3 5 4  1.34e-2
CTHERM4 4 3  3.22e-2
CTHERM5 3 2  3.92e-1
CTHERM6 2 TL 4.82e-1
RTHERM1 TH 6 1.80e-4
RTHERM2 6 5  2.20e-3
RTHERM3 5 4  8.00e-3
RTHERM4 4 3  4.70e-2
RTHERM5 3 2  8.39e-2
RTHERM6 2 TL 1.59e-1
.ENDS FCA47N60F_Thermal 
**-------------------------------------------------------------------------------
** Creation: Mar.-28-2012   Rev.: 0.0
** Fairchild Semiconductor

