*FDC6420C at Temp. Electrical Model (Complementary N-Ch)
*---------------------------------------------------------
.SUBCKT FDC6420C_Q1 20 10 30 50
*20=DRAIN 10=GATE 30=SOURCE 50=VTEMP
Rg 10 11x 1
Rdu 12x 1 1u
M1 2 1 4x 4x DMOS L=1u W=1u
.MODEL DMOS NMOS(VTO=1 KP=1.71E+1
+THETA=.133333 VMAX=5.5E5 LEVEL=3)
Cgs 1 5x 341p
Rd 20 4 1.3E-2
Dds 5x 4 DDS
.MODEL DDS D(M=4.24E-1 VJ=6.86E-1 CJO=123p)
Dbody 5x 20 DBODY
.MODEL DBODY D(IS=7.61E-17 N=0.671205 RS=.001633 TT=5.75n)
Ra 4 2 1.3E-2 
Rs 5x 5 0.5m
Ls 5 30 0.5n
M2 1 8 6 6 INTER
E2 8 6 4 1 2
.MODEL INTER NMOS(VTO=0 KP=10 LEVEL=1)
Cgdmax 7 4 242p
Rcgd 7 4 10meg
Dgd 6 4 DGD
Rdgd 6 4 10meg
.MODEL DGD D(M=2.9E-1 VJ=2.58E-2 CJO=242p)
M3 7 9 1 1 INTER
E3 9 1 4 1 -2
*ZX SECTION
EOUT 4x 6x poly(2) (1x,0) (3x,0) 0 0 0 0 1
FCOPY 0 3x VSENSE 1
RIN 1x 0 1G
VSENSE 6x 5x 0
RREF 3x 0 10m
*TEMP SECTION
ED 101 0 VALUE {V(50,100)}
VAMB 100 0 25
EKP 1x 0 101 0 .022
*VTO TEMP SECTION
EVTO 102 0 101 0 .00065
EVT 12x 11x 102 0 1
*DIODE THEMO BREAKDOWN SECTION
EBL VB1 VB2 101 0 .08
VBLK VB2 0 20
D 20 DB1 DBLK
.MODEL DBLK D(IS=1E-14 CJO=.1p RS=.1)
EDB DB1 0 VB1 0 1
.ENDS FDC6420C_Q1
*---------------------------------------------------------------------------------------
*FDC6420C at Temp. Electrical Model (Complementary P-Ch)
*---------------------------------------------------------------------------------------
.SUBCKT FDC6420C_Q2 20 10 30 50
*20=DRAIN 10=GATE 30=SOURCE 50=VTEMP
Rg 10 11x 1
Rdu 12x 1 1u
M1 2 1 4x 4x DMOS L=1u W=1u
.MODEL DMOS PMOS(VTO=-0.9 KP=5.3
+THETA=0.166667 VMAX=9.5E5 LEVEL=3)
Cgs 1 5x 353p
Rd 20 4 1.95E-3
Dds 4 5x DDS
.MODEL DDS D(M=4.1E-1 VJ=7.17E-1 CJO=123p)
Dbody 20 5x DBODY
.MODEL DBODY D(IS=1.21E-9 N=1.42541 RS=.00104 TT=9.2n)
Ra 4 2 1.95E-3
Rs 5x 5 0.5m
Ls 5 30 0.5n
M2 1 8 6 6 INTER
E2 8 6 4 1 2
.MODEL INTER PMOS(VTO=0 KP=10 LEVEL=1)
Cgdmax 7 4 387p
Rcgd 7 4 10meg
Dgd 4 6 DGD
Rdgd 4 6 10meg
.MODEL DGD D(M=3.3E-1 VJ=9.47E-3 CJO=387p)
M3 7 9 1 1 INTER
E3 9 1 4 1 -2
*ZX SECTION
EOUT 4x 6x poly(2) (1x,0) (3x,0) 0 0 0 0 1
FCOPY 0 3x VSENSE 1
RIN 1x 0 1G
VSENSE 6x 5x 0
RREF 3x 0 10m
*TEMP SECTION
ED 101 0 VALUE {V(50,100)}
VAMB 100 0 25
EKP 1x 0 101 0 .04
*VTO TEMP SECTION
EVTO 102 0 101 0 .001
EVT 11x 12x 102 0 1
*DIODE THEMO BREAKDOWN SECTION
EBL VB1 VB2 101 0 .08
VBLK VB2 0 20
D DB1 20 DBLK
.MODEL DBLK D(IS=1E-14 CJO=.1p RS=.1)
EDB 0 DB1 VB1 0 1
.ENDS FDC6420C_Q2
*FDC6420C (Rev.C) 5/20/05 
*ST

