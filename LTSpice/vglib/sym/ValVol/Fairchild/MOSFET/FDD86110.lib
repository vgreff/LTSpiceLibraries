* PSpice Model Editor - Version 16.3.0
*$
.SUBCKT FDD86110 2 1 3	
******************************************************************
**      Fairchild Discrete Modeling Group                       **
******************************************************************
**      Website         www.fairchildsemi.com\models            **
******************************************************************
**      (C) Copyright 2009 Fairchild Semiconductor Corporation  **
**                      All rights reserved                     **
**                                                              **
**                      FDD86110 Spice model                    **
**                      Revision RevA, 04 October 2011          **
******************************************************************	
*Nom Temp 25 deg C		
Dbody 7 5 DbodyMOD 		
Dbreak 5 11 DbreakMOD 		
Lgate 1 9 5.88e-9		
Ldrain 2 5 0.1e-9		
Lsource 3 7 2.636e-9		
RLgate 1 9 58.8		
RLdrain 2 5 1		
RLsource 3 7 26.36		
Rgate 9 6 0.43		
* Shielded  Gate  		
D_D1 100 5 D_SG_cap1		
D_D2 100 101 D_SG_cap2		
R_R1 101 7 0.36		
C_C1 6 101 143e-12		
.MODEL D_SG_cap1 D (IS=1e-9 n=1 RS=5e-3 CJO=2.7e-9 M=0.6 t_abs=25)
.MODEL D_SG_cap2 D (IS=1e-9 n=1 RS=5e-3 CJO=1.8e-9 M=0.6 t_abs=25) 		
It 7 17 1		
Ebreak 11 7 17 7 110.45		
Rbreak 17 7 RbreakMOD 1 		
.MODEL RbreakMOD RES (TC1=0.67e-3 TC2=-0.7e-6)		
.MODEL DbodyMOD D (IS=2.9e-12 n=1 RS=2.8e-3 TRS1=2.5e-3 TRS2=1e-6 		
+ CJO=0.73e-9 M=0.48 TT=1e-9 XTI=3.5) ;2.2		
.MODEL DbreakMOD D (RS=8e-3 TRS1=1e-3 TRS2=1e-6 )		
Rsource 7a 7 0.729e-3		
Rdrain 5 16 RdrainMOD 7.8e-3		
.MODEL RdrainMOD RES (TC1=6.9e-3 TC2=17e-6)		
M_BSIM3 16 6 7a 7a Bsim3 W=4.09 L=1.15e-6 NRS=0 NRD=0		
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0		
*Process Parameters		
+ TOX=1000e-10 ;Oxide thickness		
+ XJ=0.62e-6 ;Channel depth		
+ NCH=0.96e17 ;Channel concentration		
*Channel Current		
+ U0=800 VSAT=500000 DROUT=1.8		
+ DELTA=0.06 PSCBE2=0 RSH=0.729e-3		
*Threshold voltage		
+ VTH0=3.59		
*Sub-threshold characteristics		
+ VOFF=-0.3 NFACTOR=1		
*Junction diodes and Capacitance		
+ LINT=0.175e-6 DLC=0.175e-6 		
+ CGSO=141e-12 CGSL=0 CGDO=3e-12 CGDL=80e-12 ;176		
+ CJ=0 CF=0 CKAPPA=1.5		
* Temperature parameters 		
+ KT1=-1.7 KT2=0 UA1=3e-9		
+ NJ=10)		
.ENDS   		
* 		
*$
