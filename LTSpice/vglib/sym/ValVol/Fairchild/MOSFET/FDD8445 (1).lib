.SUBCKT FDD8445 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Lgate 1 9 6.01e-9
Ldrain 2 5 1.0e-9
Lsource 3 7 2.2e-9
RLgate 1 9 60.1
RLdrain 2 5 10
RLsource 3 7 22
Rgate 9 6 1.7
It 7 17 1
Ebreak 11 7 17 7 43.8
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=8e-4 TC2=-1e-6)
.MODEL DbodyMOD D (IS=2.5e-12 n=1.04 RS=4.0m TRS1=1.8e-3 TRS2=2e-6 
+ CJO=0.7e-9 M=0.45 TT=20n XTI=3 )
.MODEL DbreakMOD D (RS=0.1 TRS1=1e-3 TRS2=1e-6)
Rdrain 5 16 RdrainMOD 7.6m
.MODEL RdrainMOD RES (TC1=4.0e-3 TC2=4e-6)
M_BSIM3 16 6 7 7 Bsim3 W=5.24 L=1.03u NRS=1
*__________________________________________________________
.MODEL  Bsim3 NMOS ( LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
*Process Parameters
+ TOX=450e-10 ;Oxide thickness
+ XJ=0.48u ;Channel depth
+ NCH=3.05e17 ;Channel concentration
*Channel Current
+ U0=2400 VSAT=2e5 DROUT=1.52
+ DELTA=0.2 PSCBE2=0 RSH=0.35m   
*Threshold voltage
+ VTH0=2.31
*Sub-threshold characteristics
+ VOFF=-0.29 NFACTOR=1.0
*Junction diodes and Capacitance
+ LINT=0.12u DLC=0.12u 
+ CGSO=0.1p CGSL=0 CGDO=10p CGDL=190p
+ CJ=0 CF=0 CKAPPA=1.5  
* Temperature parameters
+ KT1=-1.65 KT2=0 UA1=1.15e-8 
+ NJ=10
.ENDS
*31 March 2006 Rev.A_RG
