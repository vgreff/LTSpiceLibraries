.SUBCKT FDB8860 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Lgate 1 9 4.2e-9
Ldrain 2 5 1.0e-9
Lsource 3 7 3.6e-9
RLgate 1 9 42
RLdrain 2 5 10
RLsource 3 7 36
Rgate 9 6 1.67
It 7 17 1
Ebreak 11 7 17 7 33
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=8e-4 TC2=-1e-6)
.MODEL DbodyMOD D (IS=3.2e-11 n=1.02 RS=1.2e-3 TRS1=2.5e-3 TRS2=1e-6
+ CJO=2.8e-9 M=0.55 TT=0.1e-9 XTI=3 )
.MODEL DbreakMOD D (RS=8e-2 TRS1=1e-3 TRS2=0)
Rsource 7a 7 1.2e-3
Rdrain 5 16 RdrainMOD 0.24e-3
.MODEL RdrainMOD RES (TC1=4.9e-3 TC2=5.4e-6)
M_BSIM3 16 6 7a 7a Bsim3 W=19.5 L=0.95e-6 NRS=0 NRD=0
.MODEL  Bsim3 NMOS ( LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
*Process Parameters
+ TOX=455e-10 ;Oxide thickness
+ XJ=0.48e-6 ;Channel depth
+ NCH=1.43e17 ;Channel concentration
*Channel Current
+ U0=700 VSAT=5e5 DROUT=1.8
+ DELTA=0.1 PSCBE2=5e-5 RSH=0.55e-3
*Threshold voltage
+ VTH0=1.69
*Sub-threshold characteristics
+ VOFF=-0.18 NFACTOR=1
*Junction diodes and Capacitance
+ LINT=0.175e-6 DLC=0.175e-6 
+ CGSO=36e-12 CGSL=0 CGDO=27e-12 CGDL=200e-12
+ CJ=0 CF=0 CKAPPA=1.8
* Temperature parameters
+ KT1=-0.8 KT2=0 UA1=0.5e-9 
+ NJ=10)
* 23 Jan 2008 Rev.C1_SP
.ENDS

.SUBCKT FDB8860_JC TH TL
*Thermal Model Subcircuit  12/10/2010
CTHERM1 th c2 1.8e-3 	   
CTHERM2 c2 c3 2.5e-3
CTHERM3 c3 c4 5.0e-3
CTHERM4 c4 c5 1.0e-2
CTHERM5 c5 c6 4.0e-2
CTHERM6 c6 tl 3.0e-1

RTHERM1 th c2 .01
RTHERM2 c2 c3 .04
RTHERM3 c3 c4 .07
RTHERM4 c4 c5 0.10
RTHERM5 c5 c6 0.12
RTHERM6 c6 tl 0.13
.ends

