*FDC6320C at Temp. Electrical Model (Complementary N-Ch)
*---------------------------------------------------------
.SUBCKT FDC6320CN 20 10 30 50
*20=DRAIN 10=GATE 30=SOURCE 50=VTEMP
Rg 10 11x 1
Rdu 12x 1 1u
M1 2 1 4x 4x DMOS L=1u W=1u
.MODEL DMOS NMOS(VTO=0.9 KP=4.5E-1
+THETA=0.25 VMAX=3E5 LEVEL=3)
Cgs 1 5x 12p
Rd 20 4 7.8E-1 
Dds 5x 4 DDS
.MODEL DDS D(M=9.9E-1 VJ=2.23 CJO=8.4p)
Dbody 5x 20 DBODY
.MODEL DBODY D(IS=7.78E-9 N=1.946325 RS=0.023193 TT=15.97n)
Ra 4 2 7.8E-1 
Rs 5x 5 0.5m
Ls 5 30 0.5n
M2 1 8 6 6 INTER
E2 8 6 4 1 2
.MODEL INTER NMOS(VTO=0 KP=10 LEVEL=1)
Cgdmax 7 4 40p
Rcgd 7 4 10meg
Dgd 6 4 DGD
Rdgd 4 6 10meg
.MODEL DGD D(M=2.9E-1 VJ=1.44E-5 CJO=40p)
M3 7 9 1 1 INTER
E3 9 1 4 1 -2
*ZX SECTION
EOUT 4x 6x poly(2) (1x,0) (3x,0) 0 0 0 0 1
FCOPY 0 3x VSENSE 1
RIN 1x 0 1G
VSENSE 6x 5x 0
RREF 3x 0 10m
*TEMP SECTION
ED 101 0 VALUE {V(50,100)}
VAMB 100 0 25
EKP 1x 0 101 0 1.8
*VTO TEMP SECTION
EVTO 102 0 101 0 .0005
EVT 12x 11x 102 0 1
*DIODE THEMO BREAKDOWN SECTION
EBL VB1 VB2 101 0 .08
VBLK VB2 0 25
D 20 DB1 DBLK
.MODEL DBLK D(IS=1E-14 CJO=.1p RS=.1)
EDB DB1 0 VB1 0 1
.ENDS FDC6320CN
*---------------------------------------------------------
*FDC6320C at Temp. Electrical Model (Complementary P-Ch)
*---------------------------------------------------------
.SUBCKT FDC6320CP 20 10 30 50
*20=DRAIN 10=GATE 30=SOURCE 50=VTEMP
Rg 10 11x 1
Rdu 12x 1 1u
M1 2 1 4x 4x DMOS L=1u W=1u
.MODEL DMOS PMOS(VTO=-0.83 KP=1.53E-1
+THETA=0.25 VMAX=2E5 LEVEL=3)
Cgs 1 5x 12p
Rd 20 4 1.6
Dds 4 5x DDS
.MODEL DDS D(M=2.91E-1 VJ=4.64E-1 CJO=14.1p)
Dbody 20 5x DBODY
.MODEL DBODY D(IS=7.94E-8 N=2.460181 RS=.021333 TT=52.03n)
Ra 4 2 1.6 
Rs 5x 5 0.5m
Ls 5 30 0.5n
M2 1 8 6 6 INTER
E2 8 6 4 1 2
.MODEL INTER PMOS(VTO=0 KP=10 LEVEL=1)
Cgdmax 7 4 35.6p
Rcgd 7 4 10meg
Dgd 4 6 DGD
Rdgd 4 6 10meg
.MODEL DGD D(M=4.01E-1 VJ=2.6E-3 CJO=35.6p)
M3 7 9 1 1 INTER
E3 9 1 4 1 -2
*ZX SECTION
EOUT 4x 6x poly(2) (1x,0) (3x,0) 0 0 0 0 1
FCOPY 0 3x VSENSE 1
RIN 1x 0 1G
VSENSE 6x 5x 0
RREF 3x 0 10m
*TEMP SECTION
ED 101 0 VALUE {V(50,100)}
VAMB 100 0 25
EKP 1x 0 101 0 2.9
*VTO TEMP SECTION
EVTO 102 0 101 0 .0016
EVT 11x 12x 102 0 1
*DIODE THEMO BREAKDOWN SECTION
EBL VB1 VB2 101 0 .08
VBLK VB2 0 25
D DB1 20 DBLK
.MODEL DBLK D(IS=1E-14 CJO=.1p RS=.1)
EDB 0 DB1 VB1 0 1
.ENDS FDC6320CP
*FDC6320C (Rev.A) 1/17/03 **ST

