.SUBCKT FDB2614 2 1 3
*Nom Temp=25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Ebreak 11 7 17 7 222
Lgate 1 9 1.37e-9
Ldrain 2 5 0.1e-9
Lsource 3 7 0.29e-9
RLgate 1 9 13.7
RLdrain 2 5 1
RLsource 3 7 2.9
Rgate 9 6 1.04
It 7 17 1
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=1e-3 TC2=-4e-7)
.MODEL DbodyMOD D (IS=5e-11 n=1.1 RS=1.6e-3 TRS1=2e-3 TRS2=1e-6
+ CJO=3.7e-9 M=0.7 TT=117n XTI=2)
.MODEL DbreakMOD D (RS=0.5 TRS1=1e-3 TRS2=0)
Rdrain 5 16 RdrainMOD 21.5e-3
.MODEL RdrainMOD RES (TC1=8.3e-3 TC2=2.5e-5)
M_BSIM3 16 6 7 7 Bsim3 W=11.47 L=1.47e-6 NRS=1
.MODEL  Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
**Process Parameters
+ TOX=1250e-10 ;Oxide thickness
+ XJ=1.715e-6 ;Channel depth
+ NCH=1.8e17 ;Channel concentration
**Channel Current
+ U0=2000 VSAT=2e5 DROUT=2
+ DELTA=0.15 PSCBE2=0 RSH=0.6e-3
**Threshold voltage
+ VTH0=3.9
**Sub-threshold characteristics
+ VOFF=-0.01 NFACTOR=1
**Junction diodes and Capacitance
+ LINT=0.12e-6 DLC=0.12e-6
+ CGSO=130e-12 CGSL=0 CGDO=2e-12 CGDL=85e-12
+ CJ=0 CF=0 CKAPPA=1.5
** Temperature parameters
+ KT1=-1.8 KT2=0 UA1=0.7e-10
+ NJ=10)
.ENDS
*FDB2614(Rev.A1) 4/2/07
*SP
