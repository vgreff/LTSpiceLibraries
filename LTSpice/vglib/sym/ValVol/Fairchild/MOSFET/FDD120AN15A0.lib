.SUBCKT FDD120AN15A0 2 1 3
*Spice model for FDD120AN15A0 & FDP120AN15A0
*Nom Temp=25 deg C 
*8 July 02

Ca 12 8 2.5e-10
Cb 15 14 2.5e-10
Cin 6 8 7.5e-10

Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Dplcap 10 5 DplcapMOD

Ebreak 11 7 17 18 162
Eds 14 8 5 8 1
Egs 13 8 6 8 1
Esg 6 10 6 8 1
Evthres 6 21 19 8 1
Evtemp 20 6 18 22 1

It 8 17 1

Lgate 1 9 3e-9
Ldrain 2 5 1.0e-9
Lsource 3 7 2e-9

RLgate 1 9 30
RLdrain 2 5 10
RLsource 3 7 20

Mmed 16 6 8 8 MmedMOD
Mstro 16 6 8 8 MstroMOD 
Mweak 16 21 8 8 MweakMOD    

Rbreak 17 18 RbreakMOD 1
Rdrain 50 16 RdrainMOD 6.55e-2
Rgate 9 20 3.6
RSLC1 5 51 RSLCMOD 1.0e-6
RSLC2 5 50 1.0e3
Rsource 8 7 RsourceMOD 2.8e-2
Rvthres 22 8 RvthresMOD 1
Rvtemp 18 19 RvtempMOD 1
S1a 6 12 13 8 S1AMOD
S1b 13 12 13 8 S1BMOD
S2a 6 15 14 13 S2AMOD
S2b 13 15 14 13 S2BMOD

Vbat 22 19 DC 1

ESLC 51 50  VALUE={(V(5,51)/ABS(V(5,51)))*(PWR(V(5,51)/(1e-6*25),3))}

.MODEL DbodyMOD D (IS=4E-12 N=1.07 RS=6.5e-3 TRS1=3.0e-3 TRS2=1.5e-6
+ CJO=5.5e-10 M=0.65 TT=5e-8 XTI=4.2)
.MODEL DbreakMOD D (RS=0.5 TRS1=1e-3 TRS2=-1e-6)
.MODEL DplcapMOD D (CJO=1.56e-10 IS=1.0e-30 N=10 M=0.62)

.MODEL MmedMOD NMOS (VTO=3.6 KP=1.8 IS=1e-30 N=10 TOX=1 L=1u W=1u RG=3.6)
.MODEL MstroMOD NMOS (VTO=4.4 KP=30 IS=1e-30 N=10 TOX=1 L=1u W=1u)
.MODEL MweakMOD NMOS (VTO=3.14 KP=0.02 IS=1e-30 N=10 TOX=1 L=1u W=1u RG=36 RS=0.1) 

.MODEL RbreakMOD RES (TC1=1.1e-3 TC2=-1e-6)
.MODEL RdrainMOD RES (TC1=8.5e-3 TC2=2.5e-5)
.MODEL RSLCMOD RES (TC1=3.4e-3 TC2=1.5e-6)
.MODEL RsourceMOD RES (TC1=4.1e-3 TC2=1e-6)
.MODEL RvthresMOD RES (TC1=-3.6e-3 TC2=-1.4e-5)
.MODEL RvtempMOD RES (TC1=-4.1e-3 TC2=1.5e-6)

.MODEL S1AMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-6.0 VOFF=-4.0)
.MODEL S1BMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-4.0 VOFF=-6.0)
.MODEL S2AMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-2.5 VOFF=-0.5)
.MODEL S2BMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-0.5 VOFF=-2.5)
.ENDS


.SUBCKT FDD120AN15A0_Thermal TH TL
*Thermal Model Subcircuit

CTHERM1 TH 6 1.2e-3
CTHERM2 6 5 2e-3
CTHERM3 5 4 2.5e-3
CTHERM4 4 3 3.15e-3
CTHERM5 3 2 3.3e-3
CTHERM6 2 TL 1.35e-2

RTHERM1 TH 6 6.8e-2
RTHERM2 6 5 1.18e-1
RTHERM3 5 4 2.28e-1
RTHERM4 4 3 3.28e-1
RTHERM5 3 2 5.28e-1
RTHERM6 2 TL 5.78e-1
.ends

