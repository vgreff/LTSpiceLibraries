.SUBCKT FDB8443 2 1 3
*Nom Temp 25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Lgate 1 9 4.052e-9
Ldrain 2 5 1e-9
Lsource 3 7 2.044e-9
RLgate 1 9 40.52
RLdrain 2 5 10
RLsource 3 7 20.44
Rgate 9 6 0.9
It 7 17 1
Ebreak 11 7 17 7 44
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=1e-3 TC2=1e-6)
.MODEL DbodyMOD D (IS=2e-12 n=1 RS=1.2e-3 TRS1=1.8e-3 TRS2=2.5e-6
+ CJO=1.3e-9 M=0.45 TT=16e-9 XTI=3)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1e-3 TRS2=1e-6 )
Rdrain 5 16 RdrainMOD 1.8e-3
.MODEL RdrainMOD RES (TC1=5.4e-3 TC2=7.0e-6)
M_BSIM3 16 6 7 7 Bsim3 W= 15.01 L=1.03e-6 NRS=1 NRD=0
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
*Process Parameters
+ TOX= 455e-10 ;Oxide thickness
+ XJ= 0.48e-6 ;Channel depth
+ NCH= 3.05e17 ;Channel concentration
*Channel Current
+ U0=4500 VSAT=800000 DROUT=1.52
+ DELTA=0.3 PSCBE2=0 RSH=0.5e-3
*Threshold voltage
+ VTH0=2.45
*Sub-threshold characteristics
+ VOFF=-0.32 NFACTOR=1
*Junction diodes and Capacitance
+ LINT=0.12e-6 DLC=0.12e-6	
+ CGSO=12e-12 CGSL=0 CGDO=5e-12 CGDL=220e-12
+ CJ=0 CF=0 CKAPPA=2
* Temperature parameters
+ KT1=-1.7 KT2=0 UA1=20e-9
+ NJ=10)
* 3 Jan 2007 Rev.A_SP
.ENDS		
