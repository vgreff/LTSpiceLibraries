.SUBCKT FDA032N08 2 1 3
******************************************************************
**      Fairchild Discrete Modeling Group                       **
******************************************************************
**      Website         www.fairchildsemi.com\models            **
**      Scott Pearson   scott.pearson@fairchildsemi.com         **
**      Sylvie Tran     sylvie.tran@fairchildsemi.com           **
******************************************************************
**      (C) Copyright 2008 Fairchild Semiconductor Corporation  **
**                      All rights reserved                     **
**                                                              **
**                      FDA032N08 Spice model                   **
**                      Revision RevA, 19 Jan 2009              **
******************************************************************
*Nom Temp 25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Lgate 1 9 5.954e-9
Ldrain 2 5 1.44e-9
Lsource 3 7 2.009e-9
RLgate 1 9 59.54
RLdrain 2 5 14.4
RLsource 3 7 20.09
Rgate 9 6 0.75
It 7 17 1
Ebreak 11 7 17 7 82
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=1.25e-3 TC2=-1e-6)
.MODEL DbodyMOD D (IS=10e-12 n=1 RS=1.019e-3 TRS1=3.0e-3 TRS2=1e-6
+ CJO=3.7e-9 M=0.45 TT=1e-9 XTI=3)
.MODEL DbreakMOD D (RS=30e-3 TRS1=1e-3 TRS2=1e-6 )
Rsource 7a 7 0.401e-3
Rdrain 5 16 RdrainMOD 1.77e-3
.MODEL RdrainMOD RES (TC1=7e-3 TC2=1.4e-5)
M_BSIM3 16 6 7a 7a Bsim3 W=25.335 L=1.58e-6 NRS=0 NRD=0
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
*Process Parameters
+ TOX=1000e-10 ;Oxide thickness
+ XJ=1e-6 ;Channel depth
+ NCH=1.4e17 ;Channel concentration
*Channel Current
+ U0=700 VSAT=500000 DROUT=1.8
+ DELTA=0.2 PSCBE2=0.00001 RSH=0.401e-3
*Threshold voltage
+ VTH0=3.78
*Sub-threshold characteristics
+ VOFF=-0.1 NFACTOR=1
*Junction diodes and Capacitance
+ LINT=0.255e-6 DLC=0.255e-6
+ CGSO=75e-12 CGSL=0e-12 CGDO=5.9e-12 CGDL=124.6e-12
+ CJ=0 CF=0 CKAPPA=1.5
* Temperature parameters
+ KT1=-2.2 KT2=0 UA1=0.8e-9
+ NJ=10)
.ENDS
