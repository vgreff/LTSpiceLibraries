* PSpice Model Editor - Version 16.0.0
*$
.SUBCKT FDB088N08 2 1 3
******************************************************************
**      Fairchild Discrete Modeling Group                       **
******************************************************************
**      Website         www.fairchildsemi.com\models            **
**      Scott Pearson   scott.pearson@fairchildsemi.com         **
**      Sylvie Tran     sylvie.tran@fairchildsemi.com           **
******************************************************************
**      (C) Copyright 2008 Fairchild Semiconductor Corporation  **
**                      All rights reserved                     **
**                                                              **
**                      FDB088N08 Spice model                   **
**                      Revision RevA, 3 April 2009 (ST)        **
******************************************************************
*Nom Temp 25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Lgate 1 9 5.973e-9
Ldrain 2 5 1.44e-9
Lsource 3 7 2.154e-9
RLgate 1 9 59.73
RLdrain 2 5 14.4
RLsource 3 7 21.54
Rgate 9 6 1.4
It 7 17 1
Ebreak 11 7 17 7 78
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=0.95e-3 TC2=-1e-6)
.MODEL DbodyMOD D (IS=5.95e-12 n=1 RS=1.85e-3 TRS1=1.5e-3 TRS2=1e-6
+ CJO=1.28e-9 M=0.65 TT=0.1e-9 XTI=1)
.MODEL DbreakMOD D (RS=80e-3 TRS1=1e-3 TRS2=1e-6 )
Rsource 7a 7 0.535e-3
Rdrain 5 16 RdrainMOD 6.05e-3
.MODEL RdrainMOD RES (TC1=9e-3 TC2=8e-6)
M_BSIM3 16 6 7a 7a Bsim3 W=7.13 L=1.58e-6 NRS=0 NRD=0
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
*Process Parameters
+ TOX=500e-10 ;Oxide thickness
+ XJ=0.91e-6 ;Channel depth
+ NCH=1.6e17 ;Channel concentration
*Channel Current
+ U0=700 VSAT=100000 DROUT=1.8
+ DELTA=0.05 PSCBE2=0.00001 RSH=0.535e-3
*Threshold voltage
+ VTH0=3
*Sub-threshold characteristics
+ VOFF=-0.1 NFACTOR=1
*Junction diodes and Capacitance
+ LINT=0.31e-6 DLC=0.31e-6
+ CGSO=71e-12 CGSL=0e-12 CGDO=3.85e-12 CGDL=302e-12
+ CJ=0 CF=0 CKAPPA=1
* Temperature parameters
+ KT1=-1.22 KT2=0 UA1=1.25e-9
+ NJ=10)
.ENDS
*$
