.SUBCKT FDD5353 2 1 3
*Nom Temp 25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Lgate 1 9 4.255e-9
Ldrain 2 5 0.1e-9
Lsource 3 7 2.33e-9
Rgate 9 6 1.57
It 7 17 1
Ebreak 11 7 17 7 66.3
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=1e-3 TC2=-1e-6)
.MODEL DbodyMOD D (IS=2.0e-12 n=1 RS=2.4e-3 TRS1=1.2e-3 TRS2=1e-6
+ CJO=0.4e-9 M=0.4 TT=1e-9 XTI=1)
.MODEL DbreakMOD D (RS=80e-3 TRS1=1e-3 TRS2=1e-6 )
Rsource 7a 7 0.7e-3
Rdrain 5 16 RdrainMOD 8.1e-3
.MODEL RdrainMOD RES (TC1=6.4e-3 TC2=1.6e-5)
M_BSIM3 16 6 7a 7a Bsim3 W=3.702 L=1.58e-6 NRS=0 NRD=0
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
*Process Parameters
+ TOX=500e-10 ;Oxide thickness
+ XJ=0.91e-6 ;Channel depth
+ NCH=1.1e17 ;Channel concentration
*Channel Current
+ U0=800 VSAT=500000 DROUT=1.8
+ DELTA=0.1 PSCBE2=0.00001 RSH=0.852e-3
*Threshold voltage
+ VTH0=2.1
*Sub-threshold characteristics
+ VOFF=-0.1 NFACTOR=1.3
*Junction diodes and Capacitance
+ LINT=0.31e-6 DLC=0.31e-6
+ CGSO=97e-12 CGSL=0 CGDO=1e-12 CGDL=300e-12
+ CJ=0 CF=0 CKAPPA=1.5
* Temperature parameters
+ KT1=-1.1 KT2=0 UA1=1e-9
+ NJ=10)
* Rev_A		2/13/2008	SP
.ENDS
