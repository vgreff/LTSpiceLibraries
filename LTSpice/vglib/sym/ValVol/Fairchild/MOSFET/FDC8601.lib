.SUBCKT FDC8601 2 1 3
******************************************************************
**      Fairchild Discrete Modeling Group                       **
******************************************************************
**      Website         www.fairchildsemi.com\models            **
******************************************************************
**      (C) Copyright 2009 Fairchild Semiconductor Corporation  **
**                      All rights reserved                     **
**                                                              **
**                      FDC8601 Spice model                     **
**                    Revision RevA, 15 Dec 2010                **
******************************************************************
*Nom Temp 25 deg C
Dbody 7 5 DbodyMOD 
Dbreak 5 11 DbreakMOD 
Lgate 1 9 0.399e-9
Ldrain 2 5 0.1e-9
Lsource 3 7 0.009e-9
RLgate 1 9 3.99
RLdrain 2 5 1
RLsource 3 7 0.09
Rgate 9 6 0.6
*Shielded  Gate
D_D1 100 5 D_SG_cap
D_D2 100 101 D_SG_cap
R_R1 101 7 1.456
C_C1 6 101 25e-12
.MODEL D_SG_cap D (IS=1e-9 n=1 RS=4e-3 CJO=0.22e-9 M=0.52 t_abs=25) 
It 7 17 1
Ebreak 11 7 17 7 109.6
Rbreak 17 7 RbreakMOD 1 
.MODEL RbreakMOD RES (TC1=0.67e-3 TC2=-0.3e-6)
.MODEL DbodyMOD D (IS=1.0e-12 n=1.05 RS=26.0e-3 TRS1=2.5e-3 TRS2=1e-6 
+ CJO=0.06e-9 M=0.38 TT=1e-9 XTI=2.75)
.MODEL DbreakMOD D (RS=8e-3 TRS1=1e-3 TRS2=1e-6 )
Rsource 7a 7 11.913e-3
Rdrain 5 16 RdrainMOD 52e-3
.MODEL RdrainMOD RES (TC1=7.5e-3 TC2=22e-6)
M_BSIM3 16 6 7a 7a Bsim3 W=0.371963371428571 L=1.15e-6 NRS=0 NRD=0
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
*Process Parameters
+ TOX=1000e-10 ;Oxide thickness
+ XJ=0.62e-6 ;Channel depth
+ NCH=1.2e17 ;Channel concentration
*Channel Current
+ U0=825 VSAT=500000 DROUT=1.8
+ DELTA=0.4 PSCBE2=0 RSH=11.913e-3
*Threshold voltage
+ VTH0=3.25
*Sub-threshold characteristics
+ VOFF=-0.09 NFACTOR=1.9
*Junction diodes and Capacitance
+ LINT=0.175e-6 DLC=0.225e-6 
+ CGSO=220e-12 CGSL=0 CGDO=1e-12 CGDL=195e-12 
+ CJ=0 CF=0 CKAPPA=1
* Temperature parameters 
+ KT1=-2.2 KT2=0 UA1=6.2e-9
+ NJ=10)
.ENDS   
