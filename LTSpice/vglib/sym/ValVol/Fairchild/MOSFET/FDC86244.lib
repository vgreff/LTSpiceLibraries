.SUBCKT FDC86244 2 1 3
******************************************************************
**      Fairchild Discrete Modeling Group                       **
******************************************************************
**      Website         www.fairchildsemi.com\models            **
******************************************************************
**      (C) Copyright 2009 Fairchild Semiconductor Corporation  **
**                      All rights reserved                     **
**                                                              **
**                      FDC86244 Spice model                 	**
**                    Revision RevA, 27 Nov 2010                **
******************************************************************
*Nom Temp 25 deg C
Dbody 7 5 DbodyMOD 
Dbreak 5 11 DbreakMOD 
Lgate 1 9 0.559e-9
Ldrain 2 5 0.1e-9
Lsource 3 7 0.011e-9
RLgate 1 9 5.59
RLdrain 2 5 1
RLsource 3 7 0.11
Rgate 9 6 1.51
*Shielded  Gate  
D_D1 100 5 D_SG_cap
D_D2 100 101 D_SG_cap
R_R1 101 7 1.456
C_C1 6 101 31.5e-12
.MODEL D_SG_cap D (IS=1e-9 n=1 RS=4e-3 CJO=0.4e-9 M=0.7 t_abs=25) 
It 7 17 1
Ebreak 11 7 17 7 160.5
Rbreak 17 7 RbreakMOD 1 
.MODEL RbreakMOD RES (TC1=0.73e-3 TC2=-0.9e-6)
.MODEL DbodyMOD D (IS=1e-12 n=1 RS=35e-3 TRS1=2.5e-3 TRS2=1e-6 
+ CJO=0.1e-9 M=0.55 TT=1e-9 XTI=2.75)
.MODEL DbreakMOD D (RS=8e-3 TRS1=1e-3 TRS2=1e-6 )
Rsource 7a 7 5.712e-3
Rdrain 5 16 RdrainMOD 90.0e-3
.MODEL RdrainMOD RES (TC1=8e-3 TC2=22e-6)
M_BSIM3 16 6 7a 7a Bsim3 W=0.4864 L=1.1e-6 NRS=0 NRD=0
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
*Process Parameters
+ TOX=1000e-10 ;Oxide thickness
+ XJ=0.54e-6 ;Channel depth
+ NCH=0.8e17 ;Channel concentration
*Channel Current
+ U0=670 VSAT=500000 DROUT=1.8
+ DELTA=0.03 PSCBE2=0 RSH=5.712e-3
*Threshold voltage
+ VTH0=3.17
*Sub-threshold characteristics
+ VOFF=-0.13 NFACTOR=1.5
*Junction diodes and Capacitance
+ LINT=0.2e-6 DLC=0.21e-6 
+ CGSO=270e-12 CGSL=0 CGDO=0.3e-12 CGDL=120e-12 
+ CJ=0 CF=0 CKAPPA=0.8
* Temperature parameters 
+ KT1=-2.0 KT2=0 UA1=3.5e-9
+ NJ=10)
.ENDS   
