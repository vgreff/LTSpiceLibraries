.SUBCKT FDB8444 2 1 3 
*Nom Temp=25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Ebreak 11 7 17 7 44.4
Lgate 1 9 4.4e-9
Ldrain 2 5 1.0e-9
Lsource 3 7 1.8e-9
RLgate 1 9 44
RLdrain 2 5 10
RLsource 3 7 18
Rgate 9 6 1.9
It 7 17 1
Rbreak 17 7 RbreakMOD 1
.MODEL DbodyMOD D (IS=10e-12 n=1.04 RS=2.4m TRS1=2e-3 TRS2=2e-6
+ CJO=1.53e-09 TT=2.7e-8 XTI=4.2 )
.MODEL DbreakMOD D (RS=0.029 TRS1=1e-3 TRS2=1e-6)
M_BSIM3 16 6 7 7 M_MOD W=10.98 L=1.03u NRS=1
Rdrain 5 16 RdrainMOD 3.07m
.MODEL  M_MOD NMOS (LEVEL=7 VERSION=3.2 MOBMOD=3 CAPMOD=3 paramchk=1 NQSMOD=0
*Process Parameters
+ TOX=455e-10 ;Oxide thickness
+ XJ=0.48u ;Channel depth
+ NCH=3.05e17 ;Channel concentration
*Channel Current
+ U0=1300 VSAT=2e5 DROUT=1.52
+ PSCBE2=0 RSH=0.5m DELTA=0.1
*Threshold voltage
+ VTH0=2.06
*Sub-threshold characteristics
+ VOFF=-0.2 NFACTOR=1.0
*Junction diodes and Capacitance
+ LINT=0.12u DLC=0.2u
+ CGSO=110p CGSL=0p CGDO=10p CGDL=180p
+ CF=0 CKAPPA=1.5
* Body Diode Characteristics
+ ijth=0 NJ=10
* Temperature parameters
+ KT1=-1.5  UA1=4.31e-12 UTE=-1.9
.MODEL RbreakMOD RES (TC1=8.5e-4 TC2=-4e-7)
.MODEL RdrainMOD RES (TC1=5.1e-3 TC2=8.9e-6)
*FDB8444 (Rev.A1) 6/6/08 **SP
.ENDS 


.SUBCKT FDB8444_THERMAL TH TL
*Thermal Model Subcircuit
*03/30/07
CTHERM1 TH 6 5.0e-3
CTHERM2 6 5 5.5e-3
CTHERM3 5 4 5.6e-3
CTHERM4 4 3 8.5e-3
CTHERM5 3 2 3.4e-2
CTHERM6 2 TL 20.0e-2
RTHERM1 TH 6 2.5e-4
RTHERM2 6 5 8.0e-3
RTHERM3 5 4 7.9e-2
RTHERM4 4 3 1.7e-1
RTHERM5 3 2 2.1e-1
RTHERM6 2 TL 2.4e-1
.ends

