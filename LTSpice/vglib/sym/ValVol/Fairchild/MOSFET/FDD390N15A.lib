.SUBCKT FDD390N15A 2 1 3
******************************************************************
**      Fairchild Discrete Modeling Group                       **
******************************************************************
**      Website         www.fairchildsemi.com\models            **
******************************************************************
**      (C) Copyright 2009 Fairchild Semiconductor Corporation  **
**                      All rights reserved                     **
**                                                              **
**                      FDD390N15A Spice model                  **
**                    Revision RevA, 2 Sept 2011                **
******************************************************************
Dbody 7 5 DbodyMOD 
Dbreak 5 11 DbreakMOD 
Lgate 1 9 5.373e-9
Ldrain 2 5 0.1e-9
Lsource 3 7 2.172e-9
RLgate 1 9 53.73
RLdrain 2 5 1
RLsource 3 7 21.72
Rgate 9 6 1.48
D_D1 100 5 D_SG_cap
D_D2 100 101 D_SG_cap
R_R1 101 7 0.21
C_C1 6 101 95e-12
.MODEL D_SG_cap D (IS=1e-9 n=1 RS=5e-3 CJO=1.23e-9 M=0.69 t_abs=25) 
It 7 17 1
Ebreak 11 7 17 7 165
Rbreak 17 7 RbreakMOD 1 
.MODEL RbreakMOD RES (TC1=0.74e-3 TC2=-0.58e-6)
.MODEL DbodyMOD D (IS=0.73e-12 n=1 RS=4.75e-3 TRS1=1.5e-3 TRS2=1e-6 
+ CJO=0.31e-9 M=0.55 TT=1e-9 XTI=2.75)
.MODEL DbreakMOD D (RS=8e-3 TRS1=1e-3 TRS2=1e-6 )
Rsource 7a 7 0.689e-3
Rdrain 5 16 RdrainMOD 28.0e-3
.MODEL RdrainMOD RES (TC1=8.5e-3 TC2=28e-6)
M_BSIM3 16 6 7a 7a Bsim3 W=2.25 L=1.1e-6 NRS=0 NRD=0
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
+ TOX=1000e-10
+ XJ=0.54e-6
+ NCH=0.8e17
+ U0=670 VSAT=500000 DROUT=1.8
+ DELTA=0.3 PSCBE2=0 RSH=0.689e-3
+ VTH0=3.6
+ VOFF=-0.13 NFACTOR=1.4
+ LINT=0.19e-6 DLC=0.19e-6 
+ CGSO=168e-12 CGSL=0 CGDO=0.2e-12 CGDL=125e-12 
+ CJ=0 CF=0 CKAPPA=0.65
+ KT1=-1.9 KT2=0 UA1=3.6e-9
+ NJ=10)
.ENDS
