* PSpice Model Editor - Version 10.5.0
*$
.SUBCKT FDC637BNZ 2 1 3  
*Nom Temp=25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Ebreak 11 7 17 7 22
Lgate 1 9 5.59e-10
Ldrain 2 5 0.1e-9
Lsource 3 7 0.187e-9
RLgate 1 9 5.59
RLdrain 2 5 1
RLsource 3 7 1.87
Rgate 9 6 2.1
It 7 17 1
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=6e-4 TC2=-1e-6)
.MODEL DbodyMOD D (IS=5.95e-11 n=1 RS=2.85E-2 TRS1=1.6e-3 TRS2=1e-6 
+ CJO=1.45e-10 M=0.25 TT=6.5e-9 XTI=-5)
.MODEL DbreakMOD D (RS=8e-2 TRS1=1e-3 TRS2=0)
Rdrain 5 16 RdrainMOD 12.8m
.MODEL RdrainMOD RES (TC1=1.8e-3 TC2=2.5e-6)
M_BSIM3 16 6 7 7 Bsim3 W=0.922 L=1.1u NRS=1 
.MODEL  Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
**Process Parameters
+ TOX=240e-10 ;Oxide thickness
+ XJ=0.52e-6 ;Channel depth
+ NCH=1.2e17 ;Channel concentration
**Channel Current 
+ U0=600 VSAT=1e5 DROUT=1.2
+ DELTA=0.1 PSCBE2=0 RSH=4.8e-3
**Threshold voltage
+ VTH0=1
**Sub-threshold characteristics
+ VOFF=-0.1 NFACTOR=1
**Junction diodes and Capacitance
+ LINT=0.25u DLC=0.25u 
+ CGSO=100p CGSL=0p CGDO=43p CGDL=565p
+ CJ=0 CF=0 CKAPPA=0.85
** Temperature parameters
+ KT1=-.66 KT2=0 UA1=5e-9
+ NJ=10
.ENDS
*FDC637BNZ(Rev.A) 6/6/08
*ST 
*$
