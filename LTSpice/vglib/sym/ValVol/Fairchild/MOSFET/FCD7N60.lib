**************** Power Discrete MOSFET Electrical Circuit Model ****************
* Product Name: FCD7N60 SuperFET
* 600V N-Channel MOSFET and D-PAK
*-------------------------------------------------------------------------------
.SUBCKT FCD7N60 20 10 30
Rg 10 1 1
M1 2 1 3 3 DMOS L=1u W=1u
.MODEL DMOS NMOS(VTO={4.82*{-0.00056*TEMP+1.014}} KP={{0.00021*TEMP}+6.4}
+ THETA=0.0146  VMAX=1.0E5   LEVEL=3)
Cgs 1 3 664p
Rd 20 4 0.22 TC=0.01
Dds 3 4 DDS
.MODEL DDS D(BV={650*{0.0095*TEMP+0.97625}}  M=0.48   CJO=346p   VJ=0.24)
Dbody 3 20 DBODY
.MODEL DBODY D(IS=4.8E-13  N=1.00  RS=0.018  EG=1.16  TT=360n)
Ra 4 2 0.22 TC=0.01
Rs 3 5 0.02
Ls 5 30 1.97n
M2 1 8 6 6 INTER
E2 8 6 4 1 2
.MODEL INTER NMOS (VTO=0  KP=10  LEVEL=1)
CGDMAX 7 4 730p
Rcgd 7 4 1E7
Dgd 6 4 DGD
Rdgd 4 6 1E7
.MODEL DGD D(M=0.52  CJO=730p  VJ=0.14)
M3 7 9 1 1 INTER
E3 9 1 4 1 -2
.ENDS
*-------------------------------------------------------------------------------

********************* Power Discrete MOSFET Thermal Model **********************
.SUBCKT FCD7N60_Thermal TH TL
CTHERM1 TH 6 2.64e-4
CTHERM2 6 5 5.08e-3
CTHERM3 5 4 2.34e-2
CTHERM4 4 3 3.62e-2
CTHERM5 3 2 4.72e-1
CTHERM6 2 TL 5.82e-1

RTHERM1 TH 6 1.30e-2
RTHERM2 6 5 2.88e-2
RTHERM3 5 4 3.68e-2
RTHERM4 4 3 4.22e-1
RTHERM5 3 2 4.56e-1
RTHERM6 2 TL 5.48e-1
.ENDS
*-------------------------------------------------------------------------------
* Creation : Oct.-11-2005 
* Fairchild Semiconductor

