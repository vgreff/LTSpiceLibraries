.SUBCKT FDD4243 2 1 3
*Nom Temp=25 deg C
Dbody 5 7 DbodyMOD
Dbreak 7 11 DbreakMOD
Ebreak 5 11 17 7 -45
Lgate 1 9 4.64e-9
Ldrain 2 5 1e-9
Lsource 3 7 2.55e-9
RLgate 1 9 46.4
RLdrain 2 5 10
RLsource 3 7 25.5
Rgate 9 6 4
It 7 17 1
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=7.8e-4 TC2=-2e-7)
.MODEL DbodyMOD D (IS=5.5e-13 n=1 RS=9.5E-3 TRS1=1e-3 TRS2=2e-6
+ CJO=3.8e-10 M=0.5 TT=19e-9 XTI=3)
.MODEL DbreakMOD D (RS=0.5 TRS1=1e-3 TRS2=0)
Rdrain 5 16 RdrainMOD 27m
.MODEL RdrainMOD RES (TC1=4e-3 TC2=2e-5)
M_BSIM3 16 6 7 7 M_MOD W=1.37 L=1.37e-6 NRS=1
.MODEL  M_MOD PMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
*Process Parameters
+ TOX=400e-10 ;Oxide thickness
+ XJ=0.92e-6 ;Channel depth
+ NCH=1.6e17 ;Channel concentration
*Channel Current
+ U0=250 vsat=1e5 drout=1
+ DELTA=0.25 PSCBE2=0 RSH=2.4e-3
*Threshold voltage
+ VTH0=-1.6
*Sub-threshold characteristics
+ VOFF=-0.1 NFACTOR=1.4
*Junction diodes and Capacitance
+ LINT=0.31e-6 DLC=0.31e-6
+ CGSO=220e-12 CGSL=0 CGDO=60e-12 CGDL=320e-12
+ CJ=0 CF=0p CKAPPA=0.3
* Temperature parameters
+ KT1=-1 KT2=0 UA1=0.2e-9
+ NJ=10)
*FDD4243 Rev.A 11/2/2006_ST
.ENDS

.SUBCKT FDD4243_Thermal TH TL
* FDD4243 thermal model added 1/17/2008 by SP
CTHERM1 TH 6 1.2e-3
CTHERM2 6 5 2.0e-3
CTHERM3 5 4 2.8e-3
CTHERM4 4 TL 1.8e-2
RTHERM1 TH 6 1e-1
RTHERM2 6 5  4.0e-1
RTHERM3 5 4  8.1e-1
RTHERM4 4 TL  1.0e-0
.ENDS
