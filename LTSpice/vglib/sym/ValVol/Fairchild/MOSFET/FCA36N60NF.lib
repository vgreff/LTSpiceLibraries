**************** Power Discrete MOSFET Electrical Circuit Model *****************
** Product Name: FCA36N60NF
** 600V N-Channel SupreMOS, FRFET
** Model Type: BSIM3V3
**-------------------------------------------------------------------------------
.SUBCKT FCA36N60NF 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5    DbodyMOD
Dbreak 5 11  DbreakMOD
Ebreak 11 7 17 7 650
Lgate 1 9    1.183e-9
Ldrain 2 5   1.500e-9
Lsource 3 7  1.658e-9
RLgate 1 9   11.83
RLdrain 2 5  15.0
RLsource 3 7 16.58
Rgate 9 6    0.5
It 7 17      1
Rbreak 17 7  RbreakMOD 1
.MODEL RbreakMOD RES (TC1=1.05e-3 TC2=-1.05e-7)
.MODEL DbodyMOD D (IS=2.5e-11  N=1      RS=6.92e-3   TRS1=3.2e-3 TRS2=3.8e-6 
+ CJO=2.46e-8      M=0.84      VJ=0.42  TT=1.81e-7   XTI=3       EG=1.14)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1.0e-3 TRS2=1e-6)
Rdrain 5 16 RdrainMOD 0.080
.MODEL RdrainMOD RES (TC1=9.3e-3 TC2=3.1e-5)
M_BSIM3 16 6 7 7 Bsim3 W=7.02 L=2.0e-6 NRS=1
.MODEL  Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 PARAMCHK=1 NQSMOD=0
+ TOX=800e-10     XJ=1.4e-6      NCH=1.3e17
+ U0=700          VSAT=5.5e5     DROUT=1.0
+ DELTA=0.1       PSCBE2=0       RSH=1.09e-3
+ VTH0=4.20       VOFF=-0.1      NFACTOR=1.1
+ LINT=3.8e-7     DLC=3.8e-7     FC=0.5
+ CGSO=2.8e-14    CGSL=0         CGDO=3.9e-14
+ CGDL=7.26e-10   CJ=0           CF=0
+ CKAPPA=0.012    KT1=-1.87      KT2=0
+ UA1=-5.5e-9     NJ=10)
.ENDS

******************** Power Discrete MOSFET Thermal Model ************************
** 600V N-Channel MOSFET and TO-3PN
**-------------------------------------------------------------------------------
.SUBCKT FCA36N60NF_Thermal TH TL
CTHERM1 TH 6 1.44e-5
CTHERM2 6 5  2.08e-4
CTHERM3 5 4  3.34e-3
CTHERM4 4 3  1.22e-2
CTHERM5 3 2  3.92e-2
CTHERM6 2 TL 8.82e-2
RTHERM1 TH 6 1.80e-4
RTHERM2 6 5  2.20e-3
RTHERM3 5 4  8.00e-3
RTHERM4 4 3  4.70e-2
RTHERM5 3 2  8.39e-2
RTHERM6 2 TL 2.59e-1
.ENDS FCA36N60NF_Thermal 
**-------------------------------------------------------------------------------
** Creation: Mar.-18-2011   Rev.: 0.0
** Fairchild Semiconductor
