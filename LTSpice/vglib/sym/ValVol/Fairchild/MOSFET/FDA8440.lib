.SUBCKT FDA8440 2 1 3
*Nom Temp 25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Lgate 1 9 4.925e-9
Ldrain 2 5 1.44e-9
Lsource 3 7 4.029e-9
Rgate 9 6 1.43
It 7 17 1
Ebreak 11 7 17 7 44
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=0.8e-3 TC2=-1e-6)
.MODEL DbodyMOD D (IS=1.5e-11 n=1 RS=1.22e-3 TRS1=2.7e-3 TRS2=2.0e-6
+ CJO=2.0e-9 M=0.4 TT=0.1e-9 XTI=3)
.MODEL DbreakMOD D (RS=80e-3 TRS1=1e-3 TRS2=-1e-6)
Rsource 7a 7 0.7e-3
Rdrain 5 16 RdrainMOD 0.81e-3
.MODEL RdrainMOD RES (TC1=5.8e-3 TC2=1.5e-5)
M_BSIM3 16 6 7a 7a Bsim3 W=34.385 L=1.03e-6 NRS=0 NRD=0
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
*Process Parameters
+ TOX=450e-10 ;Oxide thickness
+ XJ=0.48e-6 ;Channel depth
+ NCH=1.6e17 ;Channel concentration
*Channel Current
+ U0=750 VSAT=800000 DROUT=1.8
+ DELTA=0.01 PSCBE2=0.00001 RSH=0.704e-3
*Threshold voltage
+ VTH0=2.2
*Sub-threshold characteristics
+ VOFF=-0.1 NFACTOR=1.1
*Junction diodes and Capacitance
+ LINT=0.12e-6 DLC=0.12e-6
+ CGSO=50e-12 CGSL=0 CGDO=2e-12 CGDL=260e-12
+ CJ=0 CF=0 CKAPPA=2
* Temperature parameters
+ KT1=-1.2 KT2=0 UA1=9e-9
+ NJ=10)
* Rev.A		SP		3/10/2008
.ENDS

.SUBCKT FDA8440_THERMAL TH TL
*Thermal Model Subcircuit
*03/10/08
*CTHERM1 TH 6 5.0e-3
CTHERM2 TH 5 1.1e-2
CTHERM3 5 4 1.2e-2
CTHERM4 4 3 1.4e-2
CTHERM5 3 2 3.4e-2
CTHERM6 2 TL 2.0e-1
*RTHERM1 TH 6 1.0e-4
RTHERM2 TH 5 1.0e-4
RTHERM3 5 4 3.36e-2
RTHERM4 4 3 1.0e-1
RTHERM5 3 2 1.2e-1
RTHERM6 2 TL 1.8e-1
.ends
