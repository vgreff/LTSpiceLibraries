* PSpice Model Editor - Version 16.3.0
*$
.SUBCKT FDC8602 2 1 3
******************************************************************
**      Fairchild Discrete Modeling Group                       **
******************************************************************
**      Website         www.fairchildsemi.com\models            **
******************************************************************
**      (C) Copyright 2009 Fairchild Semiconductor Corporation  **
**                      All rights reserved                     **
**                                                              **
**                      FDC8602 Spice model                     **
**                      Revision RevA, 3 Feb 2011               **
******************************************************************
		
*Nom Temp 25 deg C		
Dbody 7 5 DbodyMOD 		
Dbreak 5 11 DbreakMOD 		
Lgate 1 9 0.695e-9		
Ldrain 2 5 0.1e-9		
Lsource 3 7 0.413e-9		
RLgate 1 9 6.95		
RLdrain 2 5 1		
RLsource 3 7 4.13		
Rgate 9 6 1.07
* Shielded gate
D_D1         100 5 D_SG_cap1
D_D2         100 101 D_SG_cap2
R_R1         101 7  1.5
C_C1         6 101  4.3e-12
.MODEL D_SG_cap1 D (IS=1e-9 n=1 RS=4.0e-3 CJO=0.096e-9 M=0.56 t_abs=25)
.MODEL D_SG_cap2 D (IS=1e-9 n=1 RS=4.0e-3 CJO=0.015e-9 M=0.6 t_abs=25)			
It 7 17 1		
Ebreak 11 7 17 7 111.5		
Rbreak 17 7 RbreakMOD 1 		
.MODEL RbreakMOD RES (TC1=0.67e-3 TC2=-0.6e-6)		
.MODEL DbodyMOD D (IS=0.2e-12 n=1 RS=49e-3 TRS1=2.0e-3 TRS2=1e-6 		
+ CJO=0.023e-9 M=0.49 TT=3e-9 XTI=2.75) ;0.07		
.MODEL DbreakMOD D (RS=8e-3 TRS1=1e-3 TRS2=1e-6 )		
Rsource 7a 7 17.669e-3		
Rdrain 5 16 RdrainMOD 190e-3		
.MODEL RdrainMOD RES (TC1=6.9e-3 TC2=18e-6)		
M_BSIM3 16 6 7a 7a Bsim3 W=0.11 L=1.15e-6 NRS=0 NRD=0		
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0		
*Process Parameters		
+ TOX=1000e-10 ;Oxide thickness		
+ XJ=0.62e-6 ;Channel depth		
+ NCH=0.95e17 ;Channel concentration		
*Channel Current		
+ U0=840 VSAT=500000 DROUT=1.8		
+ DELTA=0.19 PSCBE2=0 RSH=17.669e-3		
*Threshold voltage		
+ VTH0=3.18		
*Sub-threshold characteristics		
+ VOFF=-0.35 NFACTOR=1		
*Junction diodes and Capacitance		
+ LINT=0.175e-6 DLC=0.175e-6 		
+ CGSO=156e-12 CGSL=0 CGDO=3e-12 CGDL=560e-12 ;195		
+ CJ=0 CF=0 CKAPPA=1.5		
* Temperature parameters 		
+ KT1=-1.8 KT2=0 UA1=4.5e-9		
+ NJ=10)		
.ENDS   		
* 		
*$
