**************** Power Discrete MOSFET Electrical Circuit Model *****************
** Product Name: FCH76N60NF
** 600V N-Channel MOSFET, FRFET
** Model Type: BSIM3V3
**-------------------------------------------------------------------------------
.SUBCKT FCH76N60NF 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5    DbodyMOD
Dbreak 5 11  DbreakMOD
Ebreak 11 7 17 7 650
Lgate 1 9    1.183e-9
Ldrain 2 5   1.500e-9
Lsource 3 7  1.012e-9
RLgate 1 9   11.83
RLdrain 2 5  15.00
RLsource 3 7 10.12
Rgate 9 6    1.5
It 7 17      1
Rbreak 17 7  RbreakMOD 1
.MODEL RbreakMOD RES (TC1=1.15e-3 TC2=-1.65e-6)
.MODEL DbodyMOD D (IS=4.5e-12  N=1.0    RS=2.72e-3   TRS1=2.32e-3  TRS2=5.4e-6 
+ CJO=5.16e-8      M=0.76      VJ=0.48  TT=1.82e-7   XTI=3         EG=1.12)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1.0e-3 TRS2=1e-6)
Rdrain 5 16 RdrainMOD 0.0287
.MODEL RdrainMOD RES (TC1=9.55e-3 TC2=2.45e-5)
M_BSIM3 16 6 7 7 Bsim3 W=20.7 L=2.0e-6 NRS=1
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 PARAMCHK=1 NQSMOD=0
+ TOX=800e-10     XJ=1.4e-6      NCH=1.3e17
+ U0=700          VSAT=3.0e5     DROUT=1.0
+ DELTA=0.1       PSCBE2=0       RSH=2.13e-3
+ VTH0=3.45       VOFF=-0.1      NFACTOR=1.1
+ LINT=4.2e-7     DLC=4.2e-7     FC=0.5
+ CGSO=1.8e-14    CGSL=0         CGDO=3.16e-13
+ CGDL=5.75e-10   CJ=0           CF=0
+ CKAPPA=0.014    KT1=-1.67      KT2=0
+ UA1=2.15e-9     NJ=10)
.ENDS

******************** Power Discrete MOSFET Thermal Model ************************
** Package: TO-247
**-------------------------------------------------------------------------------
.SUBCKT FCH76N60NF_Thermal TH TL
CTHERM1 TH 6 1.04e-4
CTHERM2 6 5  2.08e-3
CTHERM3 5 4  2.24e-2
CTHERM4 4 3  8.72e-2
CTHERM5 3 2  4.92e-1
CTHERM6 2 TL 6.82e-1
RTHERM1 TH 6 1.80e-4
RTHERM2 6 5  2.20e-3
RTHERM3 5 4  8.00e-3
RTHERM4 4 3  4.73e-2
RTHERM5 3 2  6.29e-2
RTHERM6 2 TL 1.09e-1
.ENDS FCH76N60NF_Thermal 
**-------------------------------------------------------------------------------
** Creation: Oct.-27-2010   Rev.: 0.0
** Fairchild Semiconductor

