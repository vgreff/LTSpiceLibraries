**************** Power Discrete MOSFET Electrical Circuit Model *****************
** Product Name: FDB28N30
** 300V N-Channel MOSFET and D2-PAK
** Model Type: BSIM3V3
**-------------------------------------------------------------------------------
.SUBCKT FDB28N30 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5    DbodyMOD
Dbreak 5 11  DbreakMOD
Ebreak 11 7 17 7 300
Lgate 1 9    1.183e-9
Ldrain 2 5   1.440e-9
Lsource 3 7  1.658e-9
RLgate 1 9   11.83
RLdrain 2 5  14.4
RLsource 3 7 16.58
Rgate 9 6    0.5
It 7 17      1
Rbreak 17 7  RbreakMOD 1
.MODEL RbreakMOD RES (TC1=1.25e-3 TC2=-1.56e-6)
.MODEL DbodyMOD D (IS=2.55e-13 N=1     RS=5.02e-3 TRS1=2.5e-3 TRS2=1e-6 
+ CJO=2.72e-9      M=0.51      VJ=0.47 TT=2.94e-7 XTI=3       EG=1.17)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1e-3 TRS2=1.0e-6)
Rdrain 5 16 RdrainMOD 1.04e-1
.MODEL RdrainMOD RES (TC1=8.25e-3 TC2=2.69e-5)
M_BSIM3 16 6 7 7 Bsim3 W=4.15 L=2.0e-6 NRS=1
.MODEL  Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 PARAMCHK=1 NQSMOD=0
+ TOX=1100e-10   XJ=1.4e-6    NCH=1.3e17
+ U0=700         VSAT=1.5e5   DROUT=1.0    
+ DELTA=0.10     PSCBE2=0     RSH=5.09e-6
+ VTH0=4.48      VOFF=-0.1    NFACTOR=1.1
+ LINT=3.65e-7   DLC=3.65e-7  FC=0.5
+ CGSO=1.2e-15   CGSL=0       CGDO=1.0e-15
+ CGDL=3.85e-10  CJ=0         CF=0   
+ CKAPPA=0.12    KT1=-1.78    KT2=0
+ UA1=1.2e-9     NJ=10 )
.ENDS FDB28N30

*************** Power Discrete MOSFET Thermal Model ********************
** Product: FDB28N30
**----------------------------------------------------------------------
.SUBCKT FDB28N30_THERMAL TH TL
CTHERM1 TH 6 1.04e-5
CTHERM2 6 5  6.14e-4
CTHERM3 5 4  6.82e-3
CTHERM4 4 3  9.44e-3
CTHERM5 3 2  7.02e-2
CTHERM6 2 TL 4.52e-1
RTHERM1 TH 6 1.00e-4
RTHERM2 6 5  3.00e-3
RTHERM3 5 4  3.50e-2
RTHERM4 4 3  9.60e-2
RTHERM5 3 2  1.50e-1
RTHERM6 2 TL 2.16e-1
.ENDS FDB28N30_THERMAL
**---------------------------------------------------------------------
** Creation: Aug.-13-2010   Rev.:0.0
** Fairchild Semiconductor

