* PSpice Model Editor - Version 10.5.0
*$
.SUBCKT FDB8453LZ 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Ebreak 11 7 17 7 44
Lgate 1 9 5.71e-9
Ldrain 2 5 1e-9
Lsource 3 7 2.343e-9
RLgate 1 9 57.1
RLdrain 2 5 10
RLsource 3 7 23.43
Rgate 9 6 2.3
It 7 17 1
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=9e-4 TC2=-1e-6)
.MODEL DbodyMOD D (IS=2.9e-11 n=1.1 RS=1.9m TRS1=0.4e-3 TRS2=0.4e-6 
+ CJO=1.4e-9 M=0.5 TT=10n XTI=3)
.MODEL DbreakMOD D (RS=0.1 TRS1=1e-3 TRS2=1e-6)
Rdrain 5 16 RdrainMOD 4.7m
.MODEL RdrainMOD RES (TC1=6.5e-3 TC2=7e-6)
M_BSIM3 16 6 7 7 Bsim3 W=5.3 L=1.03u NRS=1 NRD=0
.MODEL  Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0)
**Process Parameters
+ TOX=450e-10 ;Oxide thickness
+ XJ=0.48u ;Channel depth
+ NCH=1.6e17 ;Channel concentration
**Channel Current
+ U0=700 VSAT=5e5 DROUT=1.5
+ DELTA=0.1 PSCBE2=1e-5 RSH=0.923e-3
**Threshold voltage
+ VTH0=1.7
**Sub-threshold characteristics
+ VOFF=-0.1 NFACTOR=1
**Junction diodes and Capacitance
+ LINT=0.12u DLC=0.12u 
+ CGSO=90p CGSL=0 CGDO=17p CGDL=101p
+ CJ=0 CF=0 CKAPPA=1.2
** Temperature parameters
+ KT1=-1.2 KT2=0 UA1=0.5e-8 
+ NJ=10)
.ENDS
*FDB8453LZ(Rev.A) 8/8/07
*ST
*$
