* PSpice Model Editor - Version 16.0.0
*$
.SUBCKT FDB029N06 2 1 3
******************************************************************
**      Fairchild Discrete Modeling Group                       **
******************************************************************
**      Website         www.fairchildsemi.com\models            **
**      Scott Pearson   scott.pearson@fairchildsemi.com         **
**      Sylvie Tran     sylvie.tran@fairchildsemi.com           **
******************************************************************
**      (C) Copyright 2008 Fairchild Semiconductor Corporation  **
**                      All rights reserved                     **
**                                                              **
**                      FDB029N06 Spice model                   **
**                      Revision RevA, 18 Mar 2009 (ST)         **
******************************************************************
*.SUBCKT FDP030N06_B3 2 1 3
*Nom Temp 25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Lgate 1 9 5.939e-9
Ldrain 2 5 0.1e-9
Lsource 3 7 1.971e-9
RLgate 1 9 59.39
RLdrain 2 5 1
RLsource 3 7 19.71
Rgate 9 6 1.4
It 7 17 1
Ebreak 11 7 17 7 66
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=0.8e-3 TC2=-1e-6)
.MODEL DbodyMOD D (IS=7.95e-12 n=1 RS=1.12e-3 TRS1=3.0e-3 TRS2=1e-6
+ CJO=3.86e-9 M=0.5 TT=0.1e-9 XTI=2)
.MODEL DbreakMOD D (RS=30e-3 TRS1=1e-3 TRS2=1e-6 )
Rsource 7a 7 0.438e-3
Rdrain 5 16 RdrainMOD 1.36e-3
.MODEL RdrainMOD RES (TC1=3.5e-3 TC2=1.4e-5)
M_BSIM3 16 6 7a 7a Bsim3 W=17.635 L=1.58e-6 NRS=0 NRD=0
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
*Process Parameters
+ TOX=1050e-10 ;Oxide thickness
+ XJ=0.89e-6 ;Channel depth
+ NCH=1.2e17 ;Channel concentration
*Channel Current
+ U0=700 VSAT=500000 DROUT=1.8
+ DELTA=0.13 PSCBE2=0.00001 RSH=0.438e-3
*Threshold voltage
+ VTH0=3.95
*Sub-threshold characteristics
+ VOFF=-0.1 NFACTOR=1
*Junction diodes and Capacitance
+ LINT=0.28e-6 DLC=0.28e-6
+ CGSO=92e-12 CGSL=0e-12 CGDO=4.3e-12 CGDL=108e-12
+ CJ=0 CF=0 CKAPPA=2.2
* Temperature parameters
+ KT1=-2.06 KT2=0 UA1=1.3e-9
+ NJ=10)
.ENDS
*$
