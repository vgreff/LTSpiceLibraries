* PSpice Model Editor - Version 16.3.0
*$
.SUBCKT FDD86102 2 1 3
******************************************************************
**      Fairchild Discrete Modeling Group                       **
******************************************************************
**      Website         www.fairchildsemi.com\models            **
******************************************************************
**      (C) Copyright 2009 Fairchild Semiconductor Corporation  **
**                      All rights reserved                     **
**                                                              **
**                      FDD86102 Spice model                    **
**                      Revision RevA, 1 Apr 2010               **
******************************************************************
*Nom Temp 25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Lgate 1 9 5.083e-9
Ldrain 2 5 0.004e-9
Lsource 3 7 2.29e-9
RLgate 1 9 50.83
RLdrain 2 5 0.04
RLsource 3 7 22.9
Rgate 9 6 1.06
* Shielded gate
D_D1         100 5 D_SG_cap
D_D2         100 101 D_SG_cap
R_R1         101 7  3.37
C_C1         6 101  84e-12
.MODEL D_SG_cap D (IS=1e-9 n=1 RS=4.0e-3 CJO=1.2e-9 M=0.6 t_abs=25)	
It 7 17 1
Ebreak 11 7 17 7 109.5
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=0.66e-3 TC2=-0.6e-6)
.MODEL DbodyMOD D (IS=1.0e-12 n=1 RS=4.6e-3 TRS1=3.0e-3 TRS2=1e-6
+ CJO=0.25e-9 M=0.4 TT=3e-9 XTI=4) ;0.75
.MODEL DbreakMOD D (RS=30e-3 TRS1=1e-3 TRS2=1e-6)
Rsource 7a 7 0.971e-3
Rdrain 5 16 RdrainMOD 14e-3
.MODEL RdrainMOD RES (TC1=6.7e-3 TC2=14e-6)
M_BSIM3 16 6 7a 7a Bsim3 W=1.7 L=4.85e-6 NRS=0 NRD=0
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
*Process Parameters
+ TOX=1000e-10 ;Oxide thickness
+ XJ=0.62e-6 ;Channel depth
+ NCH=1.2e17 ;Channel concentration
*Channel Current
+ U0=840 VSAT=500000 DROUT=1.8
+ DELTA=0.16 PSCBE2=0.00001 RSH=0.971e-3
*Threshold voltage
+ VTH0=3.33
*Sub-threshold characteristics
+ VOFF=-0.1 NFACTOR=1
*Junction diodes and Capacitance
+ LINT=2.025e-6 DLC=2.025e-6
+ CGSO=196e-12 CGSL=0 CGDO=1e-12 CGDL=165e-12 ;245
+ CJ=0 CF=0 CKAPPA=1.0
* Temperature parameters
+ KT1=-1.8 KT2=0 UA1=3.95e-9
+ NJ=10)
.ENDS FDD86102
*$
