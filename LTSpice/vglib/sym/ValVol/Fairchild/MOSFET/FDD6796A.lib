.SUBCKT FDD6796A 2 1 3
*Nom Temp 25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Lgate 1 9 4.417e-9
Ldrain 2 5 0.1e-9
Lsource 3 7 2.213e-9
RLgate 1 9 44.17
RLdrain 2 5 1
RLsource 3 7 22.13
Rgate 9 6 1.62
It 7 17 1
Ebreak 11 7 17 7 27
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=0.75e-3 TC2=-5e-7)
.MODEL DbodyMOD D (IS=1.0e-12 n=1 RS=2.7e-3 TRS1=2.5e-3 TRS2=1e-6
+ CJO=5.3e-11 M=0.3 TT=0.1e-9 XTI=3)
.MODEL DbreakMOD D (RS=30e-3 TRS1=1e-3 TRS2=1e-6 )
Rsource 7a 7 1.18e-3
Rdrain 5 16 RdrainMOD 2.7e-3
.MODEL RdrainMOD RES (TC1=4.7e-3 TC2=7.0e-6)
M_BSIM3 16 6 7a 7a Bsim3 W=2.627 L=1.25e-6 NRS=0 NRD=0
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
*Process Parameters
+ TOX=450e-10 ;Oxide thickness
+ XJ=0.16e-6 ;Channel depth
+ NCH=1.8e17 ;Channel concentration
*Channel Current
+ U0=1000 VSAT=500000 DROUT=1.2
+ DELTA=0.5 PSCBE2=0.00001 RSH=1.18e-3
*Threshold voltage
+ VTH0=1.75
*Sub-threshold characteristics
+ VOFF=-0.1 NFACTOR=1.1
*Junction diodes and Capacitance
+ LINT=0.335e-6 DLC=0.335e-6
+ CGSO=34e-12 CGSL=0e-12 CGDO=50e-12 CGDL=200e-12
+ CJ=0 CF=0 CKAPPA=3.5
*Temperature parameters
+ KT1=-1.1 KT2=0 UA1=3e-9
+ NJ=10)
* Rev_A		SP	10/16/2008
.ENDS

