**************** Power Discrete MOSFET Electrical Circuit Model *****************
* Product Name: FDA18N50
* 500V N-Channel MOSFET and TO-3PN
*--------------------------------------------------------------------------------
.SUBCKT FDA18N50 20 10 30
Rg 10 1 1.58
M1 2 1 3 3 DMOS L=1u W=1u
.MODEL DMOS NMOS(VTO={5.25*{-0.001*TEMP+1.025}} KP={{-0.02*TEMP}+25}
+ THETA=0.032 VMAX=8.0E4 LEVEL=3)
Cgs 1 3 2175p
Rd 20 4 0.105 TC=0.0097
Dds 3 4 DDS
.MODEL DDS D(BV={500*{0.0013*TEMP+0.9667}} M=0.61 CJO=3590p VJ=0.48)
Dbody 3 20 DBODY
.MODEL DBODY D(IS=1.2E-12 N=1.0 RS=0.0015 EG=1.16 TT=420n)
Ra 4 2 0.105 TC=0.0097
Rs 3 5 0.0075
Ls 5 30 2.3n
M2 1 8 6 6 INTER
E2 8 6 4 1 2
.MODEL INTER NMOS (VTO=0 KP=10 LEVEL=1)
CGDmax 7 4 1240p
Rcgd 7 4 1E7
Dgd 6 4 DGD
Rdgd 4 6 1E7
.MODEL DGD D(M=0.68 CJO=1240p VJ=0.42)
M3 7 9 1 1 INTER
E3 9 1 4 1 -2
.ENDS FDA18N50
******************** Power Discrete MOSFET Thermal Model ************************
* Product Name: FDA18N50
* 500V N-Channel MOSFET and TO-3PN
*--------------------------------------------------------------------------------
.SUBCKT FDA18N50_Thermal TH TL
CTHERM1 TH 6 1.24e-4
CTHERM2 6 5  3.08e-3
CTHERM3 5 4  1.34e-2
CTHERM4 4 3  2.62e-2
CTHERM5 3 2  4.92e-1
CTHERM6 2 TL 5.82e-1
RTHERM1 TH 6 3.50e-3
RTHERM2 6 5  6.80e-3
RTHERM3 5 4  1.10e-2
RTHERM4 4 3  1.45e-1
RTHERM5 3 2  1.72e-1
RTHERM6 2 TL 1.82e-1
.ENDS FDA18N50_Thermal
*--------------------------------------------------------------------------------
* Creation: Jan.-29-2008   Rev.: 0.0
* Fairchild Semiconductor

