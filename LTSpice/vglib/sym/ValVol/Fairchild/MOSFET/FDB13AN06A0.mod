.SUBCKT FDB13AN06A0 2 1 3      
*Spice model for FDB13AN06A0 & FDP13AN06A0
*Nom Temp=25 deg C 
*August 22 2002

Ca 12 8 5.1e-10
Cb 15 14 5.1e-10
Cin 6 8 1.3e-9

Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Dplcap 10 5 DplcapMOD

Ebreak 11 7 17 18 65.40
Eds 14 8 5 8 1
Egs 13 8 6 8 1
Esg 6 10 6 8 1
Evthres 6 21 19 8 1
Evtemp 20 6 18 22 1

It 8 17 1

Lgate 1 9 6.9e-9
Ldrain 2 5 1.0e-9
Lsource 3 7 2.91e-9

RLgate 1 9 69
RLdrain 2 5 10
RLsource 3 7 29.1

Mmed 16 6 8 8 MmedMOD
Mstro 16 6 8 8 MstroMOD 
Mweak 16 21 8 8 MweakMOD    

Rbreak 17 18 RbreakMOD 1
Rdrain 50 16 RdrainMOD 3.0e-3
Rgate 9 20 3.77
RSLC1 5 51 RSLCMOD 1e-6
RSLC2 5 50 1e3
Rsource 8 7 RsourceMOD 5.5e-3
Rvthres 22 8 RvthresMOD 1
Rvtemp 18 19 RvtempMOD 1
S1a 6 12 13 8 S1AMOD
S1b 13 12 13 8 S1BMOD
S2a 6 15 14 13 S2AMOD
S2b 13 15 14 13 S2BMOD

Vbat 22 19 DC 1

ESLC 51 50  VALUE={(V(5,51)/ABS(V(5,51)))*(PWR(V(5,51)/(1e-6*160),6))}

.MODEL DbodyMOD D (IS=1.5E-11 N=1.08 RS=3.3e-3 TRS1=2.2e-3 TRS2=2.5e-9
+ CJO=.9e-9 M=5.1e-1 TT=1e-9 XTI=3.9)
.MODEL DbreakMOD D (RS=1.5e-1 TRS1=1e-3 TRS2=-8.9e-6)
.MODEL DplcapMOD D (CJO=4.1e-10 IS=1e-30 N=10 M=0.45)

.MODEL MmedMOD NMOS (VTO=3.5 KP=6 IS=1e-30 N=10 TOX=1 L=1u W=1u RG=3.77)
.MODEL MstroMOD NMOS (VTO=4.3 KP=50 IS=1e-30 N=10 TOX=1 L=1u W=1u)
.MODEL MweakMOD NMOS (VTO=2.88 KP=0.05 IS=1e-30 N=10 TOX=1 L=1u W=1u RG=3.77e+1 RS=0.1) 

.MODEL RbreakMOD RES (TC1=9e-4 TC2=-5e-7)
.MODEL RdrainMOD RES (TC1=1.5e-2 TC2=4e-5)
.MODEL RSLCMOD RES (TC1=1.8e-3 TC2=1.7e-5)
.MODEL RsourceMOD RES (TC1=1e-3 TC2=1e-6)
.MODEL RvthresMOD RES (TC1=-5.3e-3 TC2=-1.0e-5)
.MODEL RvtempMOD RES (TC1=-2.5e-3 TC2=1e-6)

.MODEL S1AMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-5 VOFF=-2)
.MODEL S1BMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-2 VOFF=-5)
.MODEL S2AMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-1.5 VOFF=.5)
.MODEL S2BMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=.5 VOFF=-1.5)
.ENDS


.SUBCKT FDB13AN06A0_Thermal TH TL
*Thermal Model Subcircuit
*08/22/02
CTHERM1 TH 6 9.7e-4
CTHERM2 6 5 6.2e-3
CTHERM3 5 4 4.6e-3
CTHERM4 4 3 4.9e-3
CTHERM5 3 2 8e-3
CTHERM6 2 TL 4.2e-2

RTHERM1 TH 6 5.24e-2
RTHERM2 6 5 10.08e-2
RTHERM3 5 4 4.28e-1
RTHERM4 4 3 1.8e-1
RTHERM5 3 2 1.9e-1
RTHERM6 2 TL 2.1e-1
.ends

