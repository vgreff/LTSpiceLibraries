* PSpice Model Editor - Version 16.0.0
*$
.SUBCKT FDD6780A 2 1 3
******************************************************************
**      Fairchild Discrete Modeling Group                       **
******************************************************************
**      Website         www.fairchildsemi.com\models            **
**      Scott Pearson   scott.pearson@fairchildsemi.com         **
**      Sylvie Tran     sylvie.tran@fairchildsemi.com           **
******************************************************************
**      (C) Copyright 2008 Fairchild Semiconductor Corporation  **
**                      All rights reserved                     **
**                                                              **
**                      FDD6780A Spice model                    **
**                      Revision RevA, 25 Feb 2009 (ST)         **
******************************************************************
*Nom Temp=25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Lgate 1 9 4.4e-9
Ldrain 2 5 0.1e-9
Lsource 3 7 2.567e-9
RLgate 1 9 44.06
RLdrain 2 5 1
RLsource 3 7 25.66
Rgate 9 6 1.2
It 7 17 1
Ebreak 11 7 17 7 27
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=0.7e-3 TC2=-1e-6)
.MODEL DbodyMOD D (IS=1.49e-12 n=1 RS=5.2e-3 TRS1=1.5e-3 TRS2=0
+ CJO=0.17e-9 M=0.35 TT=0.1e-9 XTI=1.5)
.MODEL DbreakMOD D (RS=30e-3 TRS1=1e-3 TRS2=0)
Rsource 7a 7 1.38e-3
Rdrain 5 16 RdrainMOD 4.58e-3
.MODEL RdrainMOD RES (TC1=5e-3 TC2=3.2e-6)
M_BSIM3 16 6 7a 7a Bsim3 W=1.683 L=1.25e-6 NRS=0 NRD=0
.MODEL Bsim3 NMOS(LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0)
*Process Parameters
+ TOX=450e-10 ;Oxide thickness
+ XJ=0.16e-6 ;Channel depth
+ NCH=1.9e17 ;Channel concentration
*Channel Current
+ U0=1000 VSAT=5e5 DROUT=1.2
+ DELTA=0.1 PSCBE2=1e-5 RSH=1.38e-3  
*Threshold voltage
+ VTH0=1.9
*Sub-threshold characteristics
+ VOFF=-.2 NFACTOR=1.1
*Junction diodes and Capacitance
+ LINT=0.335e-6 DLC=0.335e-6 
+ CGSO=128e-12 CGSL=0e-12 CGDO=52e-12 CGDL=196e-12
+ CJ=0 CF=0 CKAPPA=3.5
* Temperature parameters
+ KT1=-1 KT2=0 UA1=3e-10
+ NJ=10)
.ENDS
*$
