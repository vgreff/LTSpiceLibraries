* PSpice Model Editor - Version 10.5.0
*$
.SUBCKT FDD3860 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Lgate 1 9 5.132e-9
Ldrain 2 5 1.44e-9
Lsource 3 7 2.29e-9
RLgate 1 9 51.32
RLdrain 2 5 14.4
RLsource 3 7 22.9
Rgate 9 6 1.6
It 7 17 1
Ebreak 11 7 17 7 110
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=9.2e-4 TC2=-1e-6)
.MODEL DbodyMOD D (IS=1.7e-12 n=1 RS=4.3e-3 TRS1=2.6e-3 TRS2=1e-6
+ CJO=0.72e-9 M=0.55 TT=32e-9 XTI=1.5)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1e-3 TRS2=1e-6)
Rdrain 5 16 RdrainMOD 22.1e-3
.MODEL RdrainMOD RES (TC1=7e-3 TC2=20e-6)
M_BSIM3 16 6 7 7 Bsim3 W=2.9 L=1.5e-6 NRS=0 NRD=0
.MODEL Bsim3 NMOS(LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0)
**Process Parameters
+ TOX=1060e-10 ;Oxide thickness
+ XJ=0.9e-6 ;Channel depth
+ NCH=1.6e17 ;Channel concentration
**Channel Current
+ U0=700 VSAT=1e5 DROUT=1.5
+ DELTA=0.1 PSCBE2=1e-5 RSH=0.65e-3   
**Threshold voltage
+ VTH0=3
**Sub-threshold characteristics
+ VOFF=-.1 NFACTOR=1.1
**Junction diodes and Capacitance
+ LINT=0.075e-6 DLC=0.075e-6 
+ CGSO=92p CGSL=0 CGDO=15p CGDL=98p
+ CJ=0 CF=0 CKAPPA=1
** Temperature parameters
+ KT1=-2.8 KT2=0 UA1=1.2e-9
+ NJ=10)
.ENDS
*FDD3860(Rev.A) 1/25/2008
*ST
*$
