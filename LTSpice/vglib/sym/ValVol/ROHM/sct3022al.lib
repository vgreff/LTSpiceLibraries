* SCT3022AL
* SiC NMOSFET model
* 650V 93A 22mOhm
* Model Generated by ROHM
* All Rights Reserved
* Commercial Use or Resale Restricted
* Date: 2017/03/03
******************D G S
.SUBCKT SCT3022AL 1 2 3
.PARAM T0=25
.FUNC R1(I)     {15.89m*I*EXP((TEMP-T0)/255.5)+1.274u*I*ABS(I)**1.575*EXP((TEMP-T0)/-2495)}
.FUNC R2(I)     {I*(1.0*(1+TANH(I))/2+6.6*(1-TANH(I))/2)}
.FUNC V1(V,W)   {V-738.2m*ASINH(W/5.716)*EXP((TEMP-T0)/-1443)-
+                204.0m*ASINH(W/0.1115)*EXP((TEMP-T0)/-312.9)-13.55m*W*EXP((TEMP-T0)/378.6)}
.FUNC V2(V)     {421.3f*V**(16.59*EXP((TEMP-T0)/-5120))*EXP((TEMP-T0)/26.92*EXP((TEMP-T0)/-1531))}
.FUNC V3(V)     {225.3*SINH(V/225.3)*EXP((TEMP-T0)/12585)}
.FUNC I1(V,W)   {V3(V)*(1+2.078*(TANH((V3(V)-40.08)/78.25)+1)*EXP((TEMP-T0)/-155.8*EXP((TEMP-T0)/-326.5))/10)*
+                W/(ABS(W)+2.078*(TANH((V3(V)-40.08)/78.25)+1)*EXP((TEMP-T0)/-155.8*EXP((TEMP-T0)/-326.5)))}
.FUNC C1(U,V,W) {(1028*V+1971*(1-W/0.8431)**-0.4397)*(0.2108*TANH((U+7.666)/3.268)+0.7892)}
V1  1 11 0
E1 11 12 VALUE={R1(MIN(MAX(I(V1),-5k),5k))}
V2  2 21 0
E2 21 22 VALUE={R2(I(V2))}
L1  3 32 4.5n
R2  3 32 10
E3 41  0 VALUE={V1(MIN(MAX(V(22,32),0),22),MIN(MAX(V(42),0),1.2k))}
E4 42  0 VALUE={V2(MIN(MAX(V(41),0),20))}
G1 12 32 VALUE={I1(MIN(MAX(V(42),0),1.2k),V(12,32))}
R3 12 32 1T
V3 22 23 0
C1 23 12 1p
G2 22 12 VALUE={I(V3)*C1(V(22,12),MIN(MAX(V(22,12),0),0.593),MIN(V(22,12),0))}
C2 22 32 2.183n
R4 22  3 1G
********* ********* ********* ********* ********* ********* ********* ********* ********* ********* *********
.FUNC R11(I)   {79.18m*ASINH(I/0.5202)*EXP((TEMP-T0)/217.9)+22.43m*I*EXP((TEMP-T0)/599.6)}
.FUNC I11(V)   {30.71n*(EXP(V/0.09265/EXP((TEMP-T0)/-302.0))-1)*EXP((TEMP-T0)/-65.69*EXP((TEMP-T0)/-842.5))-
+               1u*TANH(-V/0.1)*EXP((TEMP-T0)/180.3)-231.9E-21*(EXP(-V/25)-1)*EXP((TEMP-T0)/-200)}
.FUNC C11(V,W) {1650*(V-0.8682)+1966*(1-W/1.736)**-0.5112}
V11 32 51 0
E11 51 52 VALUE={R11(MIN(MAX(I(V11),-400k),600k))}
V12 52 53 0
C11 53  1 1p
G11 52  1 VALUE={I11(MIN(MAX(V(52,1),-2k),5))+I(V12)*C11(MAX(V(52,1),0.8682),MIN(V(52,1),0.8682))}
R11 52  1 1T
.ENDS SCT3022AL