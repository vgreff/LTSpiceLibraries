* 09.02.2017
* UCC25600
* 8-Pin High-Performance Resonant Mode Controller
.subckt UCC25600 DT RT OC SS GD2 GRD VCC GD1
A1 N002 GRD N003 GRD GRD N002 N005 GRD DFLOP Vhigh=5 Trise=10n
A2 N003 GRD GRD GRD N005 N004 GRD GRD OR Vhigh=5 Trise=10n
A3 N002 GRD GRD GRD N003 N013 GRD GRD OR Vhigh=5 Trise=10n
V2 RT N016 2.5
B1 GRD N018 I=if(V(osco)/5,-I(R3),I(R3))
A5 N018 GRD GRD GRD GRD GRD osco GRD SCHMITT Vhigh=5 Trise=10n Vt=2.5 Vh=2.5
C1 N018 GRD 600p ic=0
G1 GRD N017 GRD N016 table=(0.49m 0.49m,5m 5m)
R2 N016 GRD 1
R3 N017 GRD 1
I1 GRD N017 1.81m
G2 N017 GRD SS GRD table=(1.2 0.543m,4 1.81m)
V4 DT N007 2.25
G3 GRD N008 GRD N007 table=(0 0,458u 458u)
R5 N007 GRD 1
D1 N008 osco Ideal
C2 N008 GRD 23p ic=0
A4 N008 GRD GRD GRD GRD N009 GRD GRD BUF Vhigh=5 Trise=10n
A6 N009 GRD GRD GRD osco GRD N003 GRD AND Vhigh=5 Trise=10n
G4 GRD SS N021 GRD 34u
I2 GRD SS 5u
A7 SS GRD GRD GRD GRD N021 N010 GRD SCHMITT Vhigh=5 Trise=10n Vt=1.1 Vh=0.1
A8 fault GRD GRD GRD N010 N022 GRD GRD OR Vhigh=5 Trise=10n
A9 fault N022 GRD GRD GRD GRD N020 GRD SRFLOP Vhigh=5 Trise=10n
S1 GRD SS N020 GRD SS
D2 GRD SS Z6
A10 VCC GRD GRD GRD GRD GRD OV GRD SCHMITT Vhigh=5 Trise=10n Vt=19 Vh=1
A12 OC GRD GRD GRD GRD GRD OVC GRD SCHMITT Vhigh=5 Trise=10n Td=200n Vt=0.8 Vh=0.2
A13 OC GRD GRD GRD GRD GRD OC_latch GRD SCHMITT Vhigh=5 Trise=10n Td=200n Vt=1.8 Vh=0.2
A11 VCC GRD GRD GRD GRD UVLO GRD GRD SCHMITT Vhigh=5 Trise=10n Vt=10 Vh=0.5
A14 OV UVLO OVC N023 GRD ifault fault GRD OR Vhigh=5 Trise=10n
A15 N024 GRD N013 fault GRD N012 GRD GRD DFLOP Vhigh=5 Trise=10n
R6 GRD N024 1k
A16 N004 GRD N006 N010 N012 N011 N001 GRD AND Vhigh=5 Trise=10n
A17 N013 GRD N006 N010 N012 N015 N014 GRD AND Vhigh=5 Trise=10n
R7 OC GRD 4meg
S2 GD1 VCC N001 GRD HOUT
S3 GRD GD1 N011 GRD LOUT
S4 GD2 VCC N014 GRD HOUT
S5 GRD GD2 N015 GRD LOUT
A18 UVLO OC_latch GRD GRD GRD N023 GRD GRD SRFLOP Vhigh=5 Trise=10n
R1 VCC GRD 70k
F1 VCC GRD V2 -1
F2 VCC GRD V4 -1
S6 GRD N019 ifault GRD SS
R4 N019 VCC 16k
R8 N018 GRD 1g
A19 N017 0 0 0 0 N006 0 0 SCHMITT Vhigh=5 Trise=10n Vt=4.08m Vh=0.12m
C3 SS GRD 10p ic=0
.model Ideal D(Ron=50 Roff=1g Vfwd=0)
.model SS SW(Ron=50 Roff=1g Vt=2.5 Vh=-0.5)
.model Z6 D(Ron=50 Roff=1g Vrev=6 )
.model HOUT SW(Ron=12 Roff=250k Vt=2.5 Vh=-0.5)
.model LOUT SW(Ron=4 Roff=250k Vt=2.5 Vh=-0.5)
.end UCC25600
