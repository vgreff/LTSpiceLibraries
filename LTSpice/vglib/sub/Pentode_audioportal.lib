.SUBCKT 6J43P A S G K 
* Pins A Anode
* S Screen
* G Grid
* K Cathode
*
* Calculate contribution to cathode current
*
Bat at 0 V=0.636*ATAN(V(A,K)/23)
Bgs gs2 0 V=(URAMP(V(S,K)/41+V(G,K)))**1.8
Bcath cc 0 V=V(gs2)*V(at)
*
* Calculate anode current
*
Ba A K I=13E-3*V(cc)
*
* Calculate screen current
*
Bscrn sc 0 V=V(gs2)*(1.1-V(at))
Bs S K I=1.E-3*V(sc)
*
* Grid current (approximation - does not model low va/vs)
*
Bg G K I=(URAMP(V(G,K)+1)**1.5)*50E-6+V(G,K)/30Meg
*
* Capacitances
*
Cg1 G K 13.5p
Cak A K 3.3p
Cg1a G A 0.035p
Cs S G 1p
.ENDS
