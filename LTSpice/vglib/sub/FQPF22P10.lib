* FQPF22P10 100V P-CHANNEL DMOSFET ELECTRICAL PARAMETERS
*------------------------------------------------------------------------------------
.SUBCKT  FQPF22P10   20  10  30
Rg     10    1    1
M1      2    1    3    3    DMOS    L=1u   W=1u
.MODEL DMOS PMOS (VTO={-3.53*{-0.00088*TEMP+1.022}} KP={14.5*{-0.000075*TEMP+1.001875}} 
+ THETA=0.056    VMAX=1.4E5   LEVEL=3)
Cgs     1    3    1010P
Rd     20    4    75m   TC=0.008
Dds     4    3    DDS
.MODEL DDS   D(BV={100*{0.0008*TEMP+0.98}}  M=0.5      CJO=300p   VJ=0.8)
Dbody  20    3    DBODY
.MODEL DBODY D(IS=2.2E-12    N=1     RS=18.3m    EG=1.11    TT=110n)
Ra      4    2    8.0m   TC=0.008
*Rs      3    5    1.2m    
Ls      3   30    2.3n rpar=5 rser=1.5m
M2      1    8    6    6    INTER
E2      8    6    4    1    2
.MODEL INTER PMOS (VTO=0   KP=10   LEVEL=1)
Cgdmax  7    4    2795P
Rcgd    7    4    10meg
Dgd     4    6    DGD
Rdgd    4    6    10meg
.MODEL DGD D(M=0.85   CJO=2795P    VJ=0.45)
M3      7    9    1    1    INTER
E3      9    1    4    1    -2
.ENDS FQPF22P10
*------------------------------------------------------------------------------------
*FAIRCHILD      CASE: TO-220F      PID: FQPF22P10
    
