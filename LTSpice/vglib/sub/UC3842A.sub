.subckt uc3842a Err Vfb Ifb Osc Com Out Vcc Ref
R1 Vcc Ri 1e7
D1 COM Vcc 34V
A1 Vcc COM COM COM COM COM Ri COM SCHMITT Vt=13 Vh=3 Vhigh=5 Rout=4k6 tau=1u tripdt=10n td=3n
I1 Ri COM PULSE(1m1 0 0 1n 1n 1k 2k)
S1 Ro Vcc Ri Ro ref off
D2 Ro Vcc V65
C1 Ro 2v5 5p Rpar=230
C2 2v5 COM 5p Rpar=230
R2 Ref Ro 0.3
C3 COM Ref 10p
C4 Osc COM 5p Rpar=1e7
G1 Osc COM S 0 8.3m
A2 Osc COM 0 0 0 0 S 0 SCHMITT Vt=1.8 Vh=0.81 trise=5n tripdt=1n td=3n
C5 Vfb COM 1p
R3 3 Vfb 165k
C6 Err 3 1p Rpar=10G
A3 3 2v5 COM COM COM COM Err COM OTA Vhigh=6 Vlow=0.7 Isrc=1m Isink=-6m Rclamp=1 g=5 Rout=25k Cout=5p powerup=!V(fault,Com)
C7 Ifb COM 5p
R4 I+ Ifb 75k
C8 I+ COM 1p
D3 Err I- 2R
D4 I- COM 1R
C9 I- COM 0.1p Rpar=1e6
A4 I+ I- 0 0 0 0 R 0 SCHMITT Vt=1m Vh=1m tripdt=10n trise=100n td=3n
A5 Ref COM 0 0 0 fault 0 0 SCHMITT Vt=4.8 Vh=.1 trise=200n tripdt=20n td=3n
A6 fault 0 0 0 S 0 inh 0 OR trise=100n tripdt=20n td=3n
A7 S R 0 0 0 0 Q 0 SRFLOP td=10n trise=200n ic=0 tripdt=20n
S2 Out Vcc Q inh Ho off
S3 Com Out inh Q Lo off
C10 Vcc COM 20p
C11 Out Com 20p
*A8 0 0 0 0 0 0 0 0 Counter cycles=2 tripdt=20n
.model 2R d Vfwd=1.3 Ron=2e6
.model 1R d Vfwd=1 Ron=1 Epsilon=50m
.model Ho sw Vt= 0.48 Vh=-0.25 Ron=2 Roff=100k Vser=1.2
.model Lo sw Vt=-0.52 Vh=-0.25 Ron=6 Roff=300k
.model V65 d Vfwd=0.65 Epsilon=0.1 Ron=1
.model ref sw Vt=0 Vh=-1m Ilimit=0.1 Ron=1
+ Roff=1e7 Level=2 Epsilon=0.1
.model 34V d
+ Is=0m2 Rs=1
+ Cjo=0n2 M=0.33 tt=1u
+ Bv=32 Ibv=1u Nbv=8
.ends uc3842a
