* Source File: 74HC4051_Blk.asc
* Developed: ETech (eetech00@yahoo.com)
* Created: Mar 22 2023
* Revision: NA
*
* This spice model was tested with LTSpice. Temperature is not modeled.
*
*******************************************
*** NXP/Philips 8-Channel Analog Multiplexer/Demultiplexer
*** DIP-16 Pins: 6  11 10 9  3 13 14 15 12 1  5  2  4  16  7   8
.SUBCKT 74HC4051 Ei S0 S1 S2 Z Y0 Y1 Y2 Y3 Y4 Y5 Y6 Y7 VCC VEE VGND
XU1 S0P S0i VCC VGND HC_INP2N
XU4 S0i LS0 VEE VCC VGND HC_LLCN
XU8 Y0i Y0P ZP VEE VCC HC_SWITCH1N
XU2 S1P S1i VCC VGND HC_INP2N
XU5 S1i LS1 VEE VCC VGND HC_LLCN
XU9 Y1i Y1P ZP VEE VCC HC_SWITCH1N
XU3 S2P S2i VCC VGND HC_INP2N
XU6 S2i LS2 VEE VCC VGND HC_LLCN
XU10 Y2i Y2P ZP VEE VCC HC_SWITCH1N
XU11 Y3i Y3P ZP VEE VCC HC_SWITCH1N
XX1 LSE LS0 LS1 LS2 Y0i Y1i Y2i Y3i Y4i Y5i Y6i Y7i VCC VEE HC4051_1OF8DECODER
XU12 EiP En VCC VGND HC_INP2N
XU13 En LSE VEE VCC VGND HC_LLCN
XU14 Y4i Y4P ZP VEE VCC HC_SWITCH1N
XU15 Y5i Y5P ZP VEE VCC HC_SWITCH1N
XU16 Y6i Y6P ZP VEE VCC HC_SWITCH1N
XU17 Y7i Y7P ZP VEE VCC HC_SWITCH1N
XX3 Y0P Y0 VCC VEE HC_OUTPROT
XX4 Y1P Y1 VCC VEE HC_OUTPROT
XX5 Y2P Y2 VCC VEE HC_OUTPROT
XX6 Y3P Y3 VCC VEE HC_OUTPROT
XX2 Y4P Y4 VCC VEE HC_OUTPROT
XX12 Y5P Y5 VCC VEE HC_OUTPROT
XX13 Y6P Y6 VCC VEE HC_OUTPROT
XX14 Y7P Y7 VCC VEE HC_OUTPROT
XX15 ZP Z VCC VEE HC_OUTPROT
XX7 S0 S0P VCC VGND HC_INPROT
XX8 S1 S1P VCC VGND HC_INPROT
XX9 S2 S2P VCC VGND HC_INPROT
XX10 Ei EiP VCC VGND HC_INPROT
.ENDS 74HC4051

*** 1-of-8 Decoder ***
.SUBCKT HC4051_1OF8DECODER Ei A0 A1 A2 Y0 Y1 Y2 Y3 Y4 Y5 Y6 Y7 VCC VEE
XU1 A0i A1i A2i En Y0i VCC VEE HC_AND4
XU2 A0n A1i A2i En Y1i VCC VEE HC_AND4
XU3 A0i A1n A2i En Y2i VCC VEE HC_AND4
XU4 A0n A1n A2i En Y3i VCC VEE HC_AND4
XU5 A0i A1i A2n En Y4i VCC VEE HC_AND4
XU6 A1i A2n A0n En Y5i VCC VEE HC_AND4
XU7 A0i A2n A1n En Y6i VCC VEE HC_AND4
XU8 En A1n A0n A2n Y7i VCC VEE HC_AND4
XU12 A2 A2i VCC VEE HC_INV
XU9 Ei En VCC VEE HC_INV
XU10 A0 A0i VCC VEE HC_INV
XU11 A1 A1i VCC VEE HC_INV
XU13 A0i A0n VCC VEE HC_INV
XU14 A1i A1n VCC VEE HC_INV
XU15 A2i A2n VCC VEE HC_INV
XU16 Y0i Y0 VCC VEE HC_INV
XU17 Y1i Y1 VCC VEE HC_INV
XU18 Y2i Y2 VCC VEE HC_INV
XU19 Y3i Y3 VCC VEE HC_INV
XU20 Y4i Y4 VCC VEE HC_INV
XU21 Y5i Y5 VCC VEE HC_INV
XU22 Y6i Y6 VCC VEE HC_INV
XU23 Y7i Y7 VCC VEE HC_INV
.ENDS HC4051_1OF8DECODER

********************************************
* Source File: 74HC4052_Blk.asc
* Developed: ETech (eetech00@yahoo.com)
* Created: Mar 22 2023
* Revision: NA
*
* This spice model was tested with LTSpice. Temperature is not modeled.
*
********************************************
*** NXP/Philips Dual 4-Channel Analog Multiplexer/Demultiplexer ***
*** DIP-16 Pins: 6  10 9  12  14  15  11  13 1   5   2   4   3  16  7   8 ***
.SUBCKT 74HC4052 Ei S0 S1 1Y0 1Y1 1Y2 1Y3 1Z 2Y0 2Y1 2Y2 2Y3 2Z VCC VEE VGND
XX13 S0 S0P VCC VGND HC_INPROT
XX14 S1 S1P VCC VGND HC_INPROT
XX15 Ei EiP VCC VGND HC_INPROT
XX1 EiP S0P S1P 1Y0P 1Y1P 1Y2P 1Y3P 1ZP VCC VEE VGND HC4052_CHANNEL
XX7 EiP S0P S1P 2Y0P 2Y1P 2Y2P 2Y3P 2ZP VCC VEE VGND HC4052_CHANNEL
XX2 1ZP 1Z VCC VEE HC_OUTPROT
XX3 1Y0P 1Y0 VCC VEE HC_OUTPROT
XX4 1Y1P 1Y1 VCC VEE HC_OUTPROT
XX5 1Y2P 1Y2 VCC VEE HC_OUTPROT
XX6 1Y3P 1Y3 VCC VEE HC_OUTPROT
XX8 2ZP 2Z VCC VEE HC_OUTPROT
XX9 2Y0P 2Y0 VCC VEE HC_OUTPROT
XX10 2Y1P 2Y1 VCC VEE HC_OUTPROT
XX11 2Y2P 2Y2 VCC VEE HC_OUTPROT
XX12 2Y3P 2Y3 VCC VEE HC_OUTPROT
.ENDS 74HC4052

*** Model for use with Single 4-Channel symbol ***
.SUBCKT 74HC4052_1 Ei S0 S1 Y0 Y1 Y2 Y3 Z VCC VEE VGND
XX13 S0 S0P VCC VGND HC_INPROT
XX14 S1 S1P VCC VGND HC_INPROT
XX15 Ei EiP VCC VGND HC_INPROT
XX1 EiP S0P S1P Y0P Y1P Y2P Y3P ZP VCC VEE VGND HC4052_CHANNEL
XX2 ZP Z VCC VEE HC_OUTPROT
XX3 Y0P Y0 VCC VEE HC_OUTPROT
XX4 Y1P Y1 VCC VEE HC_OUTPROT
XX5 Y2P Y2 VCC VEE HC_OUTPROT
XX6 Y3P Y3 VCC VEE HC_OUTPROT
.ENDS 74HC4052_1

*** internal single channel ***
.SUBCKT HC4052_CHANNEL Ei S0 S1 1Y0 1Y1 1Y2 1Y3 1Z VCC VEE VGND
XU1 S1 S1i VCC VGND HC_INP2N
XU4 S1i LS1 VEE VCC VGND HC_LLCN
XU8 Y0i 1Y0 1Z VEE VCC HC_SWITCH1N
XU2 S0 S0i VCC VGND HC_INP2N
XU5 S0i LS0 VEE VCC VGND HC_LLCN
XU9 Y1i 1Y1 1Z VEE VCC HC_SWITCH1N
XU3 Ei En VCC VGND HC_INP2N
XU6 En LSE VEE VCC VGND HC_LLCN
XU10 Y2i 1Y2 1Z VEE VCC HC_SWITCH1N
XU11 Y3i 1Y3 1Z VEE VCC HC_SWITCH1N
XU7 LSE LS1 LS0 Y0i Y1i Y2i Y3i VCC VEE HC4052_1OF4DECODER
.ends HC4052_CHANNEL

*** 1-of-4 Decoder ***
.SUBCKT HC4052_1OF4DECODER Ei S1 S0 Y0i Y1i Y2i Y3i VCC VEE
XU1 S0i S0n VCC VEE HC_INV
XX1 S0i S1i En Y0i VCC VEE HC_NAND3
XX2 S0n S1i En Y1i VCC VEE HC_NAND3
XX3 S0i S1n En Y2i VCC VEE HC_NAND3
XX4 S0n S1n En Y3i VCC VEE HC_NAND3
XU2 S1i S1n VCC VEE HC_INV
XU3 S0 S0i VCC VEE HC_INV
XU4 S1 S1i VCC VEE HC_INV
XU5 Ei En VCC VEE HC_INV
.ENDS HC4052_1OF4DECODER

********************************************
* Source File: 74HC4053_Blk.asc
* Developed: ETech (eetech00@yahoo.com)
* Created: Mar 22 2023
* Revision: NA
*
* This spice model was tested with LTSpice. Temperature is not modeled.
*
********************************************
*** NXP/Philips Triple Channel Analog Multiplexer/Demultiplexer
*** DIP-16 Pins: 6  11 10 9  12  13  14 2   1   15 5   3   4  16  7   8
.SUBCKT 74HC4053 Ei S1 S2 S3 1Y0 1Y1 1Z 2Y0 2Y1 2Z 3Y0 3Y1 3Z VCC VEE VGND
XX4 S1 S1P VCC VGND HC_INPROT
XX5 S2 S2P VCC VGND HC_INPROT
XX6 S3 S3P VCC VGND HC_INPROT
XX7 Ei EiP VCC VGND HC_INPROT
XX1 S1P EiP 1ZP 1Y1P 1Y0P VEE VCC VGND HC4053_CHANNEL
XX2 S2P EiP 2ZP 2Y1P 2Y0P VEE VCC VGND HC4053_CHANNEL
XX3 S3P EiP 3ZP 3Y1P 3Y0P VEE VCC VGND HC4053_CHANNEL
XX8 1ZP 1Z VCC VEE HC_OUTPROT
XX9 1Y1P 1Y1 VCC VEE HC_OUTPROT
XX10 1Y0P 1Y0 VCC VEE HC_OUTPROT
XX11 2ZP 2Z VCC VEE HC_OUTPROT
XX12 2Y1P 2Y1 VCC VEE HC_OUTPROT
XX13 2Y0P 2Y0 VCC VEE HC_OUTPROT
XX14 3ZP 3Z VCC VEE HC_OUTPROT
XX15 3Y1P 3Y1 VCC VEE HC_OUTPROT
XX16 3Y0P 3Y0 VCC VEE HC_OUTPROT
.ENDS 74HC4053

*** Model for use with Single Channel symbol ***
.SUBCKT 74HC4053_1 Ei S Y0 Y1 Z VCC VEE VGND
XX1 S SP VCC VGND HC_INPROT
XX2 Ei EiP VCC VGND HC_INPROT
XX3 SP EiP ZP Y1P Y0P VEE VCC VGND HC4053_CHANNEL
XX4 ZP Z VCC VEE HC_OUTPROT
XX5 Y1P Y1 VCC VEE HC_OUTPROT
XX6 Y0P Y0 VCC VEE HC_OUTPROT
.ENDS 74HC4053_1

*** internal single channel ***
.SUBCKT HC4053_CHANNEL Si Ei Z Y1 Y0 VEE VCC VGND
XU1 Si Sn VCC VGND HC_INP2N
XU3 Sn LSA VEE VCC VGND HC_LLCN
XU6 Y0S Y0 Z VEE VCC HC_SWITCH1N
XU5 LSA LSE Y0S Y1S VCC VEE HC4053_1OF2DECODER
XU2 Ei En VCC VGND HC_INP2N
XU4 En LSE VEE VCC VGND HC_LLCN
XU7 Y1S Y1 Z VEE VCC HC_SWITCH1N
.ENDS HC4053_CHANNEL

*** 1-of-2 Decoder ***
.SUBCKT HC4053_1OF2DECODER S ENi Y0 Y1 VCC VEE
XU1 S Si VCC VEE HC_INV
XU2 S ENi Y0 VCC VEE HC_OR2
XU3 Si ENi Y1 VCC VEE HC_OR2
.ENDS HC4053_1OF2DECODER

********************************************
* Source File: 74HC4051-53 internal device libraries
* Developed: ETech (eetech00@yahoo.com)
* Created: Mar 22 2023
* Revision: NA
*
* These spice models were tested with LTSpice. Temperature is not modeled.
*
********************************************
*** Input protection 
.SUBCKT HC_INPROT IN OUT VCC VEE
D1 RPD VCC Din
D2 VEE RPD Din
RP RPD IN 100
RD OUT RPD 170
* clamp diode, 0.9v@2mA, Breakdown 20v
.MODEL Din D(Is=2.4684e-07 Rs=81.3627 N=3.18857 CJO=495e-15 BV=20)
.ENDS HC_INPROT

*** Output protection
.SUBCKT HC_OUTPROT IN OUT VCC VEE
D1 OUT VCC Din
D2 VEE OUT Din
R1 OUT IN 1e-3
* clamp diode, 0.9v@2mA, Breakdown 20v
.MODEL Din D(Is=2.4684e-07 Rs=81.3627 N=3.18857 CJO=495e-15 BV=20)
.ENDS HC_OUTPROT

*** INVERTER ***
.SUBCKT HC_INV A Y VCC VEE PARAMS: LP=2.4u WP=4.92u LN=2.4u WN=2.4u M=1
MP1 Y A VCC VCC MHCPEN L=LP W=WP M=M
MN1 Y A VEE VEE MHCNEN L=LN W=WN M=M
.ENDS HC_INV

*** 2-input NOR Gate ***
.SUBCKT HC_NOR2 A B Y VCC VEE PARAMS: LP=3.2u WP=28.16u LN=3.2u WN=3.2u M=1
MP1 NET20 A VCC VCC MHCPEN L=LP W=WP M=M
MP2 Y B NET20 VCC MHCPEN L=LP W=WP M=M
MN1 Y A VEE VEE MHCNEN L=LN W=WN M=M
MN2 Y B VEE VEE MHCNEN L=LN W=WN M=M
.ENDS HC_NOR2

*** 2-input OR Gate ***
.SUBCKT HC_OR2 A B Y VCC VEE
XNOR2 A B Y1 VCC VEE HC_NOR2 
XINV Y1 Y VCC VEE HC_INV
.ENDS HC_OR2

********************************************
*** for 74HC4052
********************************************
*** 3-input NAND Gate ***
.SUBCKT HC_NAND3 A B C Y VCC VEE PARAMS: LP=9.6u WP=3.0u LN=9.6u WN=9.6u M=1
MN1 Y A 10 VEE MHCNEN L=LN W=WN M=M
MP1 VCC C Y VCC MHCPEN L=LP W=WP M=M
MN2 10 B 11 VEE MHCNEN L=LN W=WN M=M
MP2 VCC B Y VCC MHCPEN L=LP W=WP M=M
MN3 11 C VEE VEE MHCNEN L=LN W=WN M=M
MP3 VCC A Y VCC MHCPEN L=LP W=WP M=M
.ENDS HC_NAND3
********************************************

********************************************
*** for 74HC4051
********************************************
*** 4-input NAND Gate ***
.SUBCKT HC_NAND4 A B C D Y VCC VEE PARAMS: LP=19.2u WP=3.2u LN=19.2u WN=19.2u M=1
MN1 Y A 10 VEE MHCNEN L=LN W=WN M=M
MP2 VCC D Y VCC MHCPEN L=LP W=WP M=M
MN3 10 B 11 VEE MHCNEN L=LN W=WN M=M
MP4 VCC C Y VCC MHCPEN L=LP W=WP M=M
MN5 11 C 12 VEE MHCNEN L=LN W=WN M=M
MP6 VCC B Y VCC MHCPEN L=LP W=WP M=M
MN7 12 D VEE VEE MHCNEN L=LN W=WN M=M
MP8 VCC A Y VCC MHCPEN L=LP W=WP M=M
.ENDS HC_NAND4

*** 4-input AND Gate ***
.SUBCKT HC_AND4 A B C D Y VCC VEE
XNAND4 A B C D Y1 VCC VEE HC_NAND4 
XINV Y1 Y VCC VEE HC_INV
.ENDS HC_AND4

********************************************
* HCMOS Subcircuit and Primitive Elements Library
* Standard Logic Product Group
* NXP Semiconductors
* 03/22/2023 - mods by eetech00@yahoo.com
********************************************
*** Input inverting buffer ***
.SUBCKT HC_INP2N  2  3  50  60
*IN=2, OUT=3, VCC=50, GND=60
R1  2  4  100
MP1 4 50 50 50  MHCPEN W= 20U L=2.4U AD=100P AS=100P PD=40U PS= 20U
MN1 4 60 60 60  MHCNEN W= 35U L=2.4U AD=260P AS=260P PD=70U PS= 20U
MP2 3  4 50 50  MHCPEN W=176U L=2.4U AD=580P AS=580P PD=10U PS=200U
MN2 3  4 60 60  MHCNEN W=112U L=2.4U AD=325P AS=580P PD=10U PS=150U
.ENDS HC_INP2N

*** Logic Level Converter ***
.SUBCKT HC_LLCN  2  3  40  50  60
* INA = 2,  OUT = 3,  VEE = 40,  VCC = 50,  GND =  60
MP4 4  2  50  50  MHCPEN W= 30U  L= 2.4U AD=120P AS=120P PD= 40U PS= 30U
MN4 4  2  60  60  MHCNEN W= 15U  L= 2.4U AD= 60P AS= 60P PD= 20U PS= 15U
MP1 5  4  50  50  MHCPEN W=135U  L= 2.4U AD=500P AS=500P PD=100U PS=135U
MP2 6  2  50  50  MHCPEN W=135U  L= 2.4U AD=500P AS=500P PD=100U PS=135U
MN1 5  6  40  40  MHCNEN W=6.4U  L=18.8U AD= 25P AS= 25P PD= 20U PS=6.4U
MN2 6  5  40  40  MHCNEN W=6.4U  L=18.8U AD= 25P AS= 25P PD= 20U PS=6.4U
MP3 7  6  50  50  MHCPEN W= 10U  L= 4.0U AD= 40P AS= 40P PD= 20U PS= 10U
MN3 7  6  40  40  MHCNEN W=  5U  L= 4.0U AD= 20P AS= 20P PD= 10U PS=  5U
MP5 3  7  50  50  MHCPEN W= 30U  L= 2.4U AD=120P AS=120P PD= 40U PS= 30U
MN5 3  7  40  40  MHCNEN W= 15U  L= 2.4U AD= 60P AS= 60P PD= 20U PS= 15U
.ENDS HC_LLCN

*** Analog Switch ***
.SUBCKT HC_SWITCH1N  2  8  9  40  50
* INPUT = 2,   Y = 8,  Z = 9,  VEE = 40,  VCC =50
MP1 3  2  50  50  MHCPEN W=  88U L=2.4U AD= 350P AS= 350P PD=100U PS=  88U
MN1 3  2  40  40  MHCNEN W=  56U L=2.4U AD= 225P AS= 225P PD= 70U PS=  56U
MP4 4  3  50  50  MHCPEN W= 350U L=2.4U AD= 900P AS= 900P PD=200U PS= 350U
MN4 4  3  40  40  MHCNEN W= 150U L=2.4U AD= 400P AS= 400P PD=100U PS= 150U
MP5 8  4   5  50  MHCPEN W= 216U L=2.4U AD= 900P AS= 900P PD=100U PS= 216U
MN5 8  3   5   5  MHCNEN W= 108U L=2.4U AD= 430P AS= 430P PD= 50U PS= 108U
MN6 5  4  40  40  MHCNEN W= 145U L=2.4U AD= 600P AS= 600P PD= 75U PS= 145U
MP7 9  4   8  50  MHCPEN W=1068U L=2.4U AD=2500P AS=2500P PD= 10U PS=1068U M=1.955
MN7 9  3   8   5  MHCNEN W= 312U L=2.4U AD=1200P AS=1200P PD= 10U PS= 312U M=1.36
.ENDS HC_SWITCH1N

************************************************
*         NOMINAL N-Channel Transistor         *
*            UCB-3 Parameter Set               *
*         HIGH-SPEED CMOS Logic Family         *
*                10-Jan.-1995                  *
************************************************
.MODEL MHCNEN NMOS
+LEVEL = 3
+KP    = 45.3E-6
+VTO   = 0.72
+TOX   = 51.5E-9
+NSUB  = 2.8E15
+GAMMA = 0.94
+PHI   = 0.65
+VMAX  = 150E3
+RS    = 40
+RD    = 40
+XJ    = 0.11E-6
+LD    = 0.52E-6
+DELTA = 0.315
+THETA = 0.054
+ETA   = 0.025
+KAPPA = 0.0
+WD    = 0.0

***********************************************
*        NOMINAL P-Channel transistor         *
*           UCB-3 Parameter Set               *
*         HIGH-SPEED CMOS Logic Family        *
*               10-Jan.-1995                  *
***********************************************
.MODEL MHCPEN PMOS
+LEVEL = 3
+KP    = 22.1E-6
+VTO   = -0.71
+TOX   = 51.5E-9
+NSUB  = 3.3E16
+GAMMA = 0.92
+PHI   = 0.65
+VMAX  = 970E3
+RS    = 80
+RD    = 80
+XJ    = 0.63E-6
+LD    = 0.23E-6
+DELTA = 2.24
+THETA = 0.108
+ETA   = 0.322
+KAPPA = 0.0
+WD    = 0.0
