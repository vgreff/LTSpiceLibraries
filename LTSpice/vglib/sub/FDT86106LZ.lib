.SUBCKT FDT86106LZ 2 1 3
******************************************************************
**      Fairchild Discrete Modeling Group                       **
******************************************************************
**      Website         www.fairchildsemi.com\models            **
******************************************************************
**      (C) Copyright 2009 Fairchild Semiconductor Corporation  **
**                      All rights reserved                     **
**                                                              **
**                    FDT86106LZ Spice model                 	**
**                    Revision RevA, 27 Nov 2010                **
**         Revision RevA1, 23 Feb 2011  ESD diodes added        **
******************************************************************

*Nom Temp 25 deg C
Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
DESD1 91 9 DESD1MOD
DESD2 91 7 DESD2MOD
Lgate 1 9 2.714e-9
Ldrain 2 5 0.1e-9
Lsource 3 7 0.51e-9
RLgate 1 9 27.14
RLdrain 2 5 1
RLsource 3 7 5.1
Rgate 9 6 0.62
* Shielded  Gate
D_D1 100 5 D_SG_cap
D_D2 100 101 D_SG_cap
R_R1 101 7 0.54
C_C1 6 101 13e-12
.MODEL D_SG_cap D (IS=1e-9 n=1 RS=0.54e-3 CJO=0.23e-9 M=0.5 t_abs=25)
It 7 17 1
Ebreak 11 7 17 7 110.2
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=0.7e-3 TC2=-0.25e-6)
.MODEL Desd1 D (BV=10.2 TBV1=-2.9e-3 n=2 RS=35)
.MODEL Desd2 D (BV=10.5 TBV1=-2.5e-3 n=2.5 RS=35)
.MODEL DbodyMOD D (IS=3.5e-12 n=1.05 RS=42e-3 TRS1=2.5e-3 TRS2=1e-6
+ CJO=0.066e-9 M=0.38 TT=1e-9 XTI=1.75)
.MODEL DbreakMOD D (RS=8e-3 TRS1=1e-3 TRS2=1e-6 )
.MODEL DESD1MOD D (BV=12 TBV1=-2.9e-3 n=12 RS=35)
.MODEL DESD2MOD D (BV=12 TBV1=-2.5e-3 n=13 RS=35)
Rsource 7a 7 4.167e-3
Rdrain 5 16 RdrainMOD 70e-3
.MODEL RdrainMOD RES (TC1=6.2e-3 TC2=18e-6)
M_BSIM3 16 6 7a 7a Bsim3 W=0.368137142857143 L=1.226e-6 NRS=0 NRD=0
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
*Process Parameters
+ TOX=550e-10 ;Oxide thickness
+ XJ=0.62e-6 ;Channel depth
+ NCH=1.07e17 ;Channel concentration
*Channel Current
+ U0=900 VSAT=500000 DROUT=1.8
+ DELTA=0.5 PSCBE2=0 RSH=4.167e-3
*Threshold voltage
+ VTH0=1.85
*Sub-threshold characteristics
+ VOFF=-0.12 NFACTOR=1.3
*Junction diodes and Capacitance
+ LINT=0.141e-6 DLC=0.141e-6
+ CGSO=152e-12 CGSL=0 CGDO=1e-12 CGDL=190e-12
+ CJ=0 CF=0 CKAPPA=0.5
* Temperature parameters
+ KT1=-1.2 KT2=0 UA1=4.05e-9
+ NJ=10)
.ENDS FDT86106LZ

