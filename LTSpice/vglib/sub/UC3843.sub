.subckt uc3843  Err Vfb  Ifb Osc  Com  Out Vcc Ref
I1 Vcc COM TBL(0 0 1 .2m)
D1 COM Vcc 34V
A1 Vcc COM COM COM COM COM N004 COM SCHMITT Vt=8.2 Vh=0.3 Vhigh=2.5 Rout=1k
R1 Vcc N004 8e6
C1 N004 COM 1p
S1 N003 Vcc N004 2v5 ref
D2 N003 Vcc V65
C2 2v5 COM 1p Rpar=240
R2 2v5 N003 240
R3 Ref N003 280m
C3 COM Ref 10p
C4 Osc COM 5p Rpar=10meg
C5 Vfb COM 1p
R4 N006 Vfb 165k
C6 Err N006 1p Rpar=10G
A4 N006 2v5 COM COM COM COM Err COM OTA Vhigh=6 Vlow=0.7 Isrc=1m Isink=-6m Rclamp=1 g=5 Rout=25k Cout=5p powerup=!V(fault,Com)
C7 Ifb COM 5p
R5 N009 Ifb 75k
C8 N009 COM 1p
D3 Err N011 2R
D4 N011 COM 1R
C9 N011 COM 0.1p Rpar=1e6
A6 Ref COM COM COM COM fault COM COM SCHMITT Vt=4.8 Vh=.1 trise=200n
S2 Out Vcc N008 N007 Ho
S3 Com Out N007 N008 Lo
C10 Vcc COM 20p
C11 Out Com 20p
A2 N009 N011 COM COM COM COM N010 COM SCHMITT vt=1m vh=1m tripdt=3n trise=100n
A3 N005 N010 COM COM COM COM N008 COM SRFLOP td=10n trise=200n
A7 Osc COM COM COM COM COM N005 COM SCHMITT vt=1.75 vh=0.85 td=10n trise=100n
G2 Osc COM N005 COM 8.3m
A5 N005 0 0 0 fault 0 N007 0 OR trise=100n
.model 1R d(Vfwd=1 Ron=1)
.model 2R d(Vfwd=1.3 Ron=2e6)
.model 34V d(Ron=.5 Vfwd=.5 Vrev=34 Rrev=22)
.model ref sw(Vh=-11m Ilimit=100m)
.model Ho sw(Vt= .48 Vh=-.25 Ron=2 Roff=100k Vser=1.2)
.model Lo sw(Vt=-.52 Vh=-.25 Ron=6 Roff=300k)
.model V65 d(Vfwd=0.65)
.ends uc3843
