*Bordodynov
.subckt ULN2003LV B[1] B[2] B[3] B[4] B[5] B[6] B[7]  E
+                         COM C[7]  C[6] C[5] C[4] C[3] C[2] C[1]

M2[1] C[1] EB[1] E  ULN
M2[2] C[2] EB[2] E  ULN
M2[3] C[3] EB[3] E  ULN
M2[4] C[4] EB[4] E  ULN
M2[5] C[5] EB[5] E  ULN
M2[6] C[6] EB[6] E  ULN
M2[7] C[7] EB[7] E   ULN
 
R1[1]  EB[1] B[1] 3k
R1[2]  EB[2] B[2] 3k
R1[3]  EB[3] B[3] 3k
R1[4]  EB[4] B[4] 3k
R1[5]  EB[5] B[5] 3k
R1[6]  EB[6] B[6] 3k
R1[7]  EB[7] B[7] 3k
R2[1] B[1] E 300k
R2[2] B[2] E 300k
R2[3] B[3] E 300k
R2[4] B[4] E 300k
R2[5] B[5] E 300k
R2[6] B[6] E 300k
R2[7] B[7] E 300k

Din[1] E B[1] Din
Din[2] E B[2] Din
Din[3] E B[3] Din
Din[4] E B[4] Din
Din[5] E B[5] Din
Din[6] E B[6] Din
Din[7] E B[7] Din

D3[1] C[1] COM DSUB
D3[2] C[2] COM DSUB
D3[3] C[3] COM DSUB
D3[4] C[4] COM DSUB
D3[5] C[5] COM DSUB
D3[6] C[6] COM DSUB
D3[7] C[7] COM DSUB
.MODEL ULN  vdmos( vto=0.8 rds=100meg kp=0.40  rd=1.8 rs=0 rb=40m ksubthres=.1  lambda=40m cgdmin=1.5p cgdmax=5p cgs=7p tt=100n ibv=250u nbv=1  is=1.4e-13 cjo=9p bv=8.5 m=0.36 vj=0.8  )
.MODEL DSUB D( IS=600f N=1 RS=0.5  BV=8
+ IBV=50u  CJO=15p TT=100n )
.MODEL Din D( IS=600f N=1 RS=2  BV=8.5 
+ IBV=.001  CJO=5p TT=100n )
.ends ULN2003lv
