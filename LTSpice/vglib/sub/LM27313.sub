.SUBCKT LM27313  VIN SHDN_N GND SW  FB
bE_BStartup gnd BG i=IF(V(SHDN_N) > 1.5 & V(Vin) > 1.23 , 1.23 , 0) rpar=1
*Rref BG1 BG 5
Cref BG Gnd {72n*5}
V1 Ref5v 0 DC=5
VMAX_DUTY MAXDUTY 0 PULSE 5 0 0 .001U .001U 550E-9 625E-9
bEMAX_DUTY 7p 0 v=  IF(V(MAXDUTY) > 1 | V(7) > 1 , 5 , 0)
R2 SHDN_N Gnd 100meg
X1 Clk_ILIMIT 7p  Q QB SRLATCHSHP_BASIC_GEN
bE_B6 10 Gnd v=  IF(I(VRSENSE) > 1.25 , 5 , 0)
VRSENSE SW 12 0
R8 Vsense Gnd 50m
R101 10 101 1
C101 101 Gnd 1n
bE_ILIMIT_1 CUR_LIMIT_n1 0 v =  IF(V(101) > 2.5, 5 , 0)
RILIMIT CUR_LIMIT_n1 CUR_LIMIT_n1_d 10m
D_D1ILIMIT CUR_LIMIT_n1_d CUR_LIMIT_n2 D_D_Ilimit
CILIMIT CUR_LIMIT_n2 0 1n
SW2  CUR_LIMIT_n2 0 MAXDUTY 0  S_VSWITCH_2
.MODEL S_VSWITCH_2 VSWITCH (RON=500M ROFF=1G VON=400M VOFF=300M)
bE_buff CUR_LIMIT_n3 0 v=  IF(V(CUR_LIMIT_n2) > 1.25 , 5 , 0)
bE_CLK_ILIMIT Clk_ILIMIT 0 v=  IF(V(CUR_LIMIT_n3) > 1.25 , 0 , IF(V(Clk_Clk) > 1.25,5,0))
.MODEL D_D_Ilimit D( IS=1e-12 TT=10p Rs=0.005 N=.01 )
bE_BR 7 Gnd v=  IF(V(Compout) > 2.5 , 5 , IF( V(101) > 2.5 , 5 , 0))
X3 VSum verx Compoutp COMP_BASIC_GEN
Rcompout Compoutp Compoutp2 1
CCompout Compoutp2 0 1n
bECompout Compout 0 V=IF(V(Compoutp2)>2.5,5,0)
bG_Bg 0 Vc1 i= V(Vdiff)
Rm Vc1 0 1Meg
Cm Vc1 0 270p
C3 Vc1 3 1.75n
R5 3 0 1500k
Dclp Vc1 clp Dclamp
Vclp clp 0 0.9
.model Dclamp D (IS=1f N=0.1 TT=10p RS=0.05 cjo=1f)
E4 verx 0 Vc1 0 1
Dminlim 0 Vc1 _DMOD
.MODEL _DMOD D BV=3 IBV=1n N=0.01 cjo=1f
E3 19 0 BG FB 83.33333333333u
bEA1 Vdiff  0 V=LIMIT(V(19),-300u,300u)
bEsense vsence_scale 0 V=V(Vsense)*48m*250
X10 vsence_scale Ramp VSum SUM2
VCH Clk_Clk Gnd PULSE 0 5 0 .001U .001U 3.125E-9 6.25E-7
VRH Ramp Gnd PULSE 0 412500u 0 500N 30N 10N 625N
bE_BD 0 14pp i= IF(V(Q) > 2.5 & V(SHDN_N) > 1.5 , 5 , 0) Rpar=1
*R14p 14p 14pp 1
C14p 14pp 0 1n
SW1   12 VSENSE 14pp 0  S_VSWITCH_1
D_SW1 VSENSE 12 D_ideal
.MODEL D_Ideal D (IS=1f N=1m RS=1m cjo=1f)
.MODEL S_VSWITCH_1 VSWITCH (RON=500M ROFF=1G VON=400M VOFF=300M)
*$
.SUBCKT SUM2  1   2   3
B1 3 0 v= V(1) + V(2)
.ENDS
*$
.SUBCKT SRLATCHSHP_BASIC_GEN S R Q QB  
bGQ 0 Qint i= IF(V(S) > 2.5,5,IF(V(R)>2.5,-5, 0)) Rpar=1000MEG
CQint Qint 0 1n
*RQint Qint 0 1000MEG
D_D10 Qint MY5 D_D1
V1 MY5 0 5
D_D11 MYVSS Qint D_D1
V2 MYVSS 0 0.1
EQ Qqq 0 Qint 0 1
X3 Qqq Qqqd1 BUF_BASIC_GEN  
RQq Qqqd1 Q 1
BEQb Qbr 0 V = IF( V(Q) > 2.5, 0.1,5)
RQb Qbr QB 1
Cdummy1 Q 0 1n
Cdummy2 QB 0 1n
.IC V(Qint) 0.1
.MODEL D_D1 D( IS=1e-15 TT=10p Rs=0.05 N=.1 cjo=1f )
.ENDS SRLATCHSHP_BASIC_GEN
*$
.SUBCKT BUF_BASIC_GEN A  Y
B_ABMGATE   0 y i=IF(V(A) > 2.5 , 5,0.1) Rpar=1
C y 0  1n
.ENDS BUF_BASIC_GEN
*$
.SUBCKT COMP_BASIC_GEN INP INM Y 
B_ABM 0 Y i=IF (V(INP) > V(INM), 5,0) Rpar=1
C y 0  1n
.ENDS COMP_BASIC_GEN
.ENDS LM27313
