* TL783C voltage regulator 
* connections: input
* | adjustment pin
* | | output
* | | |
.SUBCKT TL783C IN ADJ OUT
*
* POSITIVE ADJUSTABLE VOLTAGE REGULATOR
JADJ IN ADJ ADJ JADJMOD
VREF 4 ADJ 1.27
DBK IN 13 DMOD
*
* ZERO OF RIPPLE REJECTION
CBC 13 15 8e-010
RBC 15 5 1000
QPASS 13 5 OUT QPASSMOD
RB1 7 6 1
RB2 6 5 85.21
* CURRENT LIMITING
DSC 6 11 DMOD
bESC 11 OUT V=1.96-0.01057*V(6,5)*V(13,5)
*
* FOLDBACK CURRENT
DFB 6 12 DMOD
BFB 12 OUT V=2.326-.03221*V(13,5)+0.0001421*V(13,5)*V(13,5)-0.02*V(13,5)*V(6,5)
EB 7 OUT 8 OUT 8.069
*
* ZERO OF OUTPUT IMPEDANCE
RP 9 8 100
CPZ 10 OUT 1.989u
DPU 10 OUT DMOD
RZ 8 10 0.1
EP 9 OUT 4 OUT 100
RI OUT 4 100MEG
.MODEL QPASSMOD NPN (IS=30F BF=50 VAF=94.64 NF=7.604)
.MODEL JADJMOD NJF (BETA=8.3e-005 VTO=-1)
.MODEL DMOD D (IS=30F N=7.604 cjo=10f)
.ENDS
