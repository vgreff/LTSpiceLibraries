*****************************************************************************
.SUBCKT GPI8HINOIC VDD VIN2 VIN1 D S 
XSUB1 VIN1  S  VIN1_OUT  Sub_source
XSUB2 VIN2  S  VIN2_OUT  Sub_source

XMain VDD VIN2_OUT VIN1_OUT D S    MAIN_GPI8HINOIC
.ends
*$

.SUBCKT MAIN_GPI8HINOIC N001 N006 N007 N004 N010 

A1 N006 N007 0 0 0 N008 N002 0 SRFLOP
XU3 N003 N001 N005 EHw4k
XU4 N009 N005 N010 EHw4k
XU5 N005 N004 N010 GPI6507dot5TO
D1 N010 N005 D
XU2 N002 N001  N010 N003  Multiple
XU1 N008 N001  N010 N009  Multiple

.model D D

.ENDS 

*****************************************************************
*
.subckt GPI6507dot5TO gatein drainin sourcein
*
*
*****************************************************************



*****************************************************************
* DO NOT ALTER THE FOLOWING INTERNAL PARAMETERS
*****************************************************************

  .param ai_fac=0.5  cap_fac=0.5  res_fac=2.

*
*
rd drainin drain {0.05*res_fac} tc=0.0135
rs sourcein source {0.02*res_fac} tc=0.0135
rg gatein gate {0.38*res_fac}
*
*
bswitch drain source I = (if (v(drain,source)>0,
           +	(ai_fac*1.6*(0.8*PWR((Temp-25+273)/300,-2.7))*log(1.0+exp(13*(v(gate,source)-1.7-0.001*(Temp-25))))*
+	v(drain,source)/(1 + max(0.5+0.2*(v(gate,source)+4.1),0.2)*v(drain,source))),
           +	(-ai_fac*1.6*(0.8*PWR((Temp-25+273)/300,-2.7))*log(1.0+exp(10.5*(v(gate,drain)-1.7-0.001*(Temp-25))))*
+	v(source,drain)/(1 + max(0.5+0.2*(v(gate,drain)+6.1),0.2)*v(source,drain)))) )
*
*

           C_SG   source  gate  Q=1e-12*x*cap_fac*118.0

           C_GD   gate drain    Q=1e-12*x*cap_fac*
           +      (5.0+40.0/(1+PWR(x/50.,2)))
* in case of convergence difficulty, try to use a constant of 20pF at 100V

           C_SD   source drain  Q=1e-12*x*cap_fac*
           +      (25.0+225/(1+PWR(x/65.,2))- 40/(1+PWR(x/50.,2)))
* in case of convergence difficulty, try a constant of 70pF at 100V

.ends
*$

*****************************************************************
*
.subckt EHw4k gatein drainin sourcein
*
*****************************************************************
*	USER DEFINE max_curr



*****************************************************************
* DO NOT ALTER THE FOLOWING INTERNAL PARAMETERS
.param hemt_w=4000
*****************************************************************

.param ai_fac=hemt_w*5.49e-6 cap_fac=ai_fac  vds_fac=6.733 res_fac=2.4/(ai_fac*vds_fac)


.param K1_cur={1.6*ai_fac*1.6}    
*
*
rd drainin drain {0.05*res_fac} tc=0.0135
rs sourcein source {0.02*res_fac} tc=0.0135
rg gatein gate {0.38*res_fac}
*
*
bswitch drain source I = (if (v(drain,source)>0,
+	(K1_cur*(0.8*PWR((Temp-25+273)/300,-2.7))*log(1.0+exp(26*(v(gate,source)-7.9+6.2-0.00*(Temp-25))/2))*
           +	vds_fac*v(drain,source)/(1 + max(0.5+0.2*(v(gate,source)+4.1),0.2)*vds_fac*v(drain,source))),
+	(-K1_cur*(0.8*PWR((Temp-25+273)/300,-2.7))*log(1.0+exp(21*(v(gate,drain)-7.9+6.2-0.00*(Temp-25))/2))*
           +	vds_fac*v(source,drain)/(1 + max(0.5+0.2*(v(gate,drain)+6.1),0.2)*1.0*vds_fac*v(source,drain)))) )
*


           C_SG   source  gate  Q=1e-12*x*cap_fac*101.0
           C_GD   gate drain    Q=1e-12*x*cap_fac*33.4
           C_SD   source drain  Q=1e-12*x*cap_fac*41.6


.ends
*$
*
.subckt Multiple Vin Vdd Vss Out
*

EOUT  Out  Vss VALUE {(V(Vdd)-V(Vss))*(V(Vin))}
.ends
*$

*
.subckt Sub_source Vin Vss Out
*

EOUT  Out  0  VALUE {V(Vin)-V(Vss)}
.ends
*$

